---
title: "Edad con Salud -- cohort 2019 -- Wave 1 -- 'DEPRESION ICD-10'" 
output:
  word_document:
    reference_docx: !expr ecs.data::get_pkg_docx_template()
csl: "`r ecs.data::get_proj_csl_file()`"
editor_options: 
  markdown: 
    wrap: 72
---

```{r setup, include=FALSE}
# Activar los paquetes que R necesita para ejecutar el código:

library(Statamarkdown)

# Ruta al ejecutable de Stata (Ejecuta `sysdir` en Stata para encontrar la ruta)

# Opciones de configuración de las salidas de R y Stata:

knitr::opts_chunk$set( # Para todos los 'chunks' de código:
  echo        = FALSE, #   - No muestra la sintaxis en el documento
  results     = 'hide',#   - Omite los resultados de ejecutar la sintaxis
  cleanlog    = TRUE,  #   - Omite repetir sintaxis en resultados de Stata
  collectcode = TRUE   #   - Reutiliza el código de Stata de 'chunks' anteriores
)
```

# Variable information

+---------------------------+-----------------------------------------------------------------------------+------------+---------------------------------+
| **Main variable name**    | **Label**                                                                   | **Format** | **Code**                        |
+:==========================+:============================================================================+============+=================================+
| depression_lifetime       | Lifetime depression (c2019_w1)                                              | Binary     | 0=no;1=yes                      |
+---------------------------+-----------------------------------------------------------------------------+------------+---------------------------------+
| d_lifetime_severity       | Severity of lifetime depressive episode (c2019_w1)                          | Ordinal    | 0=No;1=Mild;2=Moderate;3=Severe |
+---------------------------+-----------------------------------------------------------------------------+------------+---------------------------------+
| depression_12m            | 12 month depression (c2019_w1)                                              | Binary     | 0=no;1=yes                      |
+---------------------------+-----------------------------------------------------------------------------+------------+---------------------------------+
| depression_symptoms12m    | 12 month depression from lifetime diagnosis + q4227/q4227a items (c2019_w1) | Binary     | 0=no;1=yes                      |
+---------------------------+-----------------------------------------------------------------------------+------------+---------------------------------+
| d_12m_item                | 12 month depression one item (c2019_w1)                                     | Binary     | 0=no;1=yes                      |
+---------------------------+-----------------------------------------------------------------------------+------------+---------------------------------+
| d_12m_comparable          | 12 month depression comparable with COVID substudy (c2019_w1)               | 0=no;1=yes | 0=no;1=yes                      |
+---------------------------+-----------------------------------------------------------------------------+------------+---------------------------------+
| depression_symptoms12m    | 12 month depression symptomatology (c2019_w1)                               | Binary     | 0=no;1=yes                      |
+---------------------------+-----------------------------------------------------------------------------+------------+---------------------------------+
| d_12m_severity            | Severity of 12 month depressive episode (c2019_w1)                          | Ordinal    | 0=No;1=Mild;2=Moderate;3=Severe |
+---------------------------+-----------------------------------------------------------------------------+------------+---------------------------------+
| d_12m_severity_comparable | Severity of 12 month depressive episode comparable (c2019_w1)               | Ordinal    | 0=No;1=Mild;2=Moderate;3=Severe |
+---------------------------+-----------------------------------------------------------------------------+------------+---------------------------------+
| d_symptoms12m_comparable  | 12 month depression comparable with COVID substudy (c2019_w1)               | Binary     | 0=no;1=yes                      |
+---------------------------+-----------------------------------------------------------------------------+------------+---------------------------------+

# Variable description

Depression has become a major public health issue as it is one of the main causes of disability worldwide, with an even greater impact on developed countries. According to The ICD-10 Classification of Mental and Behavioural Disorders, Diagnostic criteria for research (World Health Organization, 1993), the main characteristics of depression are the presence of a sustained depressed mood, the loss of interest or pleasure in activities normally pleasurable and/or the decreased energy or increased fatigability. In addition to these main criteria, the ICD-10 Diagnostic criteria for research proposes several contributing factors that can be present or absent: loss of confidence and self-esteem, feelings of self-reproach or guilt, recurrent thoughts of death or suicide, diminished ability to think or concentrate, change in psychomotor activity, sleep disturbance, and changes in appetite with the corresponding weight change. Moreover, the classification proposes a three-level gradient of severity depending on the amount of criteria present in each case, from mild to severe. In order to address these criteria and be able to assess the lifetime presence or absence of a depressive episode, as well as its severity, we used an adapted version of the Composite International Diagnostic Interview (CIDI 3.0), a comprehensive, fully-structured interview designed to be used by trained lay interviewers for the assessment of mental disorders according to the definitions and criteria of ICD-10. It is intended for use in epidemiological and cross-cultural studies, as well as for clinical and research purposes.

# Explanation of how the variables were created

Information from the CIDI depression module was used for the creation of the depression variables. Algorithms based on the ICD-10 were used for the endorsement of depressive episode at some point in their lives (lifetime prevalence; variable depression_lifetime) and in the previous 12 months (point prevalence; variable depression_12m). More precisely, the following criteria should be fulfilled: i) The depressive episode should last for at least 2 weeks; ii) There have been no hypomanic or manic symptoms sufficient to meet the criteria for hypomanic or manic episode at any time in the individual's life; iii) The episode is not attributable to psychoactive substance use or to any organic mental disorder; iv) The general criteria for depressive episode must be met; v) At least two of the following three symptoms must be present: Depressed mood, loss of interest and decreased energy; vi) an additional symptom or symptoms should be present, to give a total of at least four (please see criteria C1-C7). We also included a severity variable according to the ICD-10 criteria for both lifetime prevalence (variable d_lifetime_severity) and 12 months prevalence (variable d_12m_severity), based upon the number of symptoms the participant met. An additional variable on 12 month depression symptomatology (variable depression_symptoms12m) was also created, where only v and vi were considered.

## Criteria for the changes

- Take into account the *strict* skip logic (i.e. filters) in the questionnaire

- Consider the tri-state logic necessary for the diagnoses (i.e. missing values)

- Always generate the new variables as _missing_ first, then compute the
  non-missing values, for algorithmic consistency
  
- Always _negative_ diagnosis / symptom first, then _positive_, for algorithmic
  consistency (unless indicated otherwise)
  
- Check that the _total number of positive/negative cases_ is coincident with
  the number generated, so that the order of computing positive/negative cases
  is indifferent
  
- The "# SAME RESULT" comment has been added when _the resulting values are_
  _coincident for all the cases_, between the current version and this one.
  This does not mean necessarily that the resulting computations are equivalent
  (i.e., there could be combinations of values that give different results,
  but do not appear in the C2019W1 dataset)

# Syntax used to create the variable (Stata 13)

```{stata load-data}

* Cohorte 2019, linea base:
use "~\UAM\marta.miret@uam.es - Bases de datos maestras Edad con Salud\Ola_3\Cohorte_2019\rawdata_c2019w1.dta"

```

```{stata generate-outcomes, echo=TRUE}

*Previous recoding of the symptomatology questions
recode q4220a-q4222v (1=1) (2=0) (888 999=.) 

***Depressive episode (F32)

*G1 criterion: "The depressive episode should last for at least 2 weeks"
* The criterion is _negative_ when both q4211 and q4213 are "NO" (=2);
* *HOWEVER*, the interview skips one item or the other, so both items are
* *NEVER* going to be equal to 2, only one of them can be
* (the other will be missing). Therefore, the criterion is negative when one
* of the items is 2 and positive when one of the items is 1
* (and missing otherwise)
gen d_crit_g1 = .
replace d_crit_g1 = 0 if q4211==2 | q4213==2
replace d_crit_g1 = 1 if q4211==1 | q4213==1
label variable d_crit_g1 "G1 criterion for depression"

*G2 criterion: "There have been no hypomanic or manic symptoms sufficient to meet the criteria for hypomanic or manic episode at any time in the individual's life"
* See explanation in "criterion G1" (lines 80-85)
gen d_crit_g2 = .
replace d_crit_g2 = 0 if q4233==1 | q4225_causemed2==1
replace d_crit_g2 = 1 if q4233==2 | q4225_causemed2==2
label variable d_crit_g2 "G2 criterion for depression"

*G3 criterion: "The episode is not attributable to psychoactive substance use or to any organic mental disorder"
* The criterion is _positive_ when at least some episode cannot be attributable
* to physical causes (either none of the episodes can be attributable to
* physical causes, i.e. q224a is "No", or the episodes were not *ALWAYS*
* attributable to physical causes, i.e. q224b is "No").
* When a physician was consulted, first it is assumed that the causes were
* _non-physical_ (for simplicity, as these contemplates negative AND missing
* responses) and thus the criterion is fulfilled, and then the cases where the
* physical causes were attributed are discarded.
gen d_crit_g3 = .
replace d_crit_g3 = 1 if (q4224a==2 | q4224b==2) | q4224==1
replace d_crit_g3 = 0 if (q4224a==1 & q4224b==1) | (q4224==1 & (q4225_causemed12==1 | q4225_causemed23==1 | q4225_causemed24==1 | q4225_causemed25==1 | q4225_causemed30==1 | q4225_causemed31==1))
label variable d_crit_g3 "G3 criterion for depression" 

***Mild depressive episode (F32.0)

*A criterion: "The general criteria for depressive episode must be met"
* The criterion is _positive_ when all the three G1-G3 criteria are met.
* When *ANY* of them is known to be unmet (= 0), then the general criteria is
* known NOT to be met, as the value of the other two don´t matter.
gen d_crit_a = .
replace d_crit_a = 0 if d_crit_g1==0 | d_crit_g2==0 | d_crit_g3==0
replace d_crit_a = 1 if d_crit_g1==1 & d_crit_g2==1 & d_crit_g3==1
label variable d_crit_a "A criterion for depression"

*B criterion: "At least two of the following three symptoms must be present"

*B1: "Depressed mood to a degree that is definitely abnormal for the individual, present for most of the day and almost every day, largely uninfluenced by circumstances, and sustained for at least 2 weeks"
* The rationale here is the following: When ANY of the responses is
* positive (= 1) the symptom is _positive_. When there is a negative AND a
* missing response (in the first two items), the symptom should be missing,
* according to this criterion. *HOWEVER*, according to the skip logic in the
* questionnaire, it seems that a negative response in the first item skips the
* second one (presumably, assuming also a negative response), so this pattern
* should be understood as a _negative symptom_. Something similar happens when
* the first response is missing (DK or Refuse) and the second one is negative.
* Thus, we assume that ANY negative response in one of these two items implies
* a negative symptom (in principle); the same happens with the other two items
* (q4220c and q4220d). Thus, to have a _negative symptom_,
* both *ONE OF THE FIRST TWO ITEMS AND ONE OF THE LAST TWO ONES* must
* have a negative response.
gen d_b1 = .
replace d_b1 = 0 if (q4220a==0 | q4220b==0) & (q4220c==0 | q4220d==0)
replace d_b1 = 1 if q4220a==1 | q4220b==1 | q4220c==1 | q4220d==1
label variable d_b1 "B1 criterion for depression"
* # SAME RESULT

*B2: "Loss of interest or pleasure in activities that are normally pleasurable"
gen d_b2 = .
replace d_b2 = 0 if q4220e==0 & q4220f==0
replace d_b2 = 1 if q4220e==1 | q4220f==1
label variable d_b2 "B2 criterion for depression"
* # SAME RESULT

*B3: "Decreased energy or increased fatigability"
gen d_b3 = .
replace d_b3 = 0 if q4222e==0
replace d_b3 = 1 if q4222e==1
label variable d_b3 "B3 criterion for depression"
* # SAME RESULT

*Sum of depressive screening symptoms
* The ", missing" option must be appended, to prevent that the sum of symptoms
* is computed when all the values are missing. Also, it is crucial to know
* whether there are enough missing values to prevent computing whether the total
* sum of screening symptoms is more than 1 or less than 2.
egen d_screentotal = rowtotal (d_b1-d_b3), missing
egen d_screenmissing = rowmiss (d_b1-d_b3)
label variable d_screentotal "Screening symptoms for depression"

*To fulfil B criterion >=2 screening symptoms should be present.
* To make sure the criterion is NOT met, there must be less than two positive
* AND missing symptoms. To make sure the criterion IS met, there must be
* _at least_ two symptoms. In any other case, this criterion is _unknown_.
gen d_crit_b = .
replace d_crit_b = 0 if (d_screentotal + d_screenmissing) < 2
replace d_crit_b = 1 if d_screentotal >= 2 & d_screentotal != .
label variable d_crit_b "B criterion for depression"

*C criterion: "An additional symptom or symptoms from the following list should be present, to give a total of at least four"

*C1: "Loss of confidence and self-esteem"
* When ANY of the responses is positive (= 1) the symptom is _positive_.
* When the second is _negative_, the third one is skipped so only
* the first two ones have to be considered for a _negative_ symptom.
gen d_c1 = .
replace d_c1 = 0 if q4222k==0 & q4222l==0
replace d_c1 = 1 if q4222k==1 | q4222l==1 | q4222m==1
label variable d_c1 "C1 criterion for depression"

*C2: "Unreasonable feelings of self-reproach or excessive and inappropriate guilt"
gen d_c2 = .
replace d_c2 = 0 if q4222n==0
replace d_c2 = 1 if q4222n==1
label variable d_c2 "C2 criterion for depression"
* # SAME RESULT

*C3: "Recurrent thoughts of death or suicide, or any suicidal behaviour"
* When ANY of the responses is positive (= 1) the symptom is _positive_.
* When the third is _negative_, the fourth and fifth ones are skipped so only
* the first three ones have to be considered for a _negative_ symptom.
* *NOTE*: Consider whether _ONLY_ `q4222r` (and not `q4222s`) indicates
* _recurrent_ thoughts of death?
gen d_c3 = .
replace d_c3 = 0 if q4222r==0 & q4222s==0 & q4222t==0
replace d_c3 = 1 if q4222r==1 | q4222s==1 | q4222t==1 | q4222u==1 | q4222v==1
label variable d_c3 "C3 criterion for depression"

*C4: "Complaints or evidence of diminished ability to think or concentrate, such as indecisiveness or vacillation"
gen d_c4 = .
replace d_c4 = 0 if q4222h==0 & q4222i==0 & q4222j==0
replace d_c4 = 1 if q4222h==1 | q4222i==1 | q4222j==1
label variable d_c4 "C4 criterion for depression"

*C5: "Change in psychomotor activity, with agitation or retardation (either subjective or objective)"
gen d_c5 = .
replace d_c5 = 0 if q4222f==0 & q4222fa==0 & q4222g==0 & q4222ga==0
replace d_c5 = 1 if (q4222f==1 | q4222fa==1) | (q4222g==1 | q4222ga==1)
label variable d_c5 "C5 criterion for depression"

*C6: "Sleep disturbance of any type"
gen d_c6 = .
replace d_c6 = 0 if q4222c==0 & q4222d==0
replace d_c6 = 1 if q4222c==1 | q4222d==1
label variable d_c6 "C6 criterion for depression"

*C7: "Change in appetite (decrease or increase) with corresponding weight change"
gen d_c7 = .
replace d_c7 = 0 if q4222a==0 | q4222b==0
replace d_c7 = 1 if q4222a==1 & q4222b==1
label variable d_c7 "C7 criterion for depression"

*Sum of depressive symptoms
* The ", missing" option must be appended, to prevent that the sum of symptoms
* is computed when all the values are missing. Also, it is crucial to know
* whether there are enough missing values to prevent computing whether the total
* sum of screening symptoms is at least 1 or less than 1.
egen d_symtotal = rowtotal (d_c1-d_c7), missing
egen d_symmissing = rowmiss (d_b1-d_b3)
label variable d_symtotal "Symptoms for depression"
label variable d_symmissing "Missing symptoms for depression"

*Has the person skipped the lifetime depression module?
* The criterion was stated as "all the responses in the lifetime depression
* module are missing". However, this criterion is not strictly reliable as it is
* _not impossible_ that someone does not skip the module but decides to omit
* _all the responses_. The only input to the "lifetime depression" module is
* from item Q4218 (there are no filters that redirect to any Q4220* or Q4222*
* item), and the response in this item must be _positive_ (=1) to enter the
* module; otherwise the module is skipped.
* Also, the whole "depression" module can be skipped if any of the following
* items has a response different from _positive_ (=1): Q4202, Q4211, Q4213, or
* filter F4217 (which implies Q4214==1 AND Q4215==4 AND Q4216==4).
* _NOTE_: This criterion considers the module "skipped" for the participants
* that have been prompted with at least one of these items. If they haven´t,
* their value will be missing (as they have skipped the whole CIDI section
* altogether, e.g. proxy interviews).
gen d_skipped = .
replace d_skipped = q4202!=1 if q4202!=.
replace d_skipped = q4211!=1 if q4211!=.
replace d_skipped = q4213!=1 if q4213!=.
replace d_skipped = q4218!=1 if q4218!=.
replace d_skipped = q4214==1 & q4215==4 & q4216==4 if q4214!=. & q4215!=. & q4216!=.
* # NOTE: This variable is NEVER used in the "lifetime depression" algorithm (but is used in other(s))

***Severity of depressive episode

*To meet the criteria for "Mild depressive episode (F32.0)", at least two of the screening symptoms (B1-B3) must be present, and an additional symptom or symptoms from the rest of them (B1-C7) should be present to give a total of at least four. In the case of "Moderate depressive episode (F32.1)", there must be at least two screening symptoms and a total of at least six symptoms. In the case of "Severe depressive episode (F32.3)", the three screening symptoms (B1-B3) must be present, and additional symptoms (C1-C7) should be present to give a total of at least eight.
* In this version, we include the possibility that there are enough missing
* values to prevent computing whether the total sum of symptoms (B + C criteria)
* is equal to 3 or higher, or lower than 3. This happens when the total number
* of (symptoms + missing) is equal to or higher than 3 (and no other criterion
* is met). Therefore, the "missings" are first computed, and then the "severity"
* is computed, in increasing order, such that each severity level overrides the
* previous one.
* Then, to compute the final level of severity, this initial "severity" is
* combined with the B criterion (to obtain the "mild" or "moderate" severity),
* or with the criterion of having the three B screening symptoms (for the
* "severe" level). These are also computed in increasing order, so that each
* level overrides the previous one.
gen d_lifetime_severity_total = .
replace d_lifetime_severity_total = 0 if d_screentotal + d_screenmissing + d_symtotal + d_symmissing < 3
replace d_lifetime_severity_total = 1 if d_screentotal + d_symtotal >= 3
replace d_lifetime_severity_total = 2 if d_screentotal + d_symtotal >= 6
replace d_lifetime_severity_total = 3 if d_screentotal + d_symtotal >= 8

gen d_lifetime_severity = .
replace d_lifetime_severity = 0 if d_crit_a==0 | d_crit_b==0 | d_lifetime_severity_total==0
replace d_lifetime_severity = 1 if d_crit_a==1 & d_crit_b==1 & d_lifetime_severity_total>=1
replace d_lifetime_severity = 2 if d_crit_a==1 & d_crit_b==1 & d_lifetime_severity_total>=2
replace d_lifetime_severity = 3 if d_crit_a==1 & d_screentotal==3 & d_lifetime_severity_total==3
label variable d_lifetime_severity "Severity of lifetime depressive episode (c2019_w1)"
label define severity 0 "No" 1 "Mild" 2 "Moderate" 3 "Severe"
label values d_lifetime_severity severity
* # TWO ORIGINALLY "0" ARE NOW "1" (Mild)!!

*CIDI initial screening test
* This part sorts out the respondents that have given three negative responses
* to the initial screening test (items Q4200-Q4202). Note that when DK/Refuse
* responses have been given, although the rest of the CIDI module is omitted,
* a positive diagnostic of depression cannot be discarded, because it is unknown
* whether the screening is positive (at least one response is positive) and thus
* depression must be assessed, or not.
gen d_screening=.
replace d_screening=0 if q4200==2 & q4201==2 & q4202==2
replace d_screening=1 if q4200==1 | q4201==1 | q4202==1

***Lifetime depression diagnostic
gen depression_lifetime = .
replace depression_lifetime = 0 if d_lifetime_severity ==0 | d_screening==0
replace depression_lifetime = 1 if d_lifetime_severity >=1 & d_lifetime_severity !=. & d_screening==1
label define yesno 0 "No" 1 "Yes"
label values depression_lifetime yesno
label variable depression_lifetime "Lifetime depression (c2019_w1)"


*** Lifetime depression prevalence for proxy participants
recode P4040 (1=1) (2=0) (888=.) (999=.), gen (depression_lifetime_proxy)
label values depression_lifetime_proxy yesno
label variable depression_lifetime_proxy "Lifetime depression for proxy (c2019_w1)"

*** 12 month depression

* 12 month depression from lifetime diagnosis + q4227/q4227a items

gen depression_12m = .
replace depression_12m = 0 if depression_lifetime==0 | (depression_lifetime==1 & (q4227!=1 | q4227a!=1))
replace depression_12m = 1 if depression_lifetime==1 & (q4227==1 | q4227a==1)
label variable depression_12m "12 month depression from lifetime diagnosis + q4227/q4227a items (c2019_w1)"
label values depression_12m yesno

* 12 month depression from q4227/q4227a items

gen d_12m_item = .
replace d_12m_item = 1 if q4227==1 | q4227a==1
replace d_12m_item = 0 if proxy==2 & (q4227!=1 | q4227a!=1)
label values d_12m_item yesno
label variable d_12m_item "12 month depression one item (c2019_w1)"

* 12 months depression comparable with COVID substudy from lifetime diagnosis

gen d_lifetime_severity_comparable = .
replace d_lifetime_severity_comparable = 0 if d_crit_g1==0 | d_crit_b==0 | (d_screentotal==2 & d_symtotal<2) | (d_screentotal==3 & d_symtotal==0)
replace d_lifetime_severity_comparable = 1 if d_crit_g1==1 & ((d_screentotal==2 & d_symtotal>=2) | (d_screentotal==3 & d_symtotal>=1))
replace d_lifetime_severity_comparable = 2 if d_crit_g1==1 & ((d_screentotal==2 & d_symtotal>=4) | (d_screentotal==3 & d_symtotal>=3))
replace d_lifetime_severity_comparable = 3 if d_crit_g1==1 & (d_screentotal==3 & d_symtotal>=5)
label variable d_lifetime_severity_comparable "Severity of lifetime depressive episode comparable (c2019_w1)"
label values d_lifetime_severity_comparable severity

gen d_12m_comparable = .
replace d_12m_comparable = 1 if d_lifetime_severity_comparable>=1 & (q4227==1 | q4227a==1)
replace d_12m_comparable = 0 if proxy==2 & (d_lifetime_severity_comparable ==0 | d_skipped==1) | (d_lifetime_severity_comparable>=1 & (q4227!=1 | q4227a!=1))
label variable d_12m_comparable "12 month depression comparable with COVID substudy (c2019_w1)"
label values d_12m_comparable yesno

***Symptoms recent depression (previous 12 months)
*Previous recoding of the symptomatology questions
recode q4300a-q4302v (1=1) (888 999=.) (2=0)

***Depressive episode (F32)

*G1 criterion: "The depressive episode should last for at least 2 weeks"
gen d_crit_g1_12m = .
replace d_crit_g1_12m = 0 if q4211!=1 | q4213!=1
replace d_crit_g1_12m = 1 if q4211==1 | q4213==1
label variable d_crit_g1_12m "G1 criterion for depression 12m"

*G2 criterion: "There have been no hypomanic or manic symptoms sufficient to meet the criteria for hypomanic or manic episode at any time in the individual's life"
gen d_crit_g2_12m = 1
replace d_crit_g2_12m = 0 if q4233==1 | q4702_causemed2==1
label variable d_crit_g2_12m "G2 criterion for depression 12m"

*G3 criterion: "The episode is not attributable to psychoactive substance use or to any organic mental disorder"
gen d_crit_g3_12m = 0
replace d_crit_g3_12m = 1 if (q4701a!=1 | q4701b!=1) | (q4701==1 & q4702_causemed12!=1 & q4702_causemed23!=1 & q4702_causemed24!=1 & q4702_causemed25!=1 & q4702_causemed30!=1 & q4702_causemed31!=1)
replace d_crit_g3_12m = . if q4701a==. & q4701b==. & q4701==. & q4702_causemed12==. & q4702_causemed23==. & q4702_causemed24==. & q4702_causemed25==. & q4702_causemed30==. & q4702_causemed31==.
label variable d_crit_g3_12m "G3 criterion for depression 12m"

***Mild depressive episode (F32.0)

*A criterion: "The general criteria for depressive episode must be met"
gen d_crit_a_12m = .
replace d_crit_a_12m = 0 if d_crit_g1_12m!=1 | d_crit_g2_12m!=1 | d_crit_g3_12m!=1
replace d_crit_a_12m = 1 if d_crit_g1_12m==1 & d_crit_g2_12m==1 & d_crit_g3_12m==1
label variable d_crit_a_12m "A criterion for depression 12m"

*B criterion: "At least two of the following three symptoms must be present"

*B1: "Depressed mood to a degree that is definitely abnormal for the individual, present for most of the day and almost every day, largely uninfluenced by circumstances, and sustained for at least 2 weeks".
gen d_b1_12m = 0
replace d_b1_12m = 1 if q4300a==1 | q4300b==1 | q4300c==1 | q4300d==1
replace d_b1_12m = . if q4300a==. & q4300b==. & q4300c==. & q4300d==.
label variable d_b1_12m "B1 criterion for depression 12m"

*B2: "Loss of interest or pleasure in activities that are normally pleasurable"
gen d_b2_12m = 0
replace d_b2_12m = 1 if q4300e==1 | q4300f==1
replace d_b2_12m = . if q4300e==. & q4300f==.
label variable d_b2_12m "B2 criterion for depression 12m"

*B3: "Decreased energy or increased fatigability"
gen d_b3_12m = 0
replace d_b3_12m = 1 if q4302e==1
replace d_b3_12m = . if q4302e==.
label variable d_b3_12m "B3 criterion for depression 12m"

*Sum of depressive screening symptoms
egen d_screentotal_12m = rowtotal (d_b1_12m-d_b3_12m)
label variable d_screentotal_12m "Screening symptoms for depression 12m"

*To fulfil B criterion >=2 screening symptoms should be present.
gen d_crit_b_12m = .
replace d_crit_b_12m = 0 if d_screentotal_12m<2
replace d_crit_b_12m = 1 if d_screentotal_12m>=2
label variable d_crit_b_12m "B criterion for depression 12m"

*C criterion: "An additional symptom or symptoms from the following list should be present, to give a total of at least four"

*C1: "Loss of confidence and self-esteem"
gen d_c1_12m = 0
replace d_c1_12m = 1 if q4302k==1 | q4302l==1 | q4302m==1
replace d_c1_12m = . if q4302k==. & q4302l==. & q4302m==.
label variable d_c1_12m "C1 criterion for depression 12m"

*C2: "Unreasonable feelings of self-reproach or excessive and inappropriate guilt"
gen d_c2_12m = 0
replace d_c2_12m = 1 if q4302n==1
replace d_c2_12m = . if q4302n==.
label variable d_c2_12m "C2 criterion for depression 12m"

*C3: "Recurrent thoughts of death or suicide, or any suicidal behaviour"
gen d_c3_12m = 0
replace d_c3_12m = 1 if q4302r==1 | q4302s==1 | q4302t==1 | q4302u==1 | q4302v==1
replace d_c3_12m = 1 if q4302r==. & q4302s==. & q4302t==. & q4302u==. & q4302v==.
label variable d_c3_12m "C3 criterion for depression 12m"

*C4: "Complaints or evidence of diminished ability to think or concentrate, such as indecisiveness or vacillation"
gen d_c4_12m = 0
replace d_c4_12m = 1 if q4302h==1 | q4302i==1 | q4302j==1
replace d_c4_12m = . if q4302h==. & q4302i==. & q4302j==.
label variable d_c4_12m "C4 criterion for depression 12m"

*C5: "Change in psychomotor activity, with agitation or retardation (either subjective or objective)"
gen d_c5_12m = 0
replace d_c5_12m = 1 if (q4302f==1 | q4302fa==1) | (q4302g==1 | q4302ga==1)
replace d_c5_12m = . if q4302f==. & q4302fa==. & q4302g==. & q4302ga==.
label variable d_c5_12m "C5 criterion for depression 12m"

*C6: "Sleep disturbance of any type"
gen d_c6_12m = 0
replace d_c6_12m = 1 if q4302c==1 | q4302d==1
replace d_c6_12m = . if q4302c==. & q4302d==.
label variable d_c6_12m "C6 criterion for depression 12m"

*C7: "Change in appetite (decrease or increase) with corresponding weight change"
gen d_c7_12m = 0
replace d_c7_12m = 1 if q4302a==1 & q4302b==1
replace d_c7_12m = . if q4302a==. & q4302b==.
label variable d_c7_12m "C7 criterion for depression 12m"

*Sum of depressive symptoms
egen d_symtotal_12m = rowtotal (d_c1_12m-d_c7_12m)
label variable d_symtotal_12m "Symptoms for depression 12m"

*Have the person skipped the recent depression module?
egen serie_symp_12m = rowtotal (q4300a-q4302v)
gen d_skipped_12m = 0
replace d_skipped_12m = 1 if (q4300a!=1 & q4300b!=1 & q4300c!=1 & q4300d!=1 & q4300e!=1 & q4300f!=1) | serie_symp_12m<2

***Severity of depressive episode

*To meet the criteria for "Mild depressive episode (F32.0)", at least two of the screening symptoms (B1-B3) must be present, and an additional symptom or symptoms from the rest of them (B1-C7) should be present to give a total of at least four. In the case of "Moderate depressive episode (F32.1)", there must be at least two screening symptoms and a total of at least six symptoms. In the case of "Severe depressive episode (F32.3)", the three screening symptoms (B1-B3) must be present, and additional symptoms (C1-C7) should be present to give a total of at least eight.
gen d_12m_severity = .
replace d_12m_severity = 0 if d_crit_a_12m==0 | d_crit_b_12m==0 | (d_screentotal_12m==2 & d_symtotal_12m<2) | (d_screentotal_12m==3 & d_symtotal_12m==0)
replace d_12m_severity = 1 if d_crit_a_12m==1 & ((d_screentotal_12m==2 & d_symtotal_12m>=2) | (d_screentotal_12m==3 & d_symtotal_12m>=1))
replace d_12m_severity = 2 if d_crit_a_12m==1 & ((d_screentotal_12m==2 & d_symtotal_12m>=4) | (d_screentotal_12m==3 & d_symtotal_12m>=3))
replace d_12m_severity = 3 if d_crit_a_12m==1 & (d_screentotal_12m==3 & d_symtotal_12m>=5)
label variable d_12m_severity "Severity of 12 month depressive episode (c2019_w1)"
label values d_12m_severity severity

***12 month depression symptomatology 
gen depression_symptoms12m = .
replace depression_symptoms12m = 1 if d_12m_severity >=1
replace depression_symptoms12m = 0 if proxy==2 & (d_12m_severity ==0 | d_skipped_12m==1)
label values depression_symptoms12m yesno
label variable depression_symptoms12m "12 month depression symptomatology (c2019_w1)"

* 12 months depression comparable with COVID substudy from 12 month diagnosis

gen d_12m_severity_comparable = .
replace d_12m_severity_comparable = 0 if d_crit_g1_12m==0 | d_crit_b_12m==0 | (d_screentotal_12m==2 & d_symtotal_12m<2) | (d_screentotal_12m==3 & d_symtotal_12m==0)
replace d_12m_severity_comparable = 1 if d_crit_g1_12m==1 & ((d_screentotal_12m==2 & d_symtotal_12m>=2) | (d_screentotal_12m==3 & d_symtotal_12m>=1))
replace d_12m_severity_comparable = 2 if d_crit_g1_12m==1 & ((d_screentotal_12m==2 & d_symtotal_12m>=4) | (d_screentotal_12m==3 & d_symtotal_12m>=3))
replace d_12m_severity_comparable = 3 if d_crit_g1_12m==1 & (d_screentotal_12m==3 & d_symtotal_12m>=5)
label variable d_12m_severity_comparable "Severity of 12 month depressive episode comparable (c2019_w1)"
label values d_12m_severity_comparable severity

gen d_symptoms12m_comparable = .
replace d_symptoms12m_comparable = 1 if d_12m_severity_comparable>=1 
replace d_symptoms12m_comparable = 0 if proxy==2 & (d_12m_severity_comparable ==0 | d_skipped_12m==1) 
label variable d_symptoms12m_comparable "12 month depression comparable with COVID substudy (c2019_w1)"
label values d_symptoms12m_comparable yesno

```

```{stata descriptives, collectcode=FALSE, results='markup', echo=TRUE}

codebook depression_lifetime depression_lifetime_proxy depression_12m d_12m_item d_12m_comparable depression_symptoms12m d_symptoms12m_comparable

```

```{stata save-data}
keep ID_ECS q0007a_result proxy subsample_pre interview_pre depression_lifetime d_lifetime_severity depression_lifetime_proxy depression_12m depression_12m d_12m_item d_12m_comparable depression_symptoms12m d_12m_severity d_12m_severity_comparable d_symptoms12m_comparable 

*Cohorte 2019, linea base:

* Changed to avoid overwriting (dataset written in the same folder as the doc)
saveold "Outcome_depression_ICD10.dta", replace version(13)

```

# Contact person:

-   Name: Elvira Lara
-   e-mail: [elvira.lara\@uam.es](mailto:elvira.lara@uam.es){.email}
-   Phone number: (+34) 914974601

<br>

Original version:

-   Name: Dario Moreno Agostino 
-   e-mail: [d.moreno@ucl.ac.uk](mailto:d.moreno@ucl.ac.uk){.email}

# References

:::{#refs}
World Health Organization. (1993). The ICD-10 classification of mental and behavioural disorders: diagnostic criteria for research. Ed. World Health Organization: Geneva.
:::

# Session info

```{r, results='markup'}
devtools::session_info()
```
