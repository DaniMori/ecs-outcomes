---
title: "Description_depression_ICD11"
output:
  word_document:
    reference_docx: !expr ecs.data::get_pkg_docx_template()
  pdf_document: default
csl: "`r ecs.data::get_proj_csl_file()`"
bibliography: "`r ecs.data::get_proj_bib_file()`"
editor_options:
  markdown:
    wrap: 80
---

```{r setup, include=FALSE}
# Activar los paquetes que R necesita para ejecutar el c贸digo:

library(Statamarkdown)

# Ruta al ejecutable de Stata (Ejecuta `sysdir` en Stata para encontrar la ruta)

# Opciones de configuraci贸n de las salidas de R y Stata:

knitr::opts_chunk$set( # Para todos los 'chunks' de c贸digo:
  echo        = FALSE, #   - No muestra la sintaxis en el documento
  results     = 'hide',#   - Omite los resultados de ejecutar la sintaxis
  cleanlog    = TRUE,  #   - Omite repetir sintaxis en resultados de Stata
  collectcode = TRUE   #   - Reutiliza el c贸digo de Stata de 'chunks' anteriores
)
```

# Variable information

+--------------------+------------------+------------------+------------------+
| **Main variable    | **Label**        | **Format**       | **Code**         |
| name**             |                  |                  |                  |
+:===================+:=================+==================+==================+
| d                  | Lifetime         | Binary           | -   0=no;        |
| epression_lifetime | depression       |                  | -   1=yes        |
|                    | (c2011_w3)       |                  |                  |
+--------------------+------------------+------------------+------------------+
| depress            | Lifetime         | Binary           | -   0=no;        |
| ion_lifetime_proxy | depression for   |                  | -   1=yes        |
|                    | proxy (c2011_w3) |                  |                  |
+--------------------+------------------+------------------+------------------+

# Variable description

Depression has become a major public health issue as it is one of the main
causes of disability worldwide, with an even greater impact on developed
countries. According to The ICD-10 Classification of Mental and Behavioural
Disorders, Diagnostic criteria for research
[@world_health_organization_icd-10_1992], the main characteristics of depression
are the presence of a sustained depressed mood, the loss of interest or pleasure
in activities normally pleasurable and/or the decreased energy or increased
fatigability. In addition to these main criteria, the ICD-10 Diagnostic criteria
for research proposes several contributing factors that can be present or
absent: loss of confidence and self-esteem, feelings of self-reproach or guilt,
recurrent thoughts of death or suicide, diminished ability to think or
concentrate, change in psychomotor activity, sleep disturbance, and changes in
appetite with the corresponding weight change. Moreover, the classification
proposes a three-level gradient of severity depending on the amount of criteria
present in each case, from mild to severe. The ICD-11 refers to the same
symptoms but strengthens the criteria, making the diagnosis of depression more
demanding than its predecessor. In order to address these criteria and be able
to assess the lifetime presence or absence of a depressive episode, as well as
its severity, we used an adapted version of the Composite International
Diagnostic Interview (CIDI 3.0), a comprehensive, fully-structured interview
designed to be used by trained lay interviewers for the assessment of mental
disorders according to the definitions and criteria of ICD-11. It is intended
for use in epidemiological and cross-cultural studies, as well as for clinical
and research purposes.

# Explanation of how the variables were created

Information from the CIDI depression module was used for the creation of the
depression variables. Algorithms based on the ICD-11 were used for the
endorsement of depressive episode at some point in their lives (lifetime
prevalence; variable `depression_lifetime`). More precisely, the following
criteria must be fulfilled:

i)  The depressive episode should last for at least 2 weeks;

ii) The clinical presentation does not fulfil the diagnostic requirements for a
    Mixed Episode.;

iii) The episode is not attributable to psychoactive substance use or to any
     organic mental disorder;

iv) The general criteria for depressive episode must be met;

v)  At least one of the following three symptoms must be present: Depressed
    mood, loss of interest;

vi) Additional symptoms should be present, to give a total of at least five
    (please see criteria C1-C8);

vii) The symptoms are not better accounted for by bereavement.

The coding of this algorithm is very complex and requires many different
criteria to be taken into account. We have tried to make use of uniform coding
criteria for implementing the computation of the different symptoms and
diagnostic criteria, and also separating clearly the computation of the
*positive*/*negative*/*missing* cases, to make the code the clearest possible.
The following general principles have been followed when possible:

-   All the non-valid responses are considered to be *negative* responses; that
    is, if an item has been answered with "*Don't know*" (`888`) or "Refused"
    (`999`), it is assumed that the criterion is "not met" (=`0`). (Note though
    that this rule is reverted for the "exclusion criteria", i.e., criteria "G2"
    and "G3").

-   Only the variables that are kept in the final dataset are labelled.

-   First, all the cases are set to `.` ("Missing" value).

-   Then, the negative cases are recoded to `0` (diagnostic "No").

-   Finally, the positive cases are recoded to `1` (diagnostic "Yes").

-   Unless stated otherwise, the cases are explicitly recoded to their final
    value exclusively and exhaustively, such that the number of cases recoded to
    `0`/`1` in each step must be exactly the same as the number of `0`/`1` cases
    finally in the variable. This allows for an additional check of recoding
    errors.

-   When the values need to be compared for unequality (operator `!=`) against a
    non-missing value, it must be taken into account that a "missing" value
    gives a `1` result instead of a missing in Stata. Therefore, in order to
    avoid recoding "missing" values into non-missing ones, the "missing" values
    must be excluded from the comparison. This principle is used generally
    throughout the algorithm; when used differently, it is explained in the
    syntax.

-   When these principles are not strictly followed, an annotation is added
    right below, explaining how it changes from them and why.

# Syntax used to create the variable (Stata 15)

```{stata load-data}
* Cohorte 2011, ola 3:
use "~\UAM\Marta Miret Garcia - Bases de datos maestras Edad con Salud\Ola_3\Cohorte_2019\rawdata_c2019w1.dta"
```

```{stata generate-outcomes, echo=TRUE}
*# "Value labels (defined for all the outcome variables in the dataset)":
label define yesno    0 "No" 1 "Yes"


***#"VARIABLE `depression_lifetime`: Depressive episode (F32)"

*#G1 criterion: "The depressive episode should last for at least 2 weeks"
gen     d_crit_g1 = .
replace d_crit_g1 = 0 if (q4211!=1 & q4211!=.) | (q4213!=1 & q4213!=.)
replace d_crit_g1 = 1 if  q4211==1             |  q4213==1

*#G2 criterion: "The clinical presentation does not fulfil the diagnostic requirements for a Mixed Episode"
gen     d_crit_g2 = .
replace d_crit_g2 = 0 if (q4233==1 & proxy==2) | q4225_causemed2==1
replace d_crit_g2 = 1 if (q4233!=1 & q4233!=. & proxy==2) | q4225_causemed2!=1
*#"NOTE: When `q4225_causemed2` is not selected, its value may be `0` or
*#       'missing', so only `q4233` is considered to determine whether
*#       this criterion is 'missing'".

*#G3 criterion: "The episode is not attributable to psychoactive substance use or to any organic mental disorder"
gen     d_crit_g3 = .
replace d_crit_g3 = 0 if                            q4224b==1              | (q4224==1 & (q4225_causemed12==1 | q4225_causemed23==1 | q4225_causemed24==1 | q4225_causemed25==1 | q4225_causemed30==1 | q4225_causemed31==1))
replace d_crit_g3 = 1 if (q4224a!=1 & q4224a!=.) | (q4224b!=1 & q4224b!=.) | (q4224==1 & (q4225_causemed12!=1 & q4225_causemed23!=1 & q4225_causemed24!=1 & q4225_causemed25!=1 & q4225_causemed30!=1 & q4225_causemed31!=1))
*#"NOTE: When `q4224b==1` it is necessarily the case that `q4224a==1`,
*#       so only `q4224b` out of these two needs to be checked for considering
*#       the criterion negative."

*#G4 criterion: "The symptoms are not better accounted for by bereavement"
gen d_crit_g4 = .
replace d_crit_g4 = 0 if q1012<5 | (q1013>0 & q1013!=.)
replace d_crit_g4 = 1 if q1012==5 & q1013==0
*#"NOTE: When the participant has been widow for less than a year, the G4 criterion is consider not completed. This criterion is not present in the ICD10. "

*#G5 criterion: "The symptoms cause clinically significant distress or impairment in social, occupational, or other important areas of functioning"
recode q4228a (1=1) (2=1) (3=1) (4=1) (5=1) (6=1) (7=1) (8=1) (9=1) (10=1) (0=0) (888=0) (999=0) (97=.)
recode q4228b (1=1) (2=1) (3=1) (4=1) (5=1) (6=1) (7=1) (8=1) (9=1) (10=1) (0=0) (888=0) (999=0) (97=.)
recode q4228c (1=1) (2=1) (3=1) (4=1) (5=1) (6=1) (7=1) (8=1) (9=1) (10=1) (0=0) (888=0) (999=0) (97=.)
recode q4228d (1=1) (2=1) (3=1) (4=1) (5=1) (6=1) (7=1) (8=1) (9=1) (10=1) (0=0) (888=0) (999=0) (97=.)
recode q4214 (1=0) (2=1) (3=1) (4=1) (888=0) (999=0)
recode q4215 (1=1) (2=1) (3=0) (4=0) (888=0) (999=0)
recode q4216 (1=0) (2=1) (3=1) (4=0) (888=0) (999=0)

gen     d_crit_g5 = .
replace d_crit_g5 = 0 if q4228a==0 | q4228b==0 | q4228c==0 | q4228d==0 | q4214==0 | q4215==0 | q4216==0
replace d_crit_g5 = 1 if q4228a==1 | q4228b==1 | q4228c==1 | q4228d==1 | q4214==1 | q4215==1 | q4216==1

*#"NOTE:In the absence of certainty and information on this subject, it is considered that the symptoms affect the daily life of the individual as soon as a change in behaviour and activity is perceived. Not present in the ICD10." 


*#A criterion: "The general criteria for depressive episode must be met"
gen     d_crit_a = .
replace d_crit_a = 0 if d_crit_g1==0 | d_crit_g2==0 | d_crit_g3==0 | d_crit_g4==1 | d_crit_g5==0
replace d_crit_a = 1 if d_crit_g1==1 & d_crit_g2==1 & d_crit_g3==1 & d_crit_g4==0 & d_crit_g5==1


*#B criterion: "At least one of the following two symptoms (affective cluster) must be present"

*#B1: "Depressed mood to a degree that is definitely abnormal for the individual, present for most of the day and almost every day, largely uninfluenced by circumstances, and sustained for at least 2 weeks"
gen     d_b1 = .
replace d_b1 = 0 if (q4220a!=1 & q4220a!=.) & q4220b!=1 & q4220c!=1
replace d_b1 = 1 if q4220a==1             | q4220b==1 | q4220c==1

*#"NOTE: When `q4220b` or `q4220d` are "missing" it may be due to the interview *#skip logic in `q4220a` or `q4220c`, respectively. But if there is a response in *#`q4220a` there must be one also in `q4220c`. Therefore, only `q4220a` needs to be *#checked for missingness".

*#B2: "Loss of interest or pleasure in activities that are normally pleasurable (including sexual desire)"
gen     d_b2 = .
replace d_b2 = 0 if (q4220e!=1 & q4220e!=.) & q4220f!=1
replace d_b2 = 1 if q4220e==1             | q4220f==1


*#"Sum of depressive screening symptoms"
egen d_screentotal = rowtotal (d_b1 d_b2), missing

*#"To fulfil B criterion at least one screening symptoms should be present".
gen     d_crit_b = .
replace d_crit_b = 0 if d_screentotal<1 
replace d_crit_b = 1 if d_screentotal==1 | d_screentotal==2


*#C criterion: "Additional symptoms from the following list should be present, to give a total of at least five"

*#C1: "Beliefs of low self-worth or excessive and inappropriate guilt that may be manifestly delusional"
gen     d_c1 = .
replace d_c1 = 0 if ((q4222k!=1 & q4222k!=.) & (q4222l!=1 & q4222l!=.) & q4222m!=1) | (q4222n!=1 & q4222n!=.)
replace d_c1 = 1 if q4222k==1             | q4222l==1             | q4222m==1 | q4222n==1
*#"NOTE: When `q4222m` is "missing" it may be due to the interview
*#       skip logic in `q4222l`. Therefore, only `q4222k` and `q4222l`
*#       need to be checked for missingness".

*#C2: "Hopelessness about the future."
gen     d_c2 = .
replace d_c2 = 0 if q4220d!=1 & q4220d!=.
replace d_c2 = 1 if q4220d==1
*#"NOTE: Symptom presents in the ICD10 as, (d) bleak and pessimistic views of the future;"

*#C3: "Recurrent thoughts of death or suicide, or any suicidal behaviour"
gen     d_c3 = .
replace d_c3 = 0 if q4222r!=1 & q4222r!=. & q4222s!=1 & q4222t!=1 & q4222u!=1 & q4222v!=1
replace d_c3 = 1 if q4222r==1             | q4222s==1 | q4222t==1 | q4222u==1 | q4222v==1
*#"NOTE: When any of the two last items are 'missing' it may be due to
*#       the interview skip logic in `q4222t`. But if there is a
*#       response in `q4222r` there must be one also in `q4222s` and
*#       `q4222t`.Therefore, only `q4222r` needs to be checked for
*#       missingness".

*#C4: "Complaints or evidence of diminished ability to think or concentrate, such as indecisiveness or vacillation"
gen     d_c4 = .
replace d_c4 = 0 if q4222h!=1 & q4222h!=. & q4222i!=1 & q4222j!=1
replace d_c4 = 1 if q4222h==1             | q4222i==1 | q4222j==1
*#"NOTE: When `q4222i` and `q4222j` are 'missing' it may be due to the
*#       interview skip logic in `q4222h`. Therefore, only `q4222h` needs
*#       to be checked for missingness".

*#C5: "Change in psychomotor activity, with agitation or retardation (either subjective or objective)"
gen     d_c5 = .
replace d_c5 = 0 if (q4222f!=1 & q4222f!=. & q4222fa!=1) | (q4222g!=1 & q4222ga!=1 & q4222g!=.)
replace d_c5 = 1 if q4222f==1 | q4222fa==1 | q4222g==1 | q4222ga==1
*#"NOTE: When the three last items are 'missing' it may be due to the
*#       interview skip logic in `q4222f`. Therefore, only `q4222f` needs
*#       to be checked for missingness".

*#C6: "Sleep disturbance of any type"
gen     d_c6 = .
replace d_c6 = 0 if (q4222c!=1 & q4222c!=.) & q4222d!=1
replace d_c6 = 1 if q4222c==1             | q4222d==1
*#"NOTE: When `q4222d` is 'missing' it may be due to the interview skip
*#       logic in `q4222c`. Therefore, only `q4222c` is checked for
*#       missingness".

*#C7: "Change in appetite (decrease or increase) with corresponding weight change"
gen     d_c7 = .
replace d_c7 = 0 if (q4222a!=1 & q4222a!=.) | (q4222b!=1 & q4222b!=.)
replace d_c7 = 1 if  q4222a==1              &  q4222b==1

*#C8: "Decreased energy or increased fatigability"
gen     d_c8 = .
replace d_c8 = 0 if q4222e!=1 & q4222e!=.
replace d_c8 = 1 if q4222e==1

*#"Sum of depressive symptoms"
egen d_symptot = rowtotal (d_c1-d_c8), missing

*#"To fulfil C criterion at least four screening symptoms should be present".
gen     d_crit_c = .
replace d_crit_c = 0 if d_symptot<4 
replace d_crit_c = 1 if d_symptot>3 & d_symptot!=.

***#"Has the person skipped the lifetime depression module?"

*#"Depression symptoms recoded into 0/1 to compute the tally":
recode q4220a-q4222v (1=1) (2=0) (888 999=.)

*#"Filter variables":
gen  f4217     = q4214==1 & q4215==4 & q4216==4 if q4214!=.
egen f4221_sum = rowtotal (q4220a-q4220f), missing
egen f4223_sum = rowtotal (q4220a-q4222v), missing

*#"Variable "skipped" computation":
gen     d_skipped = .
replace d_skipped = q4202!=1                   if q4202!=.
replace d_skipped = q4211!=1    | d_skipped==1 if q4211!=.    
replace d_skipped = q4213!=1    | d_skipped==1 if q4213!=.    
replace d_skipped = q4218!=1    | d_skipped==1 if q4218!=.    
replace d_skipped = f4217==1    | d_skipped==1 if f4217!=.    
replace d_skipped = f4221_sum<1 | d_skipped==1 if f4221_sum!=.
replace d_skipped = f4223_sum<2 | d_skipped==1 if f4223_sum!=.
*#"NOTE: For clarity, this recoding adds the criteria incrementally. This
*#       implies that the new "truth values" (0/1resulting from each
*#       comparison) are "logically added" (therefore the "OR" operation)
*#       to the current values".




***#"Lifetime depression diagnostic variable":
gen     depression_lifetime = .
replace depression_lifetime = 0 if  d_skipped==1 | d_crit_a==0 | d_crit_b==0 | d_crit_c==0
replace depression_lifetime = 1 if  d_skipped==0 & d_crit_a==1 & d_crit_b==1 & d_crit_c==1
label values   depression_lifetime yesno
label variable depression_lifetime "Lifetime depression (c2011_w3)"


***#"VARIABLE `depression_lifetime_proxy`: Depressive episode for proxy participants"
recode P4040 (1=1) (2=0) (888=0) (999=0), gen (depression_lifetime_proxy)
label values   depression_lifetime_proxy yesno
label variable depression_lifetime_proxy "Lifetime depression for proxy (c2011_w3)"

```

<br>

```{stata descriptives, collectcode=FALSE, results='markup'}
codebook depression_lifetime depression_lifetime_proxy
```

```{stata save-data}
keep depression_lifetime depression_lifetime_proxy 

* Cohorte 2019, ola 1:
* save [Description_depression_c2019W1_ICD11]
saleold "~\UAM\Marta Miret Garcia - Documentacion Edad con Salud\Edad con salud - Ola 3\Outcomes\Cohorte 2019\Outcome datasets\Description_depression_c2019W1_ICD11.dta", replace version(13)

```


# Contact person:

-   Name: Lea Francia
-   e-mail: [lea.francia\@inv.uam.es](mailto:lea.francia@inv.uam.es){.email}

# References

::: {#refs}
:::

\newpage

# Session info

```{r, results='markup'}
devtools::session_info()
```
