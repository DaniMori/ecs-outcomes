---
title: "Edad con Salud -- cohort 2011 -- Wave 4 -- 'Depression according to DSM-5'"
output:
  word_document:
    reference_docx: !expr ecs.data::get_pkg_docx_template()
bibliography: ../../../../www/Edad_con_salud_outcome_vars.bib 
csl: "`r ecs.data::get_proj_csl_file()`"
editor_options: 
  markdown:
    wrap: 80
params:
  test: true
  double-coding: false
---

```{r setup, include=FALSE}
# Activar los paquetes que R necesita para ejecutar el código:

library(Statamarkdown)

options(width = 88) # Ancho de salida de texto en R

knitr::opts_chunk$set( # Para todos los 'chunks' de código:
  echo        = FALSE, #   - No muestra la sintaxis en el documento
  results     = 'hide',#   - Omite los resultados de ejecutar la sintaxis
  cleanlog    = TRUE,  #   - Omite repetir sintaxis en resultados de Stata
  collectcode = TRUE,  #   - Reutiliza el código de Stata de 'chunks' anteriores
  comment     = ''     #   - No añade ningún prefijo a los resultados
)

TEST          <- params$test
DOUBLE_CODING <- params$`double-coding` & TEST
```

# Variable information

+----------------------------------+-----------------+----------+-----------+
| Main variable name               | Label           | Format   | Code      |
+:=================================+:================+:=========+:==========+
| depression_3y_s3                 | Inter-survey    | Binary   | 0 No      |
|                                  | depression      |          |           |
|                                  | (c2011_w4)      |          | 1 Yes     |
+----------------------------------+-----------------+----------+-----------+
| depression_12m_s3                | 12 months       | Binary   | 0 No      |
|                                  | depression      |          |           |
|                                  | (c2011_w4)      |          | 1 Yes     |
+----------------------------------+-----------------+----------+-----------+
| depression_12treatment_s3        | Treatment for   | Binary   | 0 No      |
|                                  | depression in   |          |           |
|                                  | previous 12     |          | 1 Yes     |
|                                  | months          |          |           |
|                                  | (c2011_w4)      |          |           |
+----------------------------------+-----------------+----------+-----------+
| depression_self_s3               | Depression      | Binary   | 0 No      |
|                                  | self-reported   |          |           |
|                                  | (c2011_w4)      |          | 1 Yes     |
+----------------------------------+-----------------+----------+-----------+
| d_3y_comparable_s3               | Inter-survey    | Binary   | 0 no      |
|                                  | depressive      |          |           |
|                                  | episode         |          | 1 yes     |
|                                  | comparable      |          |           |
|                                  | (c2011_w4)      |          |           |
+----------------------------------+-----------------+----------+-----------+
| d_12m_comparable_s3              | 12 month        | Binary   | 0 no      |
|                                  | depression      |          |           |
|                                  | comparable      |          | 1 yes     |
|                                  | (c2011_w4)      |          |           |
+----------------------------------+-----------------+----------+-----------+

# Variable description

Depression is a common mental disorder, characterized by sadness, loss of
interest or pleasure, feelings of guilt or low self-worth, disturbed sleep or
appetite, feelings of tiredness, and poor concentration [@freeman2016].
According to The *Diagnostic and Statistical Manual of Mental Disorders*,
depression can be long-lasting or recurrent, substantially impairing an
individual's ability to function at work or school or cope with daily life
[@americanpsychiatricassociation2013]. At its most severe, depression can lead
to suicide. When mild, people can be treated without medicines but when
depression is moderate or severe they may need medication and professional
talking treatments. Depression is a disorder that can be reliably diagnosed and
treated by non-specialists as part of primary health care. Specialist care is
needed for a small proportion of individuals with complicated depression or
those who do not respond to first-line treatments.

We used an adapted version of the Composite International Diagnostic Interview
(CIDI 3.0), a comprehensive, fully-structured interview designed to be used by
trained lay interviewers for the assessment of mental disorders according to the
definitions and criteria of DSM-V. It is intended for use in epidemiological and
cross-cultural studies as well as for clinical and research purposes. The
diagnostic section of the interview is based on the World Health Organization's
Composite International Diagnostic Interview.

# Explanation of how the variables were created

An algorithm based on the DSM-V was used for the endorsement of major depressive
episode at some point in their lives (since the last survey) and in the previous
12 months (point prevalence). We also included a treatment variable for 12-month
depression. These algorithms were created only for the non-proxy respondents.
Although a variable (`depression_self_s3`) was created to assess an eventual
presence of depression episode self-reported.

As for the proxy participants, original variables include a diagnosis depression
item (`p4040_depression_s3`) and reported treatment taking (`p4040a_age_s3`). A
diagnosis of depression was endorsed if the participant answered "yes" to
p4040_depression_s3, but not 997 in p4040a_age_s3 (they affirmed they had never
had depression). We also included a treatment code (`depress_12treatment_s3`) in
those individuals who, apart from having reported depression, have received
treatment for it in the previous 12 months.

The following criteria must be fulfilled in order to diagnose depression: A) At
least five of the following symptoms have been present during the same 2-week
period and represent a change from previous functioning; (1) depressed mood, (2)
loss of interest or pleasure, (3) significant weight loss, (4) insomnia or
hypersomnia, (5) psychomotor agitation or retardation, (6) fatigue or loss of
energy, (7) feelings of worthlessness, (8) diminished ability to think or
concentrate, (9) recurrent thoughts of death. At least one of the symptoms is
either (1) depressed mood or (2) loss of interest or pleasure. (Note: Do not
include symptoms that are clearly attributable to another medical condition.) B)
The symptoms cause clinically significant distress or impairment in social,
occupational, or other important areas of functioning.; C) The episode is not
attributable to the physiological effects of a substance or to another medical
condition. ; D) The occurrence of the major depressive episode is not better
explained by schizoaffective disorder, schizophrenia, schizophreniform disorder,
delusional disorder, or other specified and unspecified schizophrenia spectrum
and other psychotic disorders; E) There has never been a manic episode or a
hypomanic episode. These criteria where used to create the variable related to
inter-survey depression (`depression_3y_s3`). We also created a variable to
assess the presence of a depressive episode during the last 12 months using only
one item (`q4227a_s3`) which directly asks for the presence of a depressive
episode during this 12-month period.

As the dataset will be used for longitudinal studies, a variable of the
prevalence of depression during the last survey comparable to other waves has
been created using the criteria A, C and D above.

The coding of this algorithm is very complex and requires many different
criteria to be taken into account. We have tried to make use of uniform coding
criteria for implementing the computation of the different symptoms and
diagnostic criteria, and also separating clearly the computation of the
positive/negative/missing cases, to make the code the clearest possible. The
following general principles have been followed when possible:

-   All the non-valid responses are considered to be negative responses; that
    is, if an item has been answered with "Don't know" (888) or "Refused" (999),
    it is assumed that the criterion is "not met" (=0).

-   When an item hasn't been answered or has been "passed" (97), it is
    considered as a missing value (=.). Only the variables that are kept in the
    final dataset are labelled. • First, all the cases are set to . ("Missing"
    value).

-   Then, the negative cases are recoded to 0 (diagnostic "No").

-   Finally, the positive cases are recoded to 1 (diagnostic "Yes").

-   Unless stated otherwise, the cases are explicitly recoded to their final
    value exclusively and exhaustively, such that the number of cases recoded to
    0/1 in each step must be exactly the same as the number of 0/1 cases finally
    in the variable. This allows for an additional check of recoding errors.

-   When the values need to be compared for unequality (operator !=) against a
    non-missing value, it must be taken into account that a "missing" value
    gives a 1 result instead of a missing in Stata. Therefore, in order to avoid
    recoding "missing" values into non-missing ones, the "missing" values must
    be excluded from the comparison. This principle is used generally throughout
    the algorithm; when used differently, it is explained in the syntax.

-   When these principles are not strictly followed, an annotation is added
    right below, explaining how it changes from them and why.

# Syntax used to create the variable (Stata 15)

```{stata load-data}

* Cohorte 2011, ola 4:
use "~\UAM\Marta Miret Garcia - Bases de datos maestras Edad con Salud\Ola_4\Cohorte 2011\rawdata_c2011w4.dta"

```

```{stata generate-outcomes, echo=!DOUBLE_CODING}
*#"Value labels (defined for all the outcome variables in the dataset)":
label define yesno  0 "No" 1 "Yes"

***#"VARIABLE `depression_3y_s3`: Major depressive episode"

*#A criterion:"At least five of the following symptoms have been present during the same 2-week"

*#A1:"Depressed mood most of the day, nearly every day, as indicated by either subjective report or observation made by others" 
gen     d_A1 = .
replace d_A1 = 0 if (q4220a_s3!=1 & q4220a_s3!=.) & q4220b_s3!=1 & q4220c_s3!=1 & q4220d_s3!=1
replace d_A1 = 1 if q4220a_s3==1 | q4220b_s3==1 | q4220c_s3==1 | q4220d_s3==1

*#A2:"Markedly diminished interest or pleasure in all, or almost all, activities most of the day nearly every day" 
gen     d_A2 = .
replace d_A2 = 0 if (q4220e_s3!=1 & q4220e_s3!=.) & (q4220f_s3!=1 & q4220f_s3!=.)
replace d_A2 = 1 if q4220e_s3==1 | q4220f_s3==1
  
*#A3:"Significant weight loss when not dieting, or weight gain or decrease or increase in appetite nearly every day"
gen     d_A3 = .
replace d_A3 = 0 if (q4222a_s3!=1 & q4222a_s3!=.) | (q4222b_s3!=1 & q4222b_s3!=.)
replace d_A3 = 1 if  q4222a_s3==1 | q4222b_s3==1

*#A4:"Insomnia or hypersomnia nearly every day" 
gen     d_A4 = .
replace d_A4 = 0 if (q4222c_s3!=1 & q4222c_s3!=.) & q4222d_s3!=.
replace d_A4 = 1 if q4222c_s3==1 | q4222d_s3==1           

*#A5:"Psychomotor agitation or retardation nearly every day" 
gen     d_A5 = .
replace d_A5 = 0 if (q4222f_s3!=1 & q4222f_s3!=. & q4222fa_s3!=1) | (q4222g_s3!=1 & q4222ga_s3!=1 & q4222g_s3!=.)
replace d_A5 = 1 if q4222f_s3==1 | q4222fa_s3==1 | q4222g_s3==1 | q4222ga_s3==1

*#A6:"Fatigue or loss of energy nearly every day" 
gen     d_A6 = .
replace d_A6 = 0 if q4222e_s3!=1 & q4222e_s3!=.
replace d_A6 = 1 if q4222e_s3==1

*#A7:"Feelings of worthlessness or excessive or inappropriate guilt nearly every day (not merely self-reproach or guilt about being sick)"
gen     d_A7 = .
replace d_A7 = 0 if (q4222k_s3!=1 & q4222k_s3!=.) & (q4222n_s3!=1 & q4222n_s3!=.) &  (q4222l_s3!=1 & q4222l_s3!=.) &  (q4222m_s3!=1 & q4222m_s3!=.) 
replace d_A7 = 1 if q4222k_s3==1 | q4222n_s3==1 | q4222l_s3==1 | q4222m_s3==1

*#A8:"Diminished ability to think or concentrate, or indecisiveness, nearly every day"
gen     d_A8 = .
replace d_A8 = 0 if (q4222h_s3!=1 & q4222h_s3!=.) & (q4222i_s3!=1 & q4222j_s3!=1)
replace d_A8 = 1 if q4222h_s3==1 | q4222i_s3==1 | q4222j_s3==1

*#A9:"Recurrent thoughts of death (not just fear of dying), recurrent suicidal ideation without a specific plan, or a suicide attempt or a specific plan for committing suicide"
gen     d_A9 = .
replace d_A9 = 0 if (q4222r_s3!=1 & q4222r_s3!=.) & (q4222s_s3!=1 & q4222t_s3!=1) & (q4222u_s3!=1 & q4222v_s3!=1)
replace d_A9 = 1 if q4222r_s3==1 | q4222s_s3==1 | q4222t_s3==1 | q4222u_s3==1 | q4222v_s3==1

*#"To fulfil A criterion at least 5 screening symptoms should be present"
egen d_symtotal = rowtotal (d_A1-d_A9), missing
*#"At least one of following symptom must be present, (A1) depressed mood, (A2) loss of interest or pleasure"
gen     d_symprior = .
replace d_symprior = 0 if d_A1==0 & d_A2==0
replace d_symprior = 1 if d_A1==1 | d_A2==1

*#"Total of A criterion"  
gen     d_crit_A = .
replace d_crit_A = 0 if (d_symtotal<5 & d_symtotal!=.) | d_symprior==0 
replace d_crit_A = 1 if (d_symtotal>4 & d_symtotal!=.) & d_symprior==1 

*#Bcriterion:"The symptoms cause clinically significant distress or impairment in social, occupational, or other important areas of functioning"

*#"Follow of A critierion"

recode q4214_s3 (1=0) (2=1) (3=1) (4=1) (else=.) 
recode q4215_s3 (1=1) (2=1) (3=1) (4=0) (else=.)
recode q4216_s3 (1=0) (2=1) (3=1) (4=0) (else=.)
recode q4700a_s3 q4700b_s3 q4700c_s3 q4700d_s3 (999=.) (888=.)
gen discomfort=. 
replace discomfort=1 if q4700a_s3>=4 | q4700b_s3>=4 | q4700c_s3>=4 | q4700d_s3>=4 
replace discomfort=0 if (q4700a_s3<4 & q4700a_s3!=.) & (q4700b_s3<4 & q4700b_s3!=.) & (q4700c_s3<4 & q4700c_s3!=.) & (q4700d_s3<4 & q4700d_s3!=.)

gen     d_crit_B = .
replace d_crit_B = 0 if q4214_s3==0 | q4215_s3==0 | q4216_s3==0 | discomfort==0
replace d_crit_B = 1 if q4214_s3==1 | q4215_s3==1 | q4216_s3==1 | discomfort==1
  
  
*#C criterion:"The episode is not attributable to the physiological effects of a substance or to another medical condition"
gen     d_crit_C = .
replace d_crit_C = 0 if                            q4224b_s3==1              | (q4224_s3==1 & (q4225_causemed12_s3==1 | q4225_causemed23_s3==1 | q4225_causemed24_s3==1 | q4225_causemed25_s3==1 | q4225_causemed30_s3==1 | q4225_causemed31_s3==1))
replace d_crit_C = 1 if (q4224a_s3!=1 & q4224a_s3!=.) | (q4224b_s3!=1 & q4224b_s3!=.) | (q4224_s3==1 & (q4225_causemed12_s3!=1 & q4225_causemed23_s3!=1 & q4225_causemed24_s3!=1 & q4225_causemed25_s3!=1 & q4225_causemed30_s3!=1 & q4225_causemed31_s3!=1))
  
*#D criterion: "The occurrence of the major depressive episode is not better explained by schizoaffective disorder, schizophrenia, schizophreniform disorder, delusional disorder, or other specified and unspecified schizophrenia spectrum and other psychotic disorders"

gen d_crit_D =. 
replace d_crit_D = 0 if q4224b_s3==1 | (q4224_s3==1 & (q4225_causemed3_s3==1 | q4225_causemed4_s3==1)) 
replace d_crit_D = 1 if (q4224a_s3!=1 & q4224a_s3!=.) | (q4224b_s3!=1 & q4224b_s3!=.) | (q4224_s3==1 & (q4225_causemed3_s3!=1 & q4225_causemed4_s3!=. & q4225_causemed4_s3!=1 & q4225_causemed4_s3!=.))

*#Ecriterion:"There is no hyper or hypomanic crisis"

gen d_crit_E =. 
replace d_crit_E = 1 if (q4233_s3!=1 & q4233_s3!=.) & q4225_causemed2_s3!=1
replace d_crit_E = 0 if (q4233_s3==1 | q4225_causemed2_s3==1) & proxy==2

 
***#"Has the person skipped the depression module?"

*#"Depression symptoms recoded into 0/1 to compute the tally":
recode q4220a_s3-q4222v_s3 (1=1) (2=0) (888 999=.)

*#"Filter variables":
gen  f4217     = q4214_s3==1 & q4215_s3==4 & q4216_s3==4 if q4214_s3!=.
egen f4221_sum = rowtotal (q4220a_s3-q4220f_s3), missing
egen f4223_sum = rowtotal (q4220a_s3-q4222v_s3), missing

*#"Variable "skipped" computation":
gen     d_skipped = .
replace d_skipped = q4202_s3!=1                   if q4202_s3!=.
replace d_skipped = q4211_s3!=1    | d_skipped==1 if q4211_s3!=.    
replace d_skipped = q4213_s3!=1    | d_skipped==1 if q4213_s3!=.    
replace d_skipped = q4218_s3!=1    | d_skipped==1 if q4218_s3!=.    
replace d_skipped = f4217==1    | d_skipped==1 if f4217!=.    
replace d_skipped = f4221_sum<1 | d_skipped==1 if f4221_sum!=.
replace d_skipped = f4223_sum<2 | d_skipped==1 if f4223_sum!=.
*#"NOTE: For clarity, this recoding adds the criteria incrementally. This
*#       implies that the new "truth values" (0/1resulting from each
*#       comparison) are "logically added" (therefore the "OR" operation)
*#       to the current values".



***#"Major depressive episode (DSM-5)"

gen     depression_3y_s3 = .
replace depression_3y_s3 = 0 if (d_skipped==1 | d_crit_A!=1 | d_crit_B!=1 | d_crit_C!=1 | d_crit_D!=1 | d_crit_E!=1) & proxy==2
replace depression_3y_s3 = 1 if d_skipped==0 & d_crit_A==1 & d_crit_B==1  & d_crit_C==1 & d_crit_D==1 & d_crit_E==1
label values   depression_3y_s3 yesno
label variable depression_3y_s3 "Inter-survey depression (c2011_w4)"


***#VARIABLE "`depression_12m_s3`: 12-month depression"

gen     depression_12m_s3 = .
replace depression_12m_s3 = 0 if q4227a_s3!=1 & proxy==2
replace depression_12m_s3 = 1 if q4227a_s3==1
label values   depression_12m_s3 yesno
label variable depression_12m_s3 "12 months depression (c2011_w4)"


***#VARIABLE "`depression_12treatment_s3`: treatment for depression, 12-months"

recode q4232_s3 (1=1) (2=0) (888=0) (999=0)
recode q4231_s3 (1=1) (2=0) (888=0) (999=0)

gen     depression_12treatment_s3 = .
replace depression_12treatment_s3 = 0 if (depression_12m_s3==0 & (q4232_s3!=. & q4231_s3!=1 & q4232_s3!=.) & (q4231_s3!=1  & q4231_s3!=.) & q4231_s3==0) | proxy==2
replace depression_12treatment_s3 = 1 if depression_12m_s3==1 & (q4231_s3==1 | q4232_s3==1)
label values   depression_12treatment_s3 yesno
label variable depression_12treatment_s3 "Treatment for depression in the previous 12 months (c2011_w4)"
*#"NOTE: We take into acount psychological treatment and pharmacological treatment.

***#VARIABLE "`depression_self_s3`: `depression self-reported`"

recode p4040_depression_s3 (1=1) (2=0) (888=0) (999=0)
gen     depression_self_s3 = .
replace depression_self_s3 = 0 if p4040_depression_s3!=1 & p4040_depression_s3!=.
replace depression_self_s3 = 1 if p4040_depression_s3==1 & p4040a_age_s3!=997
label values   depression_self_s3 yesno
label variable depression_self_s3 "Depression self-reported (c2011_w4)"

*#"Inter-survey depression "comparable" (does not take into account exclusion criteria B and E)"
gen     d_3y_comparable_s3 = .
replace d_3y_comparable_s3 = 0 if d_skipped==1 | d_crit_A==0 | d_crit_C==0 | d_crit_D==0 
replace d_3y_comparable_s3 = 1 if d_skipped==0 | d_crit_A==1 & d_crit_C==1 & d_crit_D==1
label values   d_3y_comparable_s3 yesno
label variable d_3y_comparable_s3 "Inter-survey depressive episode comparable (c2011_w4)"

*#"12-month 'comparable', based on the intermediate 'Inter-survey comparable' and the individual item":
gen     d_12m_comparable_s3 = .
replace d_12m_comparable_s3 = 0 if d_3y_comparable_s3==0 | depression_12m_s3==0
replace d_12m_comparable_s3 = 1 if d_3y_comparable_s3==1 & depression_12m_s3==1
label values   d_12m_comparable_s3 yesno
label variable d_12m_comparable_s3 "12 month depression comparable with COVID substudy (c2011_w4)"

```

```{stata descriptives, cleanlog=FALSE, collectcode=FALSE, results='markup', include=!DOUBLE_CODING}
keep ID_ECS q0007a_result proxy depression_3y_s3 depression_12m_s3 depression_12treatment_s3 depression_self_s3 d_12m_comparable_s3 d_3y_comparable_s3

codebook depression_3y_s3 depression_12m_s3 depression_12treatment_s3 depression_self_s3 d_12m_comparable_s3 d_3y_comparable_s3
```

```{stata save-data-test, eval=TEST}
saveold "Outcome_depression_DSM5.dta", replace version(13)
```

```{stata save-data, eval=!TEST}
* Cohorte 2011, ola 4:
saveold "~\UAM\Marta Miret Garcia - Documentacion Edad con Salud\Edad con salud - Ola 4\Outcomes\Cohorte 2011\Outcome datasets\Outcome_depression_DSM5.dta", replace version(13)
```

<br>

# Contact person:

Contact:

-   Name: Lea Francia
-   e-mail: [lea.francia\@inv.uam.es](mailto:lea.francia@inv.uam.es){.email}
-   Phone number: (+34) 914974601

Original version:

-   Name: Dario Moreno Agostino
-   e-mail: [d.moreno\@ucl.ac.uk](mailto:d.moreno@ucl.ac.uk){.email}

<br>

-   Name: Elvira Lara
-   e-mail: [elvira.lara\@uam.es](mailto:elvira.lara@uam.es){.email}
-   Phone number: (+34) 914974601

<br>

-   Name: Daniel Morillo
-   e-mail:
    [daniel.morillo\@inv.uam.es](mailto:daniel.morillo@inv.uam.es){.email}
-   Phone number: (+34) 914975816

# References

::: {#refs}
:::

\newpage

# Session info

```{r, results='markup'}
devtools::session_info()
```
