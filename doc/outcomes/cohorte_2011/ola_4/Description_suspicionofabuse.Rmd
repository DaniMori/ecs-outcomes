---
title: "Edad con Salud -- cohort 2011 -- wave 4 -- 'Suspicion of abuse'"
output:
  word_document:
   reference_docx: !expr ecs.data::get_pkg_docx_template()
bibliography: ../../../../www/Edad_con_salud_outcome_vars.bib 
csl: "`r ecs.data::get_proj_csl_file()`"
editor_options: 
  markdown:
    wrap: 80
params:
  test: true
  double-coding: false
---

```{r setup, include=FALSE}
# Activar los paquetes que R necesita para ejecutar el código:

library(Statamarkdown)

STATA_PATH <- r"(C:/Program Files (x86)/Stata15/StataSE-64.exe)"


options(width = 88) # Ancho de salida de texto en R

knitr::opts_chunk$set( 
  echo        = FALSE, #   - No muestra la sintaxis en el documento
  results     = 'hide',#   - Omite los resultados de ejecutar la sintaxis
  cleanlog    = TRUE,  #   - Omite repetir sintaxis en resultados de Stata
  collectcode = TRUE,  #   - Reutiliza el código de Stata de 'chunks' anteriores
  comment     = ''     #   - No añade ningún prefijo a los resultados
)

TEST          <- params$test
DOUBLE_CODING <- params$`double-coding` & TEST
```

# Variable information

+-------------------+----------------+--------------------+---------------------+
| Main variable     | Label          | Format             | Code                |
| name              |                |                    |                     |
+:==================+:===============+:===================+:====================+
| dependency_s3     | Dependency     | Nominal            | -   0 = No          |
|                   | status         |                    |     dependence      |
|                   |                |                    |                     |
|                   | (c2011_w4)     |                    | -   1 = Dependency  |
|                   |                |                    |     served          |
|                   |                |                    |                     |
|                   |                |                    | -   2 = Problematic |
|                   |                |                    |     dependence      |
|                   |                |                    |                     |
|                   |                |                    | -   3 = Neglected   |
|                   |                |                    |     dependence      |
+-------------------+----------------+--------------------+---------------------+
| abuse_negli_s3    | Negligence     | Ordinal            | -   0 = No abuse    |
|                   | abuse          |                    |                     |
|                   |                |                    | -   1 = Occasional  |
|                   | (c2011_w4)     |                    |                     |
|                   |                |                    | -   2 = Sometimes   |
|                   |                |                    |                     |
|                   |                |                    | -   3 = Quite often |
|                   |                |                    |                     |
|                   |                |                    | -   4 = Often       |
+-------------------+----------------+--------------------+---------------------+
| abuse_psycho_s3   | Psychological  | Ordinal            | -   0 = No abuse    |
|                   | abuse          |                    |                     |
|                   |                |                    | -   1 = Occasional  |
|                   | (c2011_w4)     |                    |                     |
|                   |                |                    | -   2 = Sometimes   |
|                   |                |                    |                     |
|                   |                |                    | -   3 = Quite often |
|                   |                |                    |                     |
|                   |                |                    | -   4 = Often       |
+-------------------+----------------+--------------------+---------------------+
| abuse_eco_s3      | Economical     | Ordinal            | -   0 = No abuse    |
|                   | abuse          |                    |                     |
|                   |                |                    | -   1 = Occasional  |
|                   | (c2011_w4)     |                    |                     |
|                   |                |                    | -   2 = Sometimes   |
|                   |                |                    |                     |
|                   |                |                    | -   3 = Quite often |
|                   |                |                    |                     |
|                   |                |                    | -   4 = Often       |
+-------------------+----------------+--------------------+---------------------+
| abuse_phys_s3     | Physical and   | Ordinal            | -   0 = No abuse    |
|                   | sexual abuse   |                    |                     |
|                   |                |                    | -   1 = Occasional  |
|                   | (c2011_w4)     |                    |                     |
|                   |                |                    | -   2 = Sometimes   |
|                   |                |                    |                     |
|                   |                |                    | -   3 = Quite often |
|                   |                |                    |                     |
|                   |                |                    | -   4 = Often       |
+-------------------+----------------+--------------------+---------------------+
| abusesuspicion_s3 | Suspicion of   | Dichotomic         | -   0 = No          |
|                   | abuse          |                    |                     |
|                   | (c2011_w4)     |                    | -   1 = Yes         |
+-------------------+----------------+--------------------+---------------------+
|dependency_abuse_s3| Suspicion of   | Dichotomic         | -   0 = No          |
|                   | abuse taking   |                    |                     |
|                   | into account   |                    |                     |
|                   | dependency     |                    |                     |
|                   | (c2011_w4)     |                    | -   1 = Yes         |
+-------------------+----------------+--------------------+---------------------+


# Variable description

The Spanish version of the Elder Abuse Suspicion Index [@pérez-rojo2008] is an
adaptation of the Elder Abuse Suspicion Index (EASI) [@yaffe_development_2008],
a brief instrument with direct questions aimed at detecting suspicion of abuse 
in older people (aged +64) without cognitive impairment. It is made up of five 
questions addressed to the elderly person and one to the professional, which are
asked in the course of an encounter between the elderly person and the professional
in the consultation room and are formulated by the professional using simple 
language. The first question focuses on the assessment of the level of dependency
of the elderly person for some basic or instrumental activity of daily living. 
The remaining questions refer to different types of abuse: physical and sexual, 
psychological, financial, and neglect. The items are dichotomous (0=No; 1=Yes), 
and are followed by items on its frequency on a Likert scale. 
A positive response to any of the questions would indicate suspicion of abuse.
It is noteworthy that in our version we do not use the item referring to the
professional. 

Each dichotomous item is accompanied by an item on the frequency of the abusive 
behavior they received if the dichotomous item is replied positively. This 
frequency is based on a Likert scale, ranging from 1 (ocassional) to 4 (often).
See in more detail in the 'Explanation' section below. 

The questions refer to situations that occurred in the 12 months before
the completion of the questionnaire and only persons aged 65 and over were
asked.

Variables indicating the presence and frequency of each type of abuse are
created, as well as a variable indicating the degree of dependency of the elderly
and a general variable about suspicion of abuse. These variables are comparable
with the previous waves. See 'explanation section' for more details.

- `dependency_s3`: Dependency status
- `abuse_negli_s3 `: Negligence abuse
- `abuse_psycho_s3`: Psychological abuse
- `abuse_eco_s3`: Economical abuse
- `abuse_phys_s3`: Physical abuse
- `abusesuspicion_s3`: Suspicion of abuse
- `dependency_abuse_s3`: Suspicion of abuse taking into account dependency.



# Explanation of how the variables were created

We have followed the conventions of Wave 3 (dependency, each specific type of
abuse) and we added an additional variable based on the literature:

-   Create a variable indicating the **degree of dependency** (`dependency_s3`),
    based on the block of questions `q6701_s3` (help from someone), `q6701b_s3`
    (problems with them) and `q6701d_s3` (need help). In this way, we find four
    categories:

    -   *No dependence*: has not received help because they didn't need it.
    -   *Dependency served*: has received the help needed
    -   *Problematic dependence*: has received help, but had problems with people who helped them;
    -   *Neglected dependence*: needed help but didn't receive it.

-   Create four variables, one for **each specific type of abuse**. For each 
    specific type of abuse, a pair of items is used. The first item of the pair
    indicates the presence or absence of abuse, and the second item, 
    if abuse is present, its frequency on a Likert-type scale
    (1 occasional - 4 often).

    -   `abuse_negli_s3`, negligence abuse: `q6702_s3`, `q6702a_s3`
    -   `abuse_psycho_s3`, psychological abuse: `q6703_s3`, `q6703a_s3`
    -   `abuse_eco_s3`, economical abuse: `q6704_s3`, `q6704a_s3`
    -   `abuse_phys_s3`, physical and sexual abuse: `q6705_s3`, `q6705a_s3`
  
  First, the values are recoded. Given that in the original scale of the 
  questionnaire the items are coded as 1 = yes and 2 = no, the 2 are converted
  into 0; and the 999 values are recoded into missings. This applies to all the
  items involved in the creation of these variables.
  
  The variable combines both frequency and presence absence into a frequency 
  variable. Therefore, if in the first item the
  option replied is "yes", the subsidiary item of frequency 
  (which has the suffix "a") 
  is used to create the response scale: 
  0 = No abuse,
  1 = Occasional,
  2 = Sometimes,
  3 = Quite often,
  4 = Often. 
  See syntax below for more details.


To create the `abusesuspicion_s3` variable, we keep the 4 items that
indicated the presence of each specific type of abuse introduced earlier in 
combination with item `q6701_s3`. This item indicates if the person has 
needed help, so in the case of responding 'yes' there could be suspicion of
abuse, following the criteria stablished by @yaffe_development_2008.
The total scale of suspicion of abuse was obtained by summing the answers
given to each 4 items indicating presence or absence of abuse.
A dichotomization of the scores was then made, for those
participants who answer yes to one of the five items a suspicion of abuse is
considered. In any other case, the result is 0 (1 = Yes, 0 = No).

To include the grade of dependency, `dependency_abuse_s3` is created.
This is a dichotomous variable that includes:
- No abuse in dependency: 
  - Without help and no help needed: q6701 == 2 & q6701d == 2
  - Help needed without any problems: q6701 == 1 & Q6701b == 2
- Abuse within dependency:
  - Any positive score on the dichotomous items that indicate presence or absence
  on specific types of abuse. A intermediate variable 'abuse_types' is created
  to take them into account.

# Syntax used to create the variable (Stata 15)

```{stata load-data}
* Cohorte 2011, ola 4:
use "~/UAM/Marta Miret Garcia - Bases de datos maestras Edad con Salud/Ola_4/Cohorte 2011/rawdata_c2011w4.dta"
```

```{stata generate-outcomes, echo=!DOUBLE_CODING}
*DEPENDENCY
*Dependency (comparable)
recode q6701_s3 q6701d_s3 q6701b_s3 (2=0) (999=.) 
gen dependency_s3=. 

**0 = No dependence; when the person has not had any help (q6701_s3) and has not needed it (q6701d_s3):
replace dependency_s3=0 if q6701_s3==0 & q6701d_s3==0 

**1 = Dependency served; has had help:
replace dependency_s3=1 if q6701_s3==1  

**2 = Problematic dependence; had help and had problems with people who helped them
replace dependency_s3=2 if (q6701_s3==1 & q6701b_s3==1)

**3 = Neglected dependence; needed help but didn`t receive it
replace dependency_s3=3 if q6701_s3==0 & q6701d_s3==1 

label define dep 0 "No dependence" 1 "Dependency served" 2 "Problematic dependence" 3 "Neglected dependence"  
label values dependency_s3 dep 
label variable dependency_s3 "Dependency status (c2011_w4)" 


**ABUSE VARIABLES
* All variables have the same label values. We define them firstly:
label define ab 0 "No abuse" 1 "Occasional" 2 "Sometimes" 3 "Quite often" 4 "Often" 
  

*NEGLIGENCE
*abuse_negli_s3 "Negligence (c2011_w4)"	 - q6702_s3, q6702a_s3

recode q6702_s3 (2=0) (999=.) 
recode q6702a_s3 (999=.) 

**We want the final variable to contain the Likert scale of item q6702a_s3, 
**so we generate it from item q6702a_s3. 

gen abuse_negli_s3=q6702a_s3

**Finally, we add the non-abuse cases with the dichotomous reference variable.
replace abuse_negli_s3=0 if q6702_s3==0

label values abuse_negli_s3 ab 
label variable abuse_negli_s3 "Negligence abuse (c2011_w4)" 



*PSYCHOLOGICAL ABUSE
*abuse_psycho_s3 Psychological abuse (c2011_w4) - q6703_s3, q6703a_s3 

recode q6703_s3 (2=0) (999=.) 
recode q6703a_s3 (999=.) 

**We want the final variable to contain the Likert scale of item q6703a_s3, 
**so we generate it from item q6703a_s3. 

gen abuse_psycho_s3=q6703a_s3

**Finally, we add the non-abuse cases with the dichotomous reference variable.
replace abuse_psycho_s3=0 if q6703_s3==0

label values abuse_psycho_s3 ab 
label variable abuse_psycho_s3 "Psychological abuse (c2011_w4)" 

 

*ECONOMICAL ABUSE
*abuse_eco_s3 Economical abuse (c2011_w4)- q6704_s3, q6704a_s3 
recode q6704_s3 (2=0) (999=.) 
recode q6704a_s3 (999=.) 

**We want the final variable to contain the Likert scale of item q6703a_s3, 
**so we generate it from item q6704a_s3. 

gen abuse_eco_s3=q6704a_s3

**Finally, we add the non-abuse cases with the dichotomous reference variable.
replace abuse_eco_s3=0 if q6704_s3==0

label values abuse_eco_s3 ab 
label variable abuse_eco_s3 "Economical abuse (c2011_w4)" 

 

**abuse_phys_s3	Physical and sexual abuse (c2011_w4) - q6705_s3, q6705a_s3 

recode q6705_s3 (2=0) (999=.) 
recode q6705a_s3 (999=.) 

**We want the final variable to contain the Likert scale of item q6703a_s3, 
**so we generate it from item q6703a_s3. 

gen abuse_phys_s3=q6705a_s3

**Finally, we add the non-abuse cases with the dichotomous reference variable.

replace abuse_psycho_s3=0 if q6705_s3==0

label values abuse_phys_s3 ab 
label variable abuse_phys_s3 "Physical and sexual abuse (c2011_w4)" 



*SUSPICION OF ABUSE (correct version following literature)
gen abuse_tot = (q6701_s3 + q6702_s3 + q6703_s3 + q6704_s3 + q6705_s3)

gen abusesuspicion_s3 =.
replace abusesuspicion_s3=0 if abuse_tot<1 
replace abusesuspicion_s3=1 if abuse_tot>0 & abuse_tot!=.

label define abusesuspicion_l 0 "No" 1 "Yes"
label values abusesuspicion_s3 abusesuspicion_l
label variable abusesuspicion_s3 "Suspicion of abuse (c2011_w4)"

* DEPENDENCY - ABUSE

gen abuse_types = (q6702_s3 + q6703_s3 + q6704_s3 + q6705_s3)

gen dependency_abuse_s3 =.
replace dependency_abuse_s3 = 0 if q6701_s3 == 0 & q6701d_s3 == 0
replace dependency_abuse_s3 = 0 if q6701_s3 == 1 & q6701b_s3 == 0
replace dependency_abuse_s3 = 1 if abuse_types >0 & abuse_types!=. 

label values dependency_abuse_s3 abusesuspicion_l
label variable dependency_abuse_s3 "Suspicion of abuse taking into account dependency (c2011_w4)"

```

<br>

```{stata descriptives, cleanlog=FALSE, collectcode=FALSE, results='markup', include=!DOUBLE_CODING}
keep ID_ECS q0007a_result_s3 proxy_s3 dependency_s3 abuse_negli_s3 abuse_psycho_s3 abuse_eco_s3 abuse_phys_s3 abusesuspicion_s3 dependency_abuse_s3
codebook dependency_s3 abuse_negli_s3 abuse_psycho_s3 abuse_eco_s3 abuse_phys_s3 abusesuspicion_s3 dependency_abuse_s3
```

```{stata save-data-test, eval=TEST}

saveold "Outcome_suspicionofabuse.dta", replace version(13)
```

```{stata save-data, eval=!TEST}

* Cohorte 2011, ola 4:
saveold "~\UAM\Marta Miret Garcia - Documentacion Edad con Salud\Edad con salud - Ola 4\Outcomes\Cohorte 2011\Outcome datasets\Outcome_suspicionofabuse.dta", replace version(13)
```

<br>

# Contact person:

-   Name: Lea Francia
-   e-mail: [lea.francia\@inv.uam.es](mailto:lea.francia@inv.uam.es){.email}
-   Phone number: (+34) 914974601

<br>

-   Name: Cristina Rodríguez Prada
-   e-mail:
    [cristina.rodriguez\@inv.uam.es](mailto:cristina.rodriguez@inv.uam.es){.email}

Original version:

-   Name: Dario Moreno Agostino
-   e-mail: [d.moreno\@ucl.ac.uk](mailto:d.moreno@ucl.ac.uk){.email}

# References

::: {#refs}
:::

\newpage

# Session info

```{r, results='markup'}
devtools::session_info()
```
