---
title: "Edad con Salud -- cohort 2011 -- Wave 4 -- 'DEPRESSION ICD-10'"
output:
  word_document:
    reference_docx: !expr ecs.data::get_pkg_docx_template()
# bibliography: "`r ecs.data::get_proj_bib_file()`" ## TODO: Borrar esta línea, O BIEN deescomentar esta línea y borrar siguiente UNA VEZ completo el documento
bibliography: ../../../../www/Edad_con_salud_outcome_vars.bib ## TODO: Descomentar esta línea SI se utiliza el gestor bibliográfico, en otro caso BORRAR esta línea
csl: "`r ecs.data::get_proj_csl_file()`"
editor_options: 
  markdown:
    wrap: 80
params:
  test: true
  double-coding: false
---

```{r setup, include=FALSE}
# Activar los paquetes que R necesita para ejecutar el código:


library(Statamarkdown)

# Ruta al ejecutable de Stata (Ejecuta `sysdir` en Stata para encontrar la ruta)

## TODO: sustituir '<path\to\>' por la ubicación de Stata
##       (ejecutar `sysdir` en Stata) y descomentar línea 62,
##       o eliminar linea 62:
# STATA_PATH <- r"(<path\to\>StataSE-64.exe)"


# Opciones de configuración de las salidas de R y Stata:

options(width = 88) # Ancho de salida de texto en R

knitr::opts_chunk$set( # Para todos los 'chunks' de código:
  ## TODO: Descomentar si hace falta
  # engine.path = list(stata = STATA_PATH),
  echo        = FALSE, #   - No muestra la sintaxis en el documento
  results     = 'hide',#   - Omite los resultados de ejecutar la sintaxis
  cleanlog    = TRUE,  #   - Omite repetir sintaxis en resultados de Stata
  collectcode = TRUE,  #   - Reutiliza el código de Stata de 'chunks' anteriores
  comment     = ''     #   - No añade ningún prefijo a los resultados
)

TEST          <- params$test
DOUBLE_CODING <- params$`double-coding` & TEST
```

# Variable information

+--------------------+------------------+-----------------+-----------------+
| **Main variable    | **Label**        | **Format**      | **Code**        |
| name**             |                  |                 |                 |
+:===================+:=================+=================+=================+
| d                  | Severity of      | Ordinal         | -   0=No;       |
| \                  | lifetime         |                 | -   1=Mild;     |
| _lifetime_severity | depressive       |                 | -   2=Moderate; |
|                    | episode          |                 | -   3=Severe    |
|                    | (c2011_w4)       |                 |                 |
+--------------------+------------------+-----------------+-----------------+
| d                  | Lifetime         | Binary          | -   0=no;       |
| epression_lifetime | depression       |                 | -   1=yes       |
|                    | (c2011_w4)       |                 |                 |
+--------------------+------------------+-----------------+-----------------+
| depress            | Lifetime         | Binary          | -   0=no;       |
| ion_lifetime_proxy | depression for   |                 | -   1=yes       |
|                    | proxy (c2011_w4) |                 |                 |
+--------------------+------------------+-----------------+-----------------+
| d_12m_item         | 12 month         | Binary          | -   0=no;       |
|                    | depression, one  |                 | -   1=yes       |
|                    | item (c2011_w4)  |                 |                 |
+--------------------+------------------+-----------------+-----------------+
| depression_12m     | 12 month         | Binary          | -   0=no;       |
|                    | depression from  |                 | -   1=yes       |
|                    | lifetime         |                 |                 |
|                    | diagnosis &      |                 |                 |
|                    | q4227/q4227a     |                 |                 |
|                    | items (c2011_w4) |                 |                 |
+--------------------+------------------+-----------------+-----------------+
| d_l                | Severity of      | Binary          | -   0=no;       |
| ifetime_comparable | lifetime         |                 | -   1=yes       |
|                    | depressive       |                 |                 |
|                    | episode          |                 |                 |
|                    | comparable       |                 |                 |
|                    | (c2011_w4)       |                 |                 |
+--------------------+------------------+-----------------+-----------------+
| d_12m_comparable   | 12 month         | Binary          | -   0=no;       |
|                    | depression       |                 | -   1=yes       |
|                    | comparable with  |                 |                 |
|                    | COVID substudy   |                 |                 |
|                    | (c2011_w4)       |                 |                 |
+--------------------+------------------+-----------------+-----------------+

# Variable description

Depression has become a major public health issue as it is one of the main
causes of disability worldwide, with an even greater impact on developed
countries. According to The ICD-10 Classification of Mental and Behavioural
Disorders, Diagnostic criteria for research
[@world_health_organization_icd-10_1992], the main characteristics of depression
are the presence of a sustained depressed mood, the loss of interest or pleasure
in activities normally pleasurable and/or the decreased energy or increased
fatigability. In addition to these main criteria, the ICD-10 Diagnostic criteria
for research proposes several contributing factors that can be present or
absent: loss of confidence and self-esteem, feelings of self-reproach or guilt,
recurrent thoughts of death or suicide, diminished ability to think or
concentrate, change in psychomotor activity, sleep disturbance, and changes in
appetite with the corresponding weight change. Moreover, the classification
proposes a three-level gradient of severity depending on the amount of criteria
present in each case, from mild to severe. In order to address these criteria
and be able to assess the lifetime presence or absence of a depressive episode,
as well as its severity, we used an adapted version of the Composite
International Diagnostic Interview (CIDI 3.0), a comprehensive, fully-structured
interview designed to be used by trained lay interviewers for the assessment of
mental disorders according to the definitions and criteria of ICD-10. It is
intended for use in epidemiological and cross-cultural studies, as well as for
clinical and research purposes.

# Explanation of how the variables were created

Information from the CIDI depression module was used for the creation of the
depression variables. Algorithms based on the ICD-10 were used for the
endorsement of depressive episode at some point in their lives (lifetime
prevalence; variable `depression_lifetime`) and in the previous 12 months (point
prevalence; variable `depression_12m`). More precisely, the following criteria
must be fulfilled:

i)  The depressive episode should last for at least 2 weeks;

ii) There have been no hypomanic or manic symptoms sufficient to meet the
    criteria for hypomanic or manic episode at any time in the individual's
    life;

iii) The episode is not attributable to psychoactive substance use or to any
     organic mental disorder;

iv) The general criteria for depressive episode must be met;

v)  At least two of the following three symptoms must be present: Depressed
    mood, loss of interest and decreased energy;

vi) an additional symptom or symptoms should be present, to give a total of at
    least four (please see criteria C1-C7).

We also included a severity variable according to the ICD-10 criteria for the
lifetime prevalence (variable `d_lifetime_severity`) based upon the number of
symptoms the participant met.

There is also a section of the interview for directly diagnosing 12-month
depression based on the symptomatology. However, errors in the filters (i.e.
skip logic) of the interview lead to most of the participants not responding to
this part of the interview when they should. Therefore, these variables have
been dropped. Finally, as as abbreviated version of the CIDI interview was used
for the post-lockdown telephonic interview in the COVID substudy, comparable
versions of both lifetime (`d_lifetime_comparable`) and recent
(`d_12m_comparable`) have been created, applying the same criteria and
algorithm.

The coding of this algorithm is very complex and requires many different
criteria to be taken into account. We have tried to make use of uniform coding
criteria for implementing the computation of the different symptoms and
diagnostic criteria, and also separating clearly the computation of the
*positive*/*negative*/*missing* cases, to make the code the clearest possible.
The following general principles have been followed when possible:

-   All the non-valid responses are considered to be *negative* responses; that
    is, if an item has been answered with "*Don't know*" (`888`) or "Refused"
    (`999`), it is assumed that the criterion is "not met" (=`0`). (Note though
    that this rule is reverted for the "exclusion criteria", i.e., criteria "G2"
    and "G3").

-   Only the variables that are kept in the final dataset are labelled.

-   First, all the cases are set to `.` ("Missing" value).

-   Then, the negative cases are recoded to `0` (diagnostic "No").

-   Finally, the positive cases are recoded to `1` (diagnostic "Yes").

-   Unless stated otherwise, the cases are explicitly recoded to their final
    value exclusively and exhaustively, such that the number of cases recoded to
    `0`/`1` in each step must be exactly the same as the number of `0`/`1` cases
    finally in the variable. This allows for an additional check of recoding
    errors.

-   When the values need to be compared for unequality (operator `!=`) against a
    non-missing value, it must be taken into account that a "missing" value
    gives a `1` result instead of a missing in Stata. Therefore, in order to
    avoid recoding "missing" values into non-missing ones, the "missing" values
    must be excluded from the comparison. This principle is used generally
    throughout the algorithm; when used differently, it is explained in the
    syntax.

-   When these principles are not strictly followed, an annotation is added
    right below, explaining how it changes from them and why.

# Syntax used to create the variable (Stata 15)

```{stata load-data}
* Cohorte 2011, linea base:
use "~\UAM\Marta Miret Garcia - Bases de datos maestras Edad con Salud\Ola_4\Cohorte 2011\rawdata_c2011w4.dta"
```

```{stata generate-outcomes, echo=TRUE}
*# "Value labels (defined for all the outcome variables in the dataset)":
label define severity 0 "No" 1 "Mild" 2 "Moderate" 3 "Severe"
label define yesno    0 "No" 1 "Yes"


***#"VARIABLE `depression_lifetime`: Depressive episode (F32)"

*#G1 criterion: "The depressive episode should last for at least 2 weeks"
gen     d_crit_g1 = .
replace d_crit_g1 = 0 if (q4211_s3!=1 & q4211_s3!=.) | (q4213_s3!=1 & q4213_s3!=.)
replace d_crit_g1 = 1 if  q4211_s3==1             |  q4213_s3==1

*#G2 criterion: "There have been no hypomanic or manic symptoms sufficient to meet the criteria for hypomanic or manic episode at any time in the individual's life"
gen     d_crit_g2 = .
replace d_crit_g2 = 0 if q4233_s3==1            | q4225_causemed2_s3==1
replace d_crit_g2 = 1 if q4233_s3!=1 & q4233_s3!=. & q4225_causemed2_s3!=1
*#"NOTE: When `q4225_causemed2` is not selected, its value may be `0` or
*#       'missing', so only `q4233` is considered to determine whether
*#       this criterion is 'missing'".

*#G3 criterion: "The episode is not attributable to psychoactive substance use or to any organic mental disorder"
gen     d_crit_g3 = .
replace d_crit_g3 = 0 if                            q4224b_s3==1              | (q4224_s3==1 & (q4225_causemed12_s3==1 | q4225_causemed23_s3==1 | q4225_causemed24_s3==1 | q4225_causemed25_s3==1 | q4225_causemed30_s3==1 | q4225_causemed31_s3==1))
replace d_crit_g3 = 1 if (q4224a_s3!=1 & q4224a!=.) | (q4224b_s3!=1 & q4224b_s3!=.) | (q4224_s3==1 & (q4225_causemed12_s3!=1 & q4225_causemed23_s3!=1 & q4225_causemed24_s3!=1 & q4225_causemed25_s3!=1 & q4225_causemed30_s3!=1 & q4225_causemed31_s3!=1))
*#"NOTE: When `q4224b==1` it is necessarily the case that `q4224a==1`,
*#       so only `q4224b` out of these two needs to be checked for considering
*#       the criterion negative."

***#"Mild depressive episode (F32.0)"

*#A criterion: "The general criteria for depressive episode must be met"
gen     d_crit_a = .
replace d_crit_a = 0 if d_crit_g1==0 | d_crit_g2==0 | d_crit_g3==0
replace d_crit_a = 1 if d_crit_g1==1 & d_crit_g2==1 & d_crit_g3==1


*#B criterion: "At least two of the following three symptoms must be present"

*#B1: "Depressed mood to a degree that is definitely abnormal for the individual, present for most of the day and almost every day, largely uninfluenced by circumstances, and sustained for at least 2 weeks"
gen     d_b1 = .
replace d_b1 = 0 if q4220a_s3!=1 & q4220a_s3!=. & q4220b_s3!=1 & q4220c_s3!=1 & q4220d_s3!=1
replace d_b1 = 1 if q4220a_s3==1             | q4220b_s3==1 | q4220c_s3==1 | q4220d_s3==1
*#"NOTE: When `q4220b` or `q4220d` are "missing" it may be due to the
*#       interview skip logic in `q4220a` or `q4220c`, respectively.
*#       But if there is a response in `q4220a` there must be one
*#       also in `q4220c`. Therefore, only `q4220a` needs to be checked
*#       for missingness".

*#B2: "Loss of interest or pleasure in activities that are normally pleasurable"
gen     d_b2 = .
replace d_b2 = 0 if q4220e_s3!=1 & q4220e_s3!=. & q4220f_s3!=1
replace d_b2 = 1 if q4220e_s3==1             | q4220f_s3==1

*#B3: "Decreased energy or increased fatigability"
gen     d_b3 = .
replace d_b3 = 0 if q4222e_s3!=1 & q4222e_s3!=.
replace d_b3 = 1 if q4222e_s3==1

*#"Sum of depressive screening symptoms"
egen d_screentotal = rowtotal (d_b1 d_b2 d_b3), missing

*#"To fulfil B criterion >=2 screening symptoms should be present".
gen     d_crit_b = .
replace d_crit_b = 0 if d_screentotal< 2
replace d_crit_b = 1 if d_screentotal>=2 & d_screentotal!=.


*#C criterion: "An additional symptom or symptoms from the following list should be present, to give a total of at least four"

*#C1: "Loss of confidence and self-esteem"
gen     d_c1 = .
replace d_c1 = 0 if q4222k_s3!=1 & q4222k_s3!=. & q4222l_s3!=1 & q4222l_s3!=. & q4222m_s3!=1
replace d_c1 = 1 if q4222k_s3==1             | q4222l_s3==1             | q4222m_s3==1
*#"NOTE: When `q4222m` is "missing" it may be due to the interview
*#       skip logic in `q4222l`. Therefore, only `q4222k` and `q4222l`
*#       need to be checked for missingness".

*#C2: "Unreasonable feelings of self-reproach or excessive and inappropriate guilt"
gen     d_c2 = .
replace d_c2 = 0 if q4222n_s3!=1 & q4222n_s3!=.
replace d_c2 = 1 if q4222n_s3==1

*#C3: "Recurrent thoughts of death or suicide, or any suicidal behaviour"
gen     d_c3 = .
replace d_c3 = 0 if q4222r_s3!=1 & q4222r_s3!=. & q4222s_s3!=1 & q4222t_s3!=1 & q4222u_s3!=1 & q4222v_s3!=1
replace d_c3 = 1 if q4222r_s3==1             | q4222s_s3==1 | q4222t_s3==1 | q4222u_s3==1 | q4222v_s3==1
*#"NOTE: When any of the two last items are 'missing' it may be due to
*#       the interview skip logic in `q4222t`. But if there is a
*#       response in `q4222r` there must be one also in `q4222s` and
*#       `q4222t`.Therefore, only `q4222r` needs to be checked for
*#       missingness".

*#C4: "Complaints or evidence of diminished ability to think or concentrate, such as indecisiveness or vacillation"
gen     d_c4 = .
replace d_c4 = 0 if q4222h_s3!=1 & q4222h_s3!=. & q4222i_s3!=1 & q4222j_s3!=1
replace d_c4 = 1 if q4222h_s3==1             | q4222i_s3==1 | q4222j_s3==1
*#"NOTE: When `q4222i` and `q4222j` are 'missing' it may be due to the
*#       interview skip logic in `q4222h`. Therefore, only `q4222h` needs
*#       to be checked for missingness".

*#C5: "Change in psychomotor activity, with agitation or retardation (either subjective or objective)"
gen     d_c5 = .
replace d_c5 = 0 if q4222f_s3!=1 & q4222f_s3!=. & q4222fa_s3!=1 & q4222g_s3!=1 & q4222ga!=1
replace d_c5 = 1 if q4222f_s3==1             | q4222fa_s3==1 | q4222g_s3==1 | q4222ga_s3==1
*#"NOTE: When the three last items are 'missing' it may be due to the
*#       interview skip logic in `q4222f`. Therefore, only `q4222f` needs
*#       to be checked for missingness".

*#C6: "Sleep disturbance of any type"
gen     d_c6 = .
replace d_c6 = 0 if q4222c_s3!=1 & q4222c_s3!=.
replace d_c6 = 1 if q4222c_s3==1             | q4222d_s3==1
*#"NOTE: When `q4222d` is 'missing' it may be due to the interview skip
*#       logic in `q4222c`. Therefore, only `q4222c` is checked for
*#       missingness".

*#C7: "Change in appetite (decrease or increase) with corresponding weight change"
gen     d_c7 = .
replace d_c7 = 0 if (q4222a_s3!=1 & q4222a_s3!=.) | (q4222b_s3!=1 & q4222b_s3!=.)
replace d_c7 = 1 if  q4222a_s3==1              &  q4222b_s3==1

*#"Sum of depressive symptoms"
egen d_symtotal = rowtotal (d_c1-d_c7), missing


***#"Has the person skipped the lifetime depression module?"

*#"Depression symptoms recoded into 0/1 to compute the tally":
recode q4220a-q4222v (1=1) (2=0) (888 999=.)

*#"Filter variables":
gen  f4217     = q4214==1 & q4215==4 & q4216==4 if q4214!=.
egen f4221_sum = rowtotal (q4220a_s3-q4220f_s3), missing
egen f4223_sum = rowtotal (q4220a_s3-q4222v_s3), missing

*#"Variable "skipped" computation":
gen     d_skipped = .
replace d_skipped = q4202_s3!=1                   if q4202_s3!=.
replace d_skipped = q4211_s3!=1    | d_skipped==1 if q4211_s3!=.    
replace d_skipped = q4213_s3!=1    | d_skipped==1 if q4213_s3!=.    
replace d_skipped = q4218_s3!=1    | d_skipped==1 if q4218_s3!=.    
replace d_skipped = f4217==1    | d_skipped==1 if f4217!=.    
replace d_skipped = f4221_sum<1 | d_skipped==1 if f4221_sum!=.
replace d_skipped = f4223_sum<2 | d_skipped==1 if f4223_sum!=.
*#"NOTE: For clarity, this recoding adds the criteria incrementally. This
*#       implies that the new "truth values" (0/1resulting from each
*#       comparison) are "logically added" (therefore the "OR" operation)
*#       to the current values".


***#"Severity of depressive episode"

*#"To meet the criteria for "Mild depressive episode (F32.0)", at least two of the screening symptoms (B1-B3) must be present, and an additional symptom or symptoms from the rest of them (B1-C7) should be present to give a total of at least four. In the case of "Moderate depressive episode (F32.1)", there must be at least two screening symptoms and a total of at least six symptoms. In the case of "Severe depressive episode (F32.3)", the three screening symptoms (B1-B3) must be present, and additional symptoms (C1-C7) should be present to give a total of at least eight".

*#"Severity according to total symptom tally (prior to applying the "screening symtpoms" criterion)":
gen     d_lifetime_severity_total = .
replace d_lifetime_severity_total = 0 if (d_screentotal+d_symtotal)< 4 & proxy==2
replace d_lifetime_severity_total = 1 if (d_screentotal+d_symtotal)>=4 & d_screentotal!=. & d_symtotal !=.
replace d_lifetime_severity_total = 2 if (d_screentotal+d_symtotal)>=6 & d_screentotal!=. & d_symtotal !=.
replace d_lifetime_severity_total = 3 if (d_screentotal+d_symtotal)>=8 & d_screentotal!=. & d_symtotal !=.
*#"NOTE: The criteria are applied incrementally, such that if a case
*#       fulfills the crieria for a higher severity level, it is first
*#       encoded as the milder level and then recoded into the more
*#       severe one".

*#"Final severity computation":
gen     d_lifetime_severity = .
replace d_lifetime_severity = 0 if (d_skipped==1 | d_crit_a==0 | d_crit_b==0 | d_lifetime_severity_total==0) & proxy==2
replace d_lifetime_severity = 1 if d_skipped==0 & d_crit_a==1 & d_crit_b==1      & d_lifetime_severity_total>=1 & d_lifetime_severity_total!=.
replace d_lifetime_severity = 2 if d_skipped==0 & d_crit_a==1 & d_crit_b==1      & d_lifetime_severity_total>=2 & d_lifetime_severity_total!=.
replace d_lifetime_severity = 3 if d_skipped==0 & d_crit_a==1 & d_screentotal==3 & d_lifetime_severity_total==3
label values   d_lifetime_severity severity
label variable d_lifetime_severity "Severity of lifetime depressive episode (c2011_w4)"
*#"NOTE: The criteria are applied incrementally, such that if a case
*#       fulfills the criteria for a higher severity level, it is first
*#       encoded as the milder level and then recoded into the more
*#       severe one".


***#"Lifetime depression diagnostic variable":
gen     depression_lifetime = .
replace depression_lifetime = 0 if d_lifetime_severity==0 & proxy==2
replace depression_lifetime = 1 if d_lifetime_severity>=1 & d_lifetime_severity !=.
label values   depression_lifetime yesno
label variable depression_lifetime "Lifetime depression (c2011_w4)"
#Due to a 888 case to "p4040_depression" we know one interview is proxy but 
#we don't know if it is a case of depression. We have to add "proxy = 2" to be 
#the "don't know" case (888) in proxy is not considered as a 'no' but as missing. 

***#"VARIABLE `depression_lifetime_proxy`: Depressive episode for proxy participants"
recode p4040_depression_s3 (1=1) (2=0) (888=0) (999=0), gen (depression_lifetime_proxy)
label values   depression_lifetime_proxy yesno
label variable depression_lifetime_proxy "Lifetime depression for proxy (c2011_w4)"


***#"VARIABLE `d_12m_item`: 12-month depression criterion from q4227/q4227a items"
gen     d_12m_item = .
replace d_12m_item = 0 if q4227!=1 & q4227a!=1 & proxy==2
replace d_12m_item = 1 if q4227==1 | q4227a==1
label values   d_12m_item yesno
label variable d_12m_item "12 month depression, one item (c2011_w4)"


***#"VARIABLE `depression_12m`: 12-month depression (combining `depression_lifetime` + `d_12m_item`)"
gen     depression_12m = .
replace depression_12m = 0 if depression_lifetime==0 | d_12m_item==0
replace depression_12m = 1 if depression_lifetime==1 & d_12m_item==1
label values   depression_12m yesno
label variable depression_12m "12 month depression from lifetime diagnosis + q4227/q4227a items (c2011_w4)"


***#"VARIABLE `d_12m_comparable`: 12-month depression (comparable with COVID substudy) from lifetime diagnosis"

*#"Lifetime "comparable" (does not take into account exclusion criteria G2 and G3)":
gen     d_lifetime_comparable = .
replace d_lifetime_comparable = 0 if d_skipped==1 | d_crit_g1==0 | d_crit_b==0 | d_lifetime_severity_total==0
replace d_lifetime_comparable = 1 if d_skipped==0 & d_crit_g1==1 & d_crit_b==1 & d_lifetime_severity_total>=1 & d_lifetime_severity_total!=.
label values   d_lifetime_comparable severity
label variable d_lifetime_comparable "Severity of lifetime depressive episode comparable (c2011_w4)"

*#"12-month 'comparable', based on the intermediate 'lifetime comparable' and the individual item":
gen     d_12m_comparable = .
replace d_12m_comparable = 0 if d_lifetime_comparable==0 | d_12m_item==0
replace d_12m_comparable = 1 if d_lifetime_comparable==1 & d_12m_item==1
label values   d_12m_comparable yesno
label variable d_12m_comparable "12 month depression comparable with COVID substudy (c2011_w4)"
```

```{stata descriptives, cleanlog=FALSE, collectcode=FALSE, results='markup', include=!DOUBLE_CODING}
keep ID_ECS q0007a_result proxy d_lifetime_severity depression_lifetime depression_lifetime_proxy d_12m_item depression_12m d_lifetime_comparable d_12m_comparable

codebook d_lifetime_severity depression_lifetime depression_lifetime_proxy d_12m_item depression_12m d_lifetime_comparable d_12m_comparable
```

```{stata save-data-test, eval=TEST}
saveold "Outcome_depression_ICD10.dta", replace version(13)
```

```{stata save-data, eval=!TEST}
*Cohorte 2011, linea base:
saveold "~\UAM\Marta Miret Garcia - Documentacion Edad con Salud\Edad con salud - Ola 4\Outcomes\Cohorte 2011\Outcome datasets\Outcome_depression_ICD10.dta", replace version(13)
```

<br>

# Contact person:

-   Name: Lea Francia
-   e-mail: [lea.francia\@inv.uam.es](mailto:lea.francia@inv.uam.es){.email}
-   Phone number: (+34) 914974601

Original version:

-   Name: Dario Moreno Agostino
-   e-mail: [d.moreno\@ucl.ac.uk](mailto:d.moreno@ucl.ac.uk){.email}

<br>

-   Name: Elvira Lara
-   e-mail: [elvira.lara\@uam.es](mailto:elvira.lara@uam.es){.email}
-   Phone number: (+34) 914974601

<br>

-   Name: Daniel Morillo
-   e-mail:
    [daniel.morillo\@inv.uam.es](mailto:daniel.morillo@inv.uam.es){.email}
-   Phone number: (+34) 914975816

# References

::: {#refs}
:::

\newpage

# Session info

```{r, results='markup'}
devtools::session_info()
```
