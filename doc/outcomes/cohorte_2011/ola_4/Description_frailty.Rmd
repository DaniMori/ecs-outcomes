---
title: "Edad con Salud -- cohort 2011 -- wave 4 -- 'Frailty'" ## TODO: Añadir "cohort", "assessment" y "variable name(s)/domain"
output:
  word_document:
    reference_docx: !expr ecs.data::get_pkg_docx_template()
# bibliography: "`r ecs.data::get_proj_bib_file()`" ## TODO: Borrar esta línea, O BIEN deescomentar esta línea y borrar siguiente UNA VEZ completo el documento
bibliography: ../../../../www/Edad_con_salud_outcome_vars.bib ## TODO: Descomentar esta línea SI se utiliza el gestor bibliográfico, en otro caso BORRAR esta línea
csl: "`r ecs.data::get_proj_csl_file()`"
# nocite: '@<cita_1>, @<cita_2>' ## TODO: Descomentar y añadir cita(s) o borrar campo
editor_options: 
  markdown:
    wrap: 80
params:
  test: true
  double-coding: false
---

```{r setup, include=FALSE}
library(Statamarkdown)
STATA_PATH <- r"(C:/Program Files (x86)/Stata15/StataSE-64.exe)"
options(width = 88) 
knitr::opts_chunk$set( 
  echo        = FALSE, 
  results     = 'hide',
  cleanlog    = TRUE,  
  collectcode = TRUE,  
  comment     = ''     
)
TEST          <- params$test
DOUBLE_CODING <- params$`double-coding` & TEST
```

# Variable information

+--------------------+------------------------------------------+------------+----------------------+
| Main variable name | Label                                    | Format     | Code                 |
+:===================+:=========================================+:===========+:=====================+
| frailty_index_s3   | Frailty (c2011_w4)                       | Dichotomous| 0 Non-frail;         |
|                    |                                          |            | 1 Frail              |
+--------------------+------------------------------------------+------------+----------------------+
| frailty_score_s3   | Frailty score (c2011_w4)                 |Quantitative| 0-1                  |
+--------------------+------------------------------------------+------------+----------------------+

# Variable description

_Frailty_ is an index that considers the various biopsychosocial factors that can
influence the presence of frailty in the elderly. This index originally includes
a total of 31 ratings, covering the three dimensions assessed: “Chronic Health 
Conditions,” “Cognitive functions” and “Disability and daily-life functioning.” 
Each of these ratings receives a specific score (@tyrovolas_frailty_2018). 
Note though that presence of injury was not assessed in this wave, so 30 
out of 31 scales were considered here.

- __Chronic Health Conditions__: Considers the variables Obesity, Hypertension, 
  Stroke, Lung disease, Angina, Asthma, Arthritis, Oral impairment, Diabetes,
  Cataracts, Use of technical aids and Grip Strength.
- __Cognitive Functions__: Comprises the assessment of _Learning and Short-term_
  _Memory_ (Delayed Recall task), _Learning and Short-term Memory_ (Immediate Recall
  task), _Working memory_ (Forwards and Backwards Digit tasks), and _Verbal fluency_
  (Animal Naming task).
  - Verbal Fluency and the Immediate Recall task were assessed using the Consortium
	to Establish a Registry for Alzheimer Disease Word List Memory (@moms_consortium_1989).
	The Delayed Recall task and the Forwards and Backwards Digit tasks were based 
	on the WAIS III (@wechsler_wais-iii_1997).
- __Disability and daily-life functioning__: Assesses the participant’s 
  functionality based on the WHODAS version 2.0 scale (_World Health Organisation Disability
  Assessment Schedule_), and adds questions that cover the self-perception of the 
  health status. Items such as Mobility, Interpersonal activities, Activities of domestic
  life, Sleep, Rest, Hearing, and Pain are considered (@wilson_improving_2006).
	Participants were asked to report the level of difficulty they had in 
	performing some activities over the past 30 days using a five-point scale 
	(none = 1, mild = 2, moderate = 3, severe = 4, extreme / can’t do = 5). 
	However, in the case of the scales for Attention, Mobility, and Interpersonal 
	activities, there are different numbers of items, so the additive scores have 
	ranges 13-65, 5-25 and 6-30, respectively.
	In the case of mobility, items 2026 and 2041 were omitted (@tyrovolas_frailty_2018).
	Quintiles of the aforementioned scores were used to classify participants: 
	without difficulty, mild, moderate, severe, and extreme.

<br>

For further details on the frailty index, please see @tyrovolas_frailty_2018 
(section 2.3.1 in p. 139, and Table 1).


# Explanation of how the variables were created

The scores are given according to the following components assessed:

•	**Dichotomous rating**: Assigned to the first 12 items (obesity, hypertension,
  stroke, lung disease, angina, asthma, arthritis, oral impairment, diabetes, 
  cataracts, use of aid machines, grip strength). 
  Integer values 0 or 1 are given depending on whether the participant has (1) 
  or does not have (0) the condition assessed.
  
•	**3-point scale rating** on the four components of cognitive function 
  and the physical activity outcome. Values of 0, 0.5 and, 1 are assigned, where
  0 means “no alterations,” 0.5 implies intermediate function, and 1 a low 
  function for the “cognitive function” components, and for physical activity 0
  means “high,” 0.5 “moderate, and 1 “low”.
  
•	**4-point scale rating**: Values of 0, 0.33, 0.66 and 1 are given to the
  vision deficit assessment. A higher value represents a greater visual 
  alteration.
  
•	**5-point scale rating**: Used for the daily life activities domain; 
  scores of 0, 0.25, 0.5, 0.75 and 1 are assigned, where the higher the assigned 
  score, the greater the limitation in the activities.
  All the subscales are then averaged, and a threshold of 0.25 is applied; 
  if the total score is below 0.25, the Non-frail category is assigned (0), 
  while a value of 0.25 or higher indicates frailty (1).


# Syntax used to create the variable (Stata 15)

```{stata load-data}
* Cohorte 2011, ola 4:
use "~\UAM\Marta Miret Garcia - Bases de datos maestras Edad con Salud\Ola_4\Cohorte 2011\rawdata_c2011w4.dta"
```

```{stata generate-outcomes, echo=!DOUBLE_CODING}
* Health deficits

** Obesity
merge 1:1 ID_ECS using "~\UAM\Marta Miret Garcia - Documentacion Edad con Salud\Edad con salud - Ola 4\Outcomes\Cohorte 2011\Outcome datasets\Outcome_anthropometricmeasures.dta", keepusing(bmi_cat_s3) gen(merge_bmi)

recode bmi_cat 0/2=0 3=1, gen(obesity)

** Chronic conditions
merge 1:1 ID_ECS using "~\UAM\Marta Miret Garcia - Documentacion Edad con Salud\Edad con salud - Ola 4\Outcomes\Cohorte 2011\Outcome datasets\Outcome_chronicconditions.dta", keepusing(hypertension_s3 stroke_s3 oral_s3 lung_s3 diab_s3 angina_s3 asthma_s3 arthritis_s3 cataracts_s3) gen(merge_chronic)
*** Merges in variables `hypertension_s3`, `stroke_s3`,  `oral_s3`,
***   `lung_s3`, `diab_s3`, `angina_s3`, `asthma_s3`, `cataracts_s3`
***   and `arthritis_s3`.
recode hypertension_s3 stroke_s3 oral_s3 lung_s3 diab_s3 angina_s3 asthma_s3 arthritis_s3 cataracts_s3 (2=0)

** Use of aid machines (Recode value `2` (= 'No') into `0`)
recode q2049_aids_s3 2=0, gen(aid_machines)


** Grip strength (weakness) (grip: 2 levels)
merge 1:1 ID_ECS using "~\UAM\Marta Miret Garcia - Documentacion Edad con Salud\Edad con salud - Ola 4\Outcomes\Cohorte 2011\Outcome datasets\Outcome_gripstrength.dta", keepusing(grip_s3) gen(merge_grip)


** Vision difficulties (vision: 4 levels; note this does not match the
**   visual acuity measure explained in Tyrovolas, 2018)
recode q2023_vis_diff_s3 q2024_vis_diff_s3 (888=.)
gen    vision = q2023_vis_diff_s3 + q2024_vis_diff_s3
recode vision 2=0 3/4=0.33 5/7=.66 8/10=1

** Hearing difficulties (WARNING!! These items do NOT correspond to the
**   domains described in Tyrovolas, 2018; the items refer to
**   "difficulty hearing someone on the other side of the room with a normal voice", `q2051`, and
**   "difficulty hearing in a conversation with another person in a silent room", `q2052`.

recode q2051_hear_diff 1=0 2=0.25 3=0.5 4=0.75 5=1 888=., gen(hearing_room)
recode q2052_hear_diff_s3 1=0 2=0.25 3=0.5 4=0.75 5=1 888=., gen(hearing_conv)

** Pain
recode q2007_pains_s3 q2009_difficulty_s3 (888=.)
gen    pain = q2007_pains_s3 + q2009_difficulty_s3
recode pain 2/3=0 4=0.25 5/6=0.5 7/8=0.75 9/10=1

** Sleep difficulties
* Changed from previous version: `q2017_rested` is considered in another
* domain later on, so this section takes only into account
* `q2016_sleep`.
recode q2016_sleep_s3  1=0 2=0.25 3=0.5 4=0.75 5=1 888=., gen(sleep)

* Cognitive functions
recode q2525_recal1_s3 q2528_recal2_s3 q2531_recal3_s3 q2534_score_s3 q2535_score_s3 q2536_score_s3 q2544_recall_s3 (999=.)

** Learning and short-term memory (delayed recall task)
xtile  delayed_recall = q2544_recall_s3, n(3)
recode delayed_recall 1=1 2=0.5 3=0

** Learning and short-term memory (immediate recall task)
egen   imm_recall_mean  = rowmean(q2525_recal1_s3 q2528_recal2_s3 q2531_recal3_s3)
xtile  immediate_recall = imm_recall_mean, n(3)
recode immediate_recall 1=1 2=0.5 3=0

** Working Memory (digit span forward and backward tasks)
egen   work_mem_mean  = rowmean(q2534_score_s3 q2535_score_s3)
xtile  working_memory = work_mem_mean, n(3)
recode working_memory 1=1 2=0.5 3=0

** Verbal Fluency
xtile  verbal_fluency = q2536_score_s3, n(3)
recode verbal_fluency 1=1 2=0.5 3=0


* Physical activity (peer week)
merge 1:1 ID_ECS using "~\UAM\Marta Miret Garcia - Documentacion Edad con Salud\Edad con salud - Ola 4\Outcomes\Cohorte 2011\Outcome datasets\Outcome_physicalactivity.dta", keepusing(physical_s3) gen(merge_physical)

generate physical_week = physical_s3 / 2

* Disability and daily life functioning
recode q2003_vigor_s3 q2005_appear_s3 q2006_stay_s3 q2012_relat_s3 q2013_confl_s3 q2014_friend_s3 q2015_strang_s3 q2025_sitting_s3 q2027_stand_s3 q2028_stand_s3 q2029_climb_s3 q2030_crouch_s3 q2031_pickup_s3 q2032_hh_resp_s3 q2033_activ_s3 q2034_reach_s3 q2036_walk_s3 q2037_wash_s3 q2038_dress_s3 q2039_daily_s3 q2040_carry_s3 q2042_eating_s3 q2043_arising_s3 q2044_toilet_s3 q2045_movement_s3 q2046_going_s3 (888=.)


** Self-care activities
gen    self_care_sum = (q2037_wash_s3 + q2038_dress_s3 + q2044_toilet_s3 + q2042_eating_s3 + q2005_appear_s3 + q2006_stay_s3)
xtile  self_care = self_care_sum, n(5)
recode self_care 1=0 2=.25 3=.5 4=.75 5=1

** Mobility

gen    m_score = (q2003_vigor_s3 + q2025_sitting_s3 + q2027_stand_s3 + q2028_stand_s3 + q2029_climb_s3 + q2030_crouch_s3 + q2031_pickup_s3 + q2034_reach_s3 + q2036_walk_s3 + q2040_carry_s3 + q2043_arising_s3 + q2045_movement_s3 + q2046_going_s3)
xtile  mobility = m_score, n(5)
recode mobility 1=0 2=.25 3=.5 4=.75 5=1


** Interpersonal activities

gen    interpersonal_sum = (q2033_activ_s3 + q2012_relat_s3 + q2013_confl_s3 + q2014_friend_s3 + q2015_strang_s3)
xtile  interpersonal = interpersonal_sum, n(5)
recode interpersonal 1=0 2=.25 3=.5 4=.75 5=1


** Difficulties in taking care of household responsibilities

xtile  hh_responsibilities = q2032_hh_resp_s3, n(5)
recode hh_responsibilities 1=0 2=.25 3=.5 4=.75 5=1


** Difficulties in day to day work

xtile  daily_work = q2039_daily_s3, n(5)
recode daily_work 1=0 2=.25 3=.5 4=.75 5=1


** Difficulties in not feeling rested and refreshed during the day

recode q2017_rested_s3 888=.
xtile  rested = q2017_rested_s3, n(5)
recode rested 1=0 2=.25 3=.5 4=.75 5=1


** Learning difficulties

recode q2011_learn_s3 (888=.)
xtile  learning = q2011_learn_s3, n(5)
recode learning 1=0 2=.25 3=.5 4=.75 5=1


** Concentration difficulties

recode q2035_concent_s3 (888=.)
xtile  concentration = q2035_concent_s3, n (5)
recode concentration 1=0 2=.25 3=.5 4=.75 5=1

* Frailty algoritm

egen frailty_score_s3 = rowmean(obesity hypertension stroke oral aid_machines lung diab_s3 angina asthma arthritis cataracts vision grip hearing_room hearing_conv pain sleep delayed_recall immediate_recall working_memory verbal_fluency physical_week mobility self_care interpersonal hh_responsibilities daily_work rested learning concentration)

label variable frailty_score_s3 "Frailty score (c2011_w4)"

gen     frailty_index_s3 = frailty_score_s3
replace frailty_index_s3 = 0 if frailty_index_s3  < 0.25
replace frailty_index_s3 = 1 if frailty_index_s3 >= 0.25 & frailty_index_s3 !=.

label variable frailty_index "Frailty (c2011_w4)"

label define frailty_levels 0 "Non-frail" 1 "Frail"
label values frailty_index frailty_levels

```

<br> 

```{stata descriptives, cleanlog=FALSE, collectcode=FALSE, results='markup', include=!DOUBLE_CODING}
keep ID_ECS q0007a_result_s3 proxy_s3 frailty_index_s3 frailty_score_s3 

codebook frailty_index_s3 frailty_score_s3
```


```{stata save-data-test, eval=TEST}
saveold "Outcome_frailty.dta", replace version(13)
```


```{stata save-data, eval=!TEST}
* Cohorte 2011, ola 4:
saveold "~\UAM\Marta Miret Garcia - Documentacion Edad con Salud\Edad con salud - Ola 4\Outcomes\Cohorte 2011\Outcome datasets\Outcome_frailty.dta", replace version(13)
```

<br>

# Contact person:

-   Name: Cristina Rodríguez Prada _(adapted algorithms for wave 4)_
-   e-mail:
    [cristina.rodriguez\@inv.uam.es](mailto:cristina.rodriguez@inv.uam.es){.email}

<br>



Original version:

-   Name: Viviana Contreras Pizarro
-   e-mail:
    [vcontrer@ucm.es](mailto:vcontrer@ucm.es){.email}

<br>

-   Name: Daniel Morillo Cuadrado
-   e-mail:
    [daniel.morillo@cibersam.es](mailto:daniel.morillo@cibersam.es){.email}

<br>

# References

::: {#refs}
:::

\newpage

# Session info

```{r, results='markup'}
devtools::session_info()
```
