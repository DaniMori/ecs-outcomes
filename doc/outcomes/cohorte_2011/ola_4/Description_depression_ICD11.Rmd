---
title: "Edad con Salud -- cohort 2011 -- wave 4 -- 'depression_ICD11'" 
output:
  word_document:
   reference_docx: !expr ecs.data::get_pkg_docx_template()
bibliography: ../../../../www/Edad_con_salud_outcome_vars.bib 
csl: "`r ecs.data::get_proj_csl_file()`"
editor_options: 
  markdown:
    wrap: 80
params:
  test: true
  double-coding: false
---

```{r setup, include=FALSE}

library(Statamarkdown)

STATA_PATH <- r"(C:/Program Files (x86)/Stata15/StataSE-64.exe)"

options(width = 88) # Ancho de salida de texto en R

knitr::opts_chunk$set( # Para todos los 'chunks' de código:
  engine.path = list(stata = STATA_PATH),
  echo        = FALSE, #   - No muestra la sintaxis en el documento
  results     = 'hide',#   - Omite los resultados de ejecutar la sintaxis
  cleanlog    = TRUE,  #   - Omite repetir sintaxis en resultados de Stata
  collectcode = TRUE,  #   - Reutiliza el código de Stata de 'chunks' anteriores
  comment     = ''     #   - No añade ningún prefijo a los resultados
)

TEST          <- params$test
DOUBLE_CODING <- params$`double-coding` & TEST
```

# Variable information

+------------------+------------------+------------------+------------------+
| Main variable    | Label            | Format           | Code             |
| name             |                  |                  |                  |
+:=================+:=================+:=================+:=================+
| depres           | Lifetime         | Binary           | 0 no             |
| sion_lifetime_s3 | (c2011_w4)       |                  |                  |
|                  |                  |                  | 1 yes            |
+------------------+------------------+------------------+------------------+
| depression_l     | Lifetime         | Binary           | 0 no             |
| ifetime_proxy_s3 | depression for   |                  |                  |
|                  | proxy (c2011_w4) |                  | 1 yes            |
+------------------+------------------+------------------+------------------+

# Variable description

Depression has become a major public health issue as it is one of the main
causes of disability worldwide, with an even greater impact on developed
countries. According to The ICD-10 Classification of Mental and Behavioural
Disorders, Diagnostic criteria for research [@worldhealthorganization1992], the
main characteristics of depression are the presence of a sustained depressed
mood, the loss of interest or pleasure in activities normally pleasurable and/or
the decreased energy or increased fatigability. In addition to these main
criteria, the ICD-10 Diagnostic criteria for research proposes several
contributing factors that can be present or absent: loss of confidence and
self-esteem, feelings of self-reproach or guilt, recurrent thoughts of death or
suicide, diminished ability to think or concentrate, change in psychomotor
activity, sleep disturbance, and changes in appetite with the corresponding
weight change. Moreover, the classification proposes a three-level gradient of
severity depending on the amount of criteria present in each case, from mild to
severe. The ICD-11 [@worldhealthorganization2019] refers to the same symptoms
but strengthens the criteria, making the diagnosis of depression more demanding
than its predecessor. In order to address these criteria and be able to assess
the lifetime presence or absence of a depressive episode, as well as its
severity, we used an adapted version of the Composite International Diagnostic
Interview (CIDI 3.0), a comprehensive, fully-structured interview designed to be
used by trained lay interviewers for the assessment of mental disorders
according to the definitions and criteria of ICD-11 (criteria can been seen at
[ICD-11 for Mortality and Morbidity Statistics
(who.int](https://icd.who.int/browse11/l-m/en#/http%3a%2f%2fid.who.int%2ficd%2fentity%2f578635574)[)](https://icd.who.int/browse11/l-m/en#/http%3a%2f%2fid.who.int%2ficd%2fentity%2f1563440232)
It is intended for use in epidemiological and cross-cultural studies, as well as
for clinical and research purposes.

# Explanation of how the variables were created

Information from the CIDI depression module was used for the creation of the
depression variables. Algorithms based on the ICD-11 were used for the
endorsement of depressive episode at some point in their lives (lifetime
prevalence; variable `depression_lifetime`). More precisely, the following
criteria must be fulfilled:

i)  Symptoms should last at least two weeks;

ii) The clinical presentation does not fulfill the diagnostic requirements for a
    Mixed Episode;

iii) The episode is not attributable to psychoactive substance use or to any
     organic mental disorder

iv) The concurrent presence of at least five of the following symptoms:

    -   **Affective cluster:** (1) Depressed mood, (2) Markedly diminished
        interest or pleasure;
    -   **Cognitive-behavioural cluster:** (3) Reduced ability to concentrate
        and sustain attention, (4) Beliefs of low self-worth or excessive and
        inappropriate guilt, (5) Hopelessness about the future, (6) Recurrent
        thoughts of death;
    -   **Neurovegetative cluster:** (7) Significantly disrupted sleep, (8)
        Significant change in appetite or weight, (9) Psychomotor agitation or
        retardation, (10) Reduce energy, or fatigue;

v)  At least one symptom from the affective cluster (depressed mood, loss of
    interest).

For reasons of precision and availability of information, certain criteria could
not be taken into account: "the symptoms result in significant distress or
significant impairment in personal, family, social, educational, occupational or
other important areas of functioning" and "symptoms are not best explained by
bereavement".

The coding of this algorithm is very complex and requires many different
criteria to be taken into account. We have tried to make use of uniform coding
criteria for implementing the computation of the different symptoms and
diagnostic criteria, and also separating clearly the computation of the
*positive*/*negative*/*missing* cases, to make the code the clearest possible.
The following general principles have been followed when possible:

-   All the non-valid responses are considered to be *negative* responses; that
    is, if an item has been answered with "*Don't know*" (`888`) or "Refused"
    (`999`), it is assumed that the criterion is "not met" (=`0`).

-   Only the variables that are kept in the final dataset are labelled.

-   First, all the cases are set to `.` ("Missing" value).

-   Then, the negative cases are recoded to `0` (diagnostic "No").

-   Finally, the positive cases are recoded to `1` (diagnostic "Yes").

-   Unless stated otherwise, the cases are explicitly recoded to their final
    value exclusively and exhaustively, such that the number of cases recoded to
    `0`/`1` in each step must be exactly the same as the number of `0`/`1` cases
    finally in the variable. This allows for an additional check of recoding
    errors.

-   When the values need to be compared for unequality (operator `!=`) against a
    non-missing value, it must be taken into account that a "missing" value
    gives a `1` result instead of a missing in Stata. Therefore, in order to
    avoid recoding "missing" values into non-missing ones, the "missing" values
    must be excluded from the comparison. This principle is used generally
    throughout the algorithm; when used differently, it is explained in the
    syntax.

-   When these principles are not strictly followed, an annotation is added
    right below, explaining how it changes from them and why.

# Syntax used to create the variable (Stata 15)

```{stata load-data}

* Cohorte 2011, ola 4:
use "~\UAM\Marta Miret Garcia - Bases de datos maestras Edad con Salud\Ola_4\Cohorte 2011\rawdata_c2011w4.dta"

```

```{stata generate-outcomes, echo=!DOUBLE_CODING}
*# "Value labels (defined for all the outcome variables in the dataset)":
label define yesno    0 "No" 1 "Yes"


***#"VARIABLE `depression_lifetime`: Depressive episode (6A70)"

*#Criterion 1: "The depressive episode should last for at least 2 weeks"
gen     d_crit1 = .
replace d_crit1 = 0 if (q4211_s3!=1 & q4211_s3!=.) | (q4213_s3!=1 & q4213_s3!=.)
replace d_crit1 = 1 if  q4211_s3==1             |  q4213_s3==1

*#Criterion 2: "The clinical presentation does not fulfil the diagnostic
*#             requirements for a Mixed Episode"
gen d_crit2 =0
replace d_crit2 = 1 if q4233_s3!=1 & q4225_causemed2_s3!=1
replace d_crit2 = . if q4233_s3==. & q4225_causemed2_s3==.

*#Criterion 3: "The episode is not attributable to psychoactive substance use or
*#             to any organic mental disorder"
gen     d_crit3 = .
replace d_crit3 = 0 if                            q4224b_s3==1              | (q4224_s3==1 & (q4225_causemed12_s3==1 | q4225_causemed23_s3==1 | q4225_causemed24_s3==1 | q4225_causemed25_s3==1 | q4225_causemed30_s3==1 | q4225_causemed31_s3==1))
replace d_crit3 = 1 if (q4224a_s3!=1 & q4224a_s3!=.) | (q4224b_s3!=1 & q4224b_s3!=.) | (q4224_s3==1 & (q4225_causemed12_s3!=1 & q4225_causemed23_s3!=1 & q4225_causemed24_s3!=1 & q4225_causemed25_s3!=1 & q4225_causemed30_s3!=1 & q4225_causemed31_s3!=1))
*#"NOTE: When `q4224b_s3==1` it is necessarily the case that `q4224a_s3==1`,
*#       so only `q4224b` out of these two needs to be checked for considering
*#       the criterion negative."

*#Filling of the three first criteria above (Criterion 1, 2 and 3):
gen     d_crit_123 = .
replace d_crit_123 = 0 if d_crit1==0 | d_crit2==0 | d_crit3==0
replace d_crit_123 = 1 if d_crit1==1 & d_crit2==1 & d_crit3==1
*#"NOTE: The criterion named "d_crit123" includes all the three exclusion criteria
*#       of the ICD11. Those three criteria are coded in different points because
*#       they do not take into account a minimum threshold of symptoms as the two
*#       other bellow (Criterion 4 and Criterion 5)."

*#Criterion 4: "A total of at least five symptoms from the following list should
*#             be present."

*# AFFECTIVE CLUSTER:

*#S1: "Depressed mood to a degree that is definitely abnormal for the
*#    individual, present for most of the day and almost every day, largely
*#    uninfluenced by circumstances, and sustained for at least 2 weeks"
gen     d_s1 = .
replace d_s1 = 0 if (q4220a_s3!=1 & q4220a_s3!=.) & q4220b_s3!=1 & q4220c_s3!=1
replace d_s1 = 1 if  q4220a_s3==1              | q4220b_s3==1 | q4220c_s3==1
*#"NOTE: When `q4220b_s3` or `q4220d_s3` are "missing" it may be due to the 
*# interview skip logic in `q4220a_s3` or `q4220c_s3`, respectively. But if there *# is a response in`q4220a_s3` there must be one also in `q4220c_s3`. Therefore, 
*# only `q4220a_s3` needs to be checked for missingness".

*#S2: "Loss of interest or pleasure in activities that are normally pleasurable
*#    (including sexual desire)"
gen     d_s2 = .
replace d_s2 = 0 if (q4220e_s3!=1 & q4220e_s3!=.) & q4220f_s3!=1
replace d_s2 = 1 if  q4220e_s3==1              | q4220f_s3==1

*# COGNITIVE-BEHAVIOURAL CLUSTER:

*#S3: "Complaints or evidence of diminished ability to think or concentrate,
*#    such as indecisiveness or vacillation"
gen     d_s3 = .
replace d_s3 = 0 if q4222h_s3!=1 & q4222h_s3!=. & q4222i_s3!=1 & q4222j_s3!=1
replace d_s3 = 1 if q4222h_s3==1             | q4222i_s3==1 | q4222j_s3==1
*#"NOTE: When `q4222i_s3` and `q4222j_s3` are 'missing' it may be due to the
*#       interview skip logic in `q4222h_s3`. Therefore, only `q4222h_s3` needs
*#       to be checked for missingness".

*#S4: "Beliefs of low self-worth or excessive and inappropriate guilt that may
*#    be manifestly delusional"
gen     d_s4 = .
replace d_s4 = 0 if ((q4222k_s3!=1 & q4222k_s3!=.) & (q4222l_s3!=1 & q4222l_s3!=.) & q4222m_s3!=1) | (q4222n_s3!=1 & q4222n_s3!=.)
replace d_s4 = 1 if q4222k_s3==1                | q4222l_s3==1               | q4222m_s3==1  |  q4222n_s3==1
*#"NOTE: When `q4222m_s3` is "missing" it may be due to the interview
*#       skip logic in `q4222l_s3`. Therefore, only `q4222k_s3` and `q4222l_s3`
*#       need to be checked for missingness".

*#S5: "Hopelessness about the future."
gen     d_s5 = .
replace d_s5 = 0 if q4220d_s3!=1 & q4220d_s3!=.
replace d_s5 = 1 if q4220d_s3==1
*#"NOTE: Symptom presents in the ICD10 as, (d) bleak and pessimistic views of
*#       the future;"

*#S6: "Recurrent thoughts of death or suicide, or any suicidal behaviour"
gen     d_s6 = .
replace d_s6 = 0 if q4222r_s3!=1 & q4222r_s3!=. & q4222s_s3!=1 & q4222t_s3!=1 & q4222u!=1 & q4222v!=1
replace d_s6 = 1 if q4222r_s3==1             | q4222s_s3==1 | q4222t_s3==1 | q4222u_s3==1 | q4222v_s3==1
*#"NOTE: When any of the two last items are 'missing' it may be due to
*#       the interview skip logic in `q4222t_s3`. But if there is a
*#       response in `q4222r_s3` there must be one also in `q4222s_s3` and
*#       `q4222t_s3`.Therefore, only `q4222r_s3` needs to be checked for
*#       missingness".

*# NEUROVEGETATIVE CLUSTER:

*#S7: "Sleep disturbance of any type"
gen     d_s7 = .
replace d_s7 = 0 if (q4222c_s3!=1 & q4222c_s3!=.) & q4222d_s3!=1
replace d_s7 = 1 if q4222c_s3==1               | q4222d_s3==1
*#"NOTE: When `q4222d_s3` is 'missing' it may be due to the interview skip
*#       logic in `q4222c_s3`. Therefore, only `q4222c_s3` is checked for
*#       missingness".

*#S8: "Change in appetite (decrease or increase) with corresponding weight
*#    change"
gen     d_s8 = .
replace d_s8 = 0 if (q4222a_s3!=1 & q4222a_s3!=.) | (q4222b_s3!=1 & q4222b_s3!=.)
replace d_s8 = 1 if  q4222a_s3==1              &  q4222b_s3==1

*#S9: "Change in psychomotor activity, with agitation or retardation (either
*#    subjective or objective)"
gen     d_s9 = .
replace d_s9 = 0 if (q4222f_s3!=1 & q4222f_s3!=.  & q4222fa_s3!=1) | (q4222g_s3!=1 & q4222ga_s3!=1 & q4222g_s3!=.)
replace d_s9 = 1 if  q4222f_s3==1 |              q4222fa_s3==1  |  q4222g_s3==1 | q4222ga_s3==1
*#"NOTE: When the three last items are 'missing' it may be due to the
*#       interview skip logic in `q4222f_s3`. Therefore, only `q4222f_s3` needs
*#       to be checked for missingness".

*#S10: "Decreased energy or increased fatigability"
gen     d_s10 = .
replace d_s10 = 0 if q4222e_s3!=1 & q4222e_s3!=.
replace d_s10 = 1 if q4222e_s3==1

*#"Sum of depressive symptoms"
egen d_symptot = rowtotal (d_s1 d_s2 d_s3 d_s4 d_s5 d_s6 d_s7 d_s8 d_s9 d_s10), missing

*#"To fulfill Criterion 4, at least five symptoms should be present".
gen     d_crit4 = .
replace d_crit4 = 0 if d_symptot<5 
replace d_crit4 = 1 if d_symptot>=5 & d_symptot!=.


*#Criterion 5: "At least one symptom from the affective cluster must be present"

*#"Sum of depressive symptoms of affective cluster"
egen d_afftotal = rowtotal (d_s1 d_s2), missing

*#"To fulfil criterion 5 at least one symptoms of the affective cluster should
*# be present".
gen     d_crit5 = .
replace d_crit5 = 0 if d_afftotal <1 
replace d_crit5 = 1 if d_afftotal==1 | d_afftotal==2


***#"Has the person skipped the lifetime depression module?"

*#"Depression symptoms recoded into 0/1 to compute the tally":
recode q4220a_s3-q4222v_s3 (1=1) (2=0) (888 999=.)

*#"Filter variables":
gen  f4217     = q4214_s3==1 & q4215_s3==4 & q4216_s3==4 if q4214_s3!=.
egen f4221_sum = rowtotal (q4220a_s3-q4220f_s3), missing
egen f4223_sum = rowtotal (q4220a_s3-q4222v_s3), missing

*#"Variable "skipped" computation":
gen     d_skipped = .
replace d_skipped = q4202_s3!=1                   if q4202_s3!=.
replace d_skipped = q4211_s3!=1    | d_skipped==1 if q4211_s3!=.    
replace d_skipped = q4213_s3!=1    | d_skipped==1 if q4213_s3!=.    
replace d_skipped = q4218_s3!=1    | d_skipped==1 if q4218_s3!=.    
replace d_skipped = f4217==1    | d_skipped==1 if f4217!=.    
replace d_skipped = f4221_sum<1 | d_skipped==1 if f4221_sum!=.
replace d_skipped = f4223_sum<2 | d_skipped==1 if f4223_sum!=.
*#"NOTE: For clarity, this recoding adds the criteria incrementally. This
*#       implies that the new "truth values" (0/1 resulting from each
*#       comparison) are "logically added" (therefore the "OR" operation)
*#       to the current values".


***#"Lifetime depression diagnostic variable":
gen     depression_lifetime_s3 = .
replace depression_lifetime_s3 = 0 if d_skipped==1 | d_crit_123==0 | d_crit4==0 | d_crit5==0
replace depression_lifetime_s3 = 1 if d_skipped==0 & d_crit_123==1 & d_crit4==1 & d_crit5==1
label values   depression_lifetime_s3 yesno
label variable depression_lifetime_s3 "Lifetime depression (c2011_w4)"


***#"VARIABLE `depression_lifetime_proxy_s3`: Depressive episode for proxy participants"
recode p4040_depression_s3 (1=1) (2=0) (888=0) (999=0), gen (depression_lifetime_proxy_s3)
label values   depression_lifetime_proxy_s3 yesno
label variable depression_lifetime_proxy_s3 "Lifetime depression for proxy (c2011_w4)"

```

<br>

```{stata descriptives, cleanlog=FALSE, collectcode=FALSE, results='markup', include=!DOUBLE_CODING}

keep ID_ECS q0007a_result proxy depression_lifetime_s3 depression_lifetime_proxy_s3

codebook depression_lifetime_s3 depression_lifetime_proxy_s3
```

```{stata save-data-test, eval=TEST}

saveold "Outcome_depression_ICD11.dta", replace version(13)
```

```{stata save-data, eval=!TEST}

* Cohorte 2011, ola 4:
saveold "~\UAM\Marta Miret Garcia - Documentacion Edad con Salud\Edad con salud - Ola 4\Outcomes\Cohorte 2011\Outcome datasets\Outcome_depression_ICD11.dta", replace version(13)

```

<br>

# Contact person:

-   Name: Lea Francia
-   e-mail: [lea.francia\@inv.uam.es](mailto:lea.francia@inv.uam.es){.email}
-   Phone number: (+34) 914974601

Original version:

-   Name: Dario Moreno Agostino
-   e-mail: [d.moreno\@ucl.ac.uk](mailto:d.moreno@ucl.ac.uk){.email}

<br>

-   Name: Elvira Lara
-   e-mail: [elvira.lara\@uam.es](mailto:elvira.lara@uam.es){.email}
-   Phone number: (+34) 914974601

<br>

-   Name: Daniel Morillo
-   e-mail:
    [daniel.morillo\@inv.uam.es](mailto:daniel.morillo@inv.uam.es){.email}
-   Phone number: (+34) 914975816

# References

::: {#refs}
:::

\newpage

# Session info

```{r, results='markup'}
devtools::session_info()
```
