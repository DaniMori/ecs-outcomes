---
title: "Edad con Salud -- cohort 2011 -- Wave 3 -- 'Depression DSM-5'"
output:
  word_document:
    reference_docx: !expr ecs.data::get_pkg_docx_template()
  pdf_document: default
csl: "`r ecs.data::get_proj_csl_file()`"
bibliography: "`r ecs.data::get_proj_bib_file()`"
editor_options:
  markdown:
    wrap: 80
---

```{r setup, include=FALSE}
# Activar los paquetes que R necesita para ejecutar el código:

library(Statamarkdown)

# Ruta al ejecutable de Stata (Ejecuta `sysdir` en Stata para encontrar la ruta)

# Opciones de configuración de las salidas de R y Stata:

knitr::opts_chunk$set( # Para todos los 'chunks' de código:
  echo        = FALSE, #   - No muestra la sintaxis en el documento
  results     = 'hide',#   - Omite los resultados de ejecutar la sintaxis
  cleanlog    = TRUE,  #   - Omite repetir sintaxis en resultados de Stata
  collectcode = TRUE   #   - Reutiliza el código de Stata de 'chunks' anteriores
)
```

# Variable information

+---------------------------+----------------+------------+------------+
| **Main variable name**    | **Label**      | **Format** | **Code**   |
+:==========================+:===============+============+============+
| depression_lifetime_s2    | Lifetime       | Binary     | -   0=no;  |
|                           | depression     |            | -   1=yes  |
|                           | (c2011_w3)     |            |            |
+---------------------------+----------------+------------+------------+
| depression_12m_s2         | 12 month       | Binary     | -   0=no;  |
|                           | depression     |            | -   1=yes  |
|                           | (c2011_w3)     |            |            |
+---------------------------+----------------+------------+------------+
| depression_12treatment_s2 | Treatment for  | Binary     | -   0=no;  |
|                           | depression in  |            | -   1=yes  |
|                           | previous 12    |            |            |
|                           | monts          |            |            |
|                           | (c2011_w3)     |            |            |
+---------------------------+----------------+------------+------------+
| depress_self_s2           | depression,    | Binary     | -   0=no;  |
|                           | self reported  |            | -   1=yes  |
|                           | (c2011_w3)     |            |            |
+---------------------------+----------------+------------+------------+

# Variable description

Depression is a common mental disorder, characterized by sadness, loss of
interest or pleasure, feelings of guilt or low self-worth, disturbed sleep or
appetite, feelings of tiredness, and poor concentration. Depression can be
long-lasting or recurrent, substantially impairing an individual's ability to
function at work or school or cope with daily life. At its most severe,
depression can lead to suicide. When mild, people can be treated without
medicines but when depression is moderate or severe they may need medication and
professional talking treatments. Depression is a disorder that can be reliably
diagnosed and treated by non-specialists as part of primary health care.
Specialist care is needed for a small proportion of individuals with complicated
depression or those who do not respond to first-line treatments.

We used an adapted version of the Composite International Diagnostic Interview
(CIDI 3.0), a comprehensive, fully-structured interview designed to be used by
trained lay interviewers for the assessment of mental disorders according to the
definitions and criteria of DSM-V [@americanpsychiatricassociation2013]. It is
intended for use in epidemiological and cross-cultural studies as well as for
clinical and research purposes. The diagnostic section of the interview is based
on the World Health Organization's Composite International Diagnostic Interview.

# Explanation of how the variables were created

An algorithm based on the DSM-V was used for the endorsement of major depressive
episode at some point in their lives (lifetime prevalence) and in the previous
12 months (point prevalence). We also included a treatment variable for 12-month
depression. These algorithms were created only for the non-proxy respondents.
Although a variable (`depression_self_s2`) was created to assess an eventually
presence of depression episode self-reported.

As for the proxy participants, original variables include `p4040_depression_s2`
and `p4040a_age_s2`. A diagnosis of depression was endorsed if the participant
answered "yes" to `p4040_depression_s2`, but not `997` in `p4040a_age_s2` (they
affirmed they had never had depression). We also included a treatment code
(`depress_12treatment_s2`) in those individuals who, apart from having reported
depression, have received treatment for it in the previous 12 months.

More precisely, the following criteria must be fulfilled: A) At least five of
the following symptoms have been present during the same 2-week period and
represent a change from previous functioning; (1) depressed mood, (2) loss of
interest or pleasure, (3) significant weight loss, (4) insomnia or hypersomnia,
(5) psychomotor agitation or retardation, (6) fatigue or loss of energy, (7)
feelings of worthlessness, (8) diminished ability to think or concentrate, (9)
recurrent thoughts of death. At least one of the symptoms is either (1)
depressed mood or (2) loss of interest or pleasure. Note: Do not include
symptoms that are clearly attributable to another medical condition. B) The
symptoms cause clinically significant distress or impairment in social,
occupational, or other important areas of functioning.; C) The episode is not
attributable to the physiological effects of a substance or to another medical
condition. ; D) The occurrence of the major depressive episode is not better
explained by schizoaffective disorder, schizophrenia, schizophreniform disorder,
delusional disorder, or other specified and unspecified schizophrenia spectrum
and other psychotic disorders.; E) There has never been a manic episode or a
hypomanic episode. Those criteria where used to created the variable related to
lifetime depression (`depression_lifetime_s2`). We also created a variable to
assess the presence of a depressive episode during the last 12 months using only
one item (`q4227_s2`) which directly questions the presence of a depressive
episode during this 12-month period.

The coding of this algorithm is very complex and requires many different
criteria to be taken into account. We have tried to make use of uniform coding
criteria for implementing the computation of the different symptoms and
diagnostic criteria, and also separating clearly the computation of the
positive/negative/missing cases, to make the code the clearest possible. The
following general principles have been followed when possible: • All the
non-valid responses are considered to be negative responses; that is, if an item
has been answered with "Don't know" (888) or "Refused" (999), it is assumed that
the criterion is "not met" (=0).• When an item hasn't been answered or has been
"passed" (97), it is considered as a missing value (=.). Only the variables that
are kept in the final dataset are labelled. • First, all the cases are set to .
("Missing" value). • Then, the negative cases are recoded to 0 (diagnostic
"No"). • Finally, the positive cases are recoded to 1 (diagnostic "Yes"). •
Unless stated otherwise, the cases are explicitly recoded to their final value
exclusively and exhaustively, such that the number of cases recoded to 0/1 in
each step must be exactly the same as the number of 0/1 cases finally in the
variable. This allows for an additional check of recoding errors. • When the
values need to be compared for unequality (operator !=) against a non-missing
value, it must be taken into account that a "missing" value gives a 1 result
instead of a missing in Stata. Therefore, in order to avoid recoding "missing"
values into non-missing ones, the "missing" values must be excluded from the
comparison. This principle is used generally throughout the algorithm; when used
differently, it is explained in the syntax. • When these principles are not
strictly followed, an annotation is added right below, explaining how it changes
from them and why.

# Syntax used to create the variable (Stata 15)

```{stata load-data}
* Cohorte 2011, ola 3:
use "~\UAM\Marta Miret Garcia - Bases de datos maestras Edad con Salud\Ola_3\Cohorte_2011\FINAL\20190208_PES16028242_Seguimiento_final_completo_V6.dta"
```

```{stata generate-outcomes, echo=TRUE}
*#"Value labels (defined for all the outcome variables in the dataset)":
label define yesno  0 "No" 1 "Yes"

***#"VARIABLE `depression_lifetime_s2`: Major depressive episode"

*#A criterion:"At least five of the following symptoms have been present during the same 2-week"
  
*#A1:"Depressed mood most of the day, nearly every day, as indicated by either subjective report or observation made by others" 
gen     d_A1 = .
replace d_A1 = 0 if q4220a!=1 & q4220a!=. & q4220b!=1 & q4220c!=1 & q4220d!=1
replace d_A1 = 1 if q4220a==1             | q4220b==1 | q4220c==1 | q4220d==1

*#A2:"Markedly diminished interest or pleasure in all, or almost all, activities most of the day nearly every day" 
gen     d_A2 = .
replace d_A2 = 0 if q4220e!=1 & q4220e!=. & q4220f!=1
replace d_A2 = 1 if q4220e==1 | q4220f==1
  
*#A3:"Significant weight loss when not dieting, or weight gain or decrease or increase in appetite nearly every day"
gen     d_A3 = .
replace d_A3 = 0 if (q4222a!=1 & q4222a!=.) | (q4222b!=1 & q4222b!=.)
replace d_A3 = 1 if  q4222a==1 | q4222b==1

*#A4:"Insomnia or hypersomnia nearly every day" 
gen     d_A4 = .
replace d_A4 = 0 if q4222c!=1 & q4222c!=.
replace d_A4 = 1 if q4222c==1 | q4222d==1           

*#A5:"Psychomotor agitation or retardation nearly every day" 
gen     d_A5 = .
replace d_A5 = 0 if (q4222f_s2!=1 & q4222f_s2!=. & q4222fa!=1) | (q4222g_s2!=1 & q4222ga!=1 & q4222g_s2!=.)
replace d_A5 = 1 if q4222f_s2==1 | q4222fa==1 | q4222g_s2==1 | q4222ga==1

*#A6:"Fatigue or loss of energy nearly every day" 
gen     d_A6 = .
replace d_A6 = 0 if q4222e!=1 & q4222e!=.
replace d_A6 = 1 if q4222e==1

*#A7:"Feelings of worthlessness or excessive or inappropriate guilt nearly every day (not merely self-reproach or guilt about being sick)"
gen     d_A7 = .
replace d_A7 = 0 if q4222n!=1 & q4222n!=.
replace d_A7 = 1 if q4222n==1

*#A8:"Diminished ability to think or concentrate, or indecisiveness, nearly every day"
gen     d_A8 = .
replace d_A8 = 0 if q4222h!=1 & q4222h!=. & q4222i!=1 & q4222j!=1
replace d_A8 = 1 if q4222h==1 | q4222i==1 | q4222j==1

*#A9:"Recurrent thoughts of death (not just fear of dying), recurrent suicidal ideation without a specific plan, or a suicide attempt or a specific plan for committing suicide"
gen     d_A9 = .
replace d_A9 = 0 if q4222r!=1 & q4222r!=. & q4222s!=1 & q4222t!=1 & q4222u!=1 & q4222v!=1
replace d_A9 = 1 if q4222r==1 | q4222s==1 | q4222t==1 | q4222u==1 | q4222v==1

*#"To fulfil A criterion at least 5 screening symptoms should be present"
egen d_symtotal = rowtotal (d_A1-d_A9), missing

*#"At least one of following symptom must be present, (A1) depressed mood, (A2) loss of interest or pleasure"
gen     d_symprior = .
replace d_symprior = 0 if d_A1==0 & d_A2==0
replace d_symprior = 1 if d_A1==1 | d_A2==1

*#"Total of A criterion"  
gen     d_crit_A = .
replace d_crit_A = 0 if d_symtotal<5 | d_symprior==0 
replace d_crit_A = 1 if (d_symtotal>4 & d_symtotal!=.) & d_symprior==1 


*#Bcriterion:"The symptoms cause clinically significant distress or impairment in social, occupational, or other important areas of functioning"

*#"Follow of A critierion"
recode q4228a (1=1) (2=1) (3=1) (4=1) (5=1) (6=1) (7=1) (8=1) (9=1) (10=1) (0=0) (888=0) (999=0) (97=.)
recode q4228b (1=1) (2=1) (3=1) (4=1) (5=1) (6=1) (7=1) (8=1) (9=1) (10=1) (0=0) (888=0) (999=0) (97=.)
recode q4228c (1=1) (2=1) (3=1) (4=1) (5=1) (6=1) (7=1) (8=1) (9=1) (10=1) (0=0) (888=0) (999=0) (97=.)
recode q4228d (1=1) (2=1) (3=1) (4=1) (5=1) (6=1) (7=1) (8=1) (9=1) (10=1) (0=0) (888=0) (999=0) (97=.)

recode q4214_s2 (1=0) (2=1) (3=1) (4=1) (888=0) (999=0)
recode q4215_s2 (1=1) (2=1) (3=0) (4=0) (888=0) (999=0)
recode q4216_s2 (1=0) (2=1) (3=1) (4=0) (888=0) (999=0)

gen     d_crit_B = .
replace d_crit_B = 0 if q4228a==0 | q4228b==0 | q4228c==0 | q4228d==0 | q4214_s2==0 | q4215_s2==0 | q4216_s2==0
replace d_crit_B = 1 if q4228a==1 | q4228b==1 | q4228c==1 | q4228d==1 | q4214_s2==1 | q4215_s2==1 | q4216_s2==1

*#"NOTE:In the absence of certainty and information on this subject found in the DSM-5, it is considered that the symptoms affect the daily life of the individual as soon as a change in behaviour and activity is perceived." 
  
  
  
*#C criterion:"The episode is not attributable to the physiological effects of a substance or to another medical condition"
gen     d_crit_C = .
replace d_crit_C = 0 if                            q4224b==1              | (q4224_s2==1 & (q4225_causemed12==1 | q4225_causemed23==1 | q4225_causemed24==1 | q4225_causemed25==1 | q4225_causemed30_s2==1 | q4225_causemed31_s2==1))
replace d_crit_C = 1 if (q4224a!=1 & q4224a!=.) | (q4224b!=1 & q4224b!=.) | (q4224_s2==1 & (q4225_causemed12!=1 & q4225_causemed23!=1 & q4225_causemed24!=1 & q4225_causemed25!=1 & q4225_causemed30_s2!=1 & q4225_causemed31_s2!=1))
  

 
***#"Has the person skipped the lifetime depression module?"

*#"Depression symptoms recoded into 0/1 to compute the tally":
recode q4220a-q4222v (1=1) (2=0) (888 999=.)

*#"Filter variables":
gen  f4217     = q4214==1 & q4215==4 & q4216==4 if q4214!=.
egen f4221_sum = rowtotal (q4220a-q4220f), missing
egen f4223_sum = rowtotal (q4220a-q4222v), missing

*#"Variable "skipped" computation":
gen     d_skipped = .
replace d_skipped = q4202!=1                   if q4202!=.
replace d_skipped = q4211!=1    | d_skipped==1 if q4211!=.    
replace d_skipped = q4213!=1    | d_skipped==1 if q4213!=.    
replace d_skipped = q4218!=1    | d_skipped==1 if q4218!=.    
replace d_skipped = f4217==1    | d_skipped==1 if f4217!=.    
replace d_skipped = f4221_sum<1 | d_skipped==1 if f4221_sum!=.
replace d_skipped = f4223_sum<2 | d_skipped==1 if f4223_sum!=.
*#"NOTE: For clarity, this recoding adds the criteria incrementally. This
*#       implies that the new "truth values" (0/1resulting from each
*#       comparison) are "logically added" (therefore the "OR" operation)
*#       to the current values".



***#"Major depressive episode (DSM-5)"
gen     depression_lifetime_s2 = .
replace depression_lifetime_s2 = 0 if d_skipped==1 | d_crit_A==0 | d_crit_B==0 | d_crit_C==0
replace depression_lifetime_s2 = 1 if d_skipped==0 & d_crit_A==1 & d_crit_B==1 & d_crit_C==1
label values   depression_lifetime_s2 yesno
label variable depression_lifetime_s2 "Lifetime depression (c2011_w3)"


***#"VARIABLE `depression_12m_s2`: 12-month depression"
gen     depression_12m_s2 = .
replace depression_12m_s2 = 0 if q4227!=1 & proxy==2
replace depression_12m_s2 = 1 if q4227==1
label values   depression_12m_s2 yesno
label variable depression_12m_s2 "12 month depression, one item (c2011_w3)"


***#"VARIABLE `depression_12treatment_s2`: treatment for depression, 12-months"
recode q4231_s2 (1=1) (2=0) (888=0) (999=0)
recode q4232_s2 (1=1) (2=0) (888=0) (999=0)

gen     depression_12treatment_s2 = .
replace depression_12treatment_s2 = 0 if (depression_12m_s2==0 & (q4231_s2==0 & q4232_s2==0)) | proxy==2
replace depression_12treatment_s2 = 1 if depression_12m_s2==1 & (q4231_s2==1 | q4232_s2==1)
label values   depression_12treatment_s2 yesno
label variable depression_12treatment_s2 "received depression's treatment in the previous 12 months (c2011_w3)"
*#"NOTE: We take into acount psychological treatment and pharmacological treatment.

***#"VARIABLE `depress_self_s2`: depression self reported"
recode p4040_depression_s2 (1=1) (2=0) (888=0) (999=0)
gen     depress_self_s2 = .
replace depress_self_s2 = 0 if p4040_depression_s2!=1 & p4040_depression_s2!=.
replace depress_self_s2 = 1 if p4040_depression_s2==1 & p4040a_age_s2!=997
label values   depress_self_s2 yesno
label variable depress_self_s2 "diagnosis of depression"
```

<br>

```{stata descriptives, collectcode=FALSE, results='markup'}
codebook depression_lifetime_s2 depression_12m_s2 depression_12treatment_s2 depress_self_s2
```

<br>

```{stata save-data}
keep depression_lifetime_s2 depression_12m_s2 depression_12treatment_s2 depress_self_s2

* Cohorte 2011, ola 3:
saveold "~\UAM\Marta Miret Garcia - Documentacion Edad con Salud\Edad con salud - Ola 3\Outcomes\Cohorte 2011\Outcome datasets\Outcome_depression_DSM5.dta", replace version(13)

```

<br>

# Contact person:

-   Name: Lea Francia
-   e-mail: [lea.francia\@inv.uam.es](mailto:lea.francia@inv.uam.es)

# References

::: {#refs}
American
Psychiatric Association. (2013). Diagnostic and statistical manual of mental
disorders (5th ed.). <https://doi.org/10.1176/appi.books.9780890425596>

Freeman,
A., Tyrovolas, S., Koyanagi, A. et al. The role of socio-economic status in
depression: results from the COURAGE (aging survey in Europe). BMC Public Health
**16**, 1098 (2016). <https://doi.org/10.1186/s12889-016-3638-0>
:::

\newpage

# Session info

```{r, results='markup'}
devtools::session_info()
```
