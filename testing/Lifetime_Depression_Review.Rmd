---
title: Revisión de la variable "Lifetime Depression (CIE-10)" de Chorte 2019, Ola 1
date: "`r lubridate::today()`"
output:
  html_document:
    toc:           yes
    toc_float:     yes
    toc_depth:       4
    code_download: yes
---

```{r setup, include=FALSE}
# Activar los paquetes que R necesita para ejecutar el código:

library(Statamarkdown)

# Opciones de configuración de las salidas de R y Stata:

knitr::opts_chunk$set(
  results     = 'markup',
  cleanlog    = TRUE,
  collectcode = TRUE
)
```

# Introducción

Este documento muestra una revisión de los criterios diagnósticos de depresión
(a lo largo de toda la vida previa a la entrevista) en la Ola 1 (línea base) de
la Cohorte 2019 del Estudio "Edad con Salud".
Este diagnóstico está basado en los criterios de la CIE-10 (ICD-10), utilizando
el instrumento Composite International Diagnostic Interview
(CIDI 3.0)

La motivación para hacer esta revisión es el haber detectado la necesidad de
revisar algunos de los criterios diagnósticos que podrían estar siendo aplicados
de manera ambigua, al asignarse valores positivos/negativos a los criterios
cuando es posible que se debiera aplicar un "missing" (valor perdido).
Al hacer esta revisión, se ha detectado a su vez discrepancias entre los valores
originales y los revisados, hallándose valores positivos donde parece que
deberían ser negativos, y viceversa.

# Descripción de la variable

Según el documento de descripción de la variable original:

> According to The ICD-10 Classification of Mental and Behavioural Disorders,
> Diagnostic criteria for research (World Health Organization, 1993),
> the main characteristics of depression are the presence of
> a sustained depressed mood,
> the loss of interest or pleasure in activities normally pleasurable and/or
> the decreased energy or increased fatigability.
> In addition to these main criteria, the ICD-10 Diagnostic criteria
> for research proposes several contributing factors
> that can be present or absent:
> loss of confidence and self-esteem, feelings of self-reproach or guilt,
> recurrent thoughts of death or suicide,
> diminished ability to think or concentrate,
> change in psychomotor activity, sleep disturbance, and
> changes in appetite with the corresponding weight change.
> Moreover, the classification proposes a three-level gradient of severity
> depending on the amount of criteria present in each case, from mild to severe.

# Explicación de la creación de la variable

Según el documento de descripción de la variable original:

> Information from the CIDI depression module was used for the creation of
> the depression variables.
> Algorithms based on the ICD-10 were used for the endorsement of
> depressive episode at some point in their lives
> (lifetime prevalence; variable `depression_lifetime`) [...].
> More precisely, the following criteria should be fulfilled:
> 
> 1. The depressive episode should last for at least 2 weeks;
> 
> 1. There have been no hypomanic or manic symptoms sufficient to meet
>    the criteria for hypomanic or manic episode at any time
>    in the individual's life;
>    
> 1. The episode is not attributable to psychoactive substance use or to any
>    organic mental disorder;
>    
> 1. The general criteria for depressive episode must be met;
> 
> 1. At least two of the following three symptoms must be present:
>    Depressed mood, loss of interest, and decreased energy;
>    
> 1. An additional symptom or symptoms should be present,
>    to give a total of at least four (please see criteria C1-C7).
>    
> We also included a severity variable according to the ICD-10 criteria for
> [...] lifetime prevalence (variable `d_lifetime_severity`) [...],
> based upon the number of symptoms the participant met.

# Versión "conservadora" en la asignación del diagnóstico

Esta revisión considera que los criterios diagnóticos no se pueden aplicar
cuando falta información para determinar si un criterio es positivo o negativo.
Esto se puede dar cuando, a lo largo del instrumento, el/la participante da
respuestas no válidas (i.e., "DK" o "Refuse").

## Criterios seguidos para la codificación

Se han utilizado los siguientes criterios para hacer los cambios en el algoritmo
de cálculo de la variable:

- Tener en cuenta los filtros en el cuestionario de forma **estricta**;
  es decir, asumir que si un ítem ha sido saltado,
  considerar de manera explícita que su valor va a ser un "missing".

- Utilizar lógica triestado para el diagnóstico (i.e., calcular explícitamente
  los "missing" donde sea necesario).

- Por consistencia, generar siempre las variables nuevas como "missing"
  en primer lugar, y después calcular los valores no perdidos.

- Por consistencia en el algoritmo, calcular primero los valores _negativos_
  de los diagnósticos / síntomas siempre, y después los _positivos_ (a no ser
  que se indique otra cosa).
  
- Asegurar que tanto los valores positivos como negativos se calculan de manera
  estricta. Es decir, que no se codifican inicialmente casos como negativos para
  después recodificarlos como positivos (a no ser que se indique otra cosa).

- Comprobar que _el número total de casos positivos/negativos_ coincide con el
  número de casos generados, de manera que sea indiferente el orden en el que se
  calculen los valores positivos/negativos (a no ser que se indique otra cosa).
  Este criterio junto con los dos anteriores permite comprobar y asegurar que
  las instrucciones para el cálculo de los valores positivos/negativos
  es correcto.

## Sintaxis desglosada (Stata 13)

En este apartado se muestra la sintaxis por bloques, mostrando el resultado del
cálculo de cada variable auxiliar utilizada para el cálculo final.

En cada bloque de código se hace el cálculo tanto de la versión original como
de la revisada de cada variable.
Para evitar el solapamiento de las variables y errores en la ejecución,
a las versiones originales se les ha añadido el sufijo `_org`.
Después, se muestra para cada par de variables (original y revisada)
la tabla de contingencia entre los valores de ambas, permitiendo así examinar
las discrepancias entre ambas formas de hacer el cálculo, si las hay.
(Fijarse que cuando todos los valores fuera de la diagonal son ceros,
todos los casos coinciden entre las dos versiones.)
En algunas ocasiones, se muestran otras tablas de contingencia para explorar
la relación entre las variables generadas y las variables "en bruto".

```{stata load-data, collectcode=TRUE, results='hide'}
* Cohorte 2019, linea base:
use "~\UAM\marta.miret@uam.es - Bases de datos maestras Edad con Salud\Ola_3\Cohorte_2019\rawdata_c2019w1.dta"
```

```{stata initial-recoding, collectcode=TRUE}
*Previous recoding of the symptomatology questions
recode q4220a-q4222v (1=1) (2=0) (888 999=.)
```

### Criterio A

Se ha de cumplir el criterio general para un episodio depresivo.
Este criterio implica que han de darse **todos** los tres criterios G
a continuación.

#### Criterio G1

El criterio es _negativo_ cuando tanto `q4211` como q4213 son "NO" (`=2`);
**SIN EMBARGO**, la entrevista salta uno de los dos ítems o el otro, de forma
que **NUNCA** se va a dar el caso de que ambos sean iguales a "2"
(sólo uno de ellos puede ser un "2", mientras que el otro sera "missing").
Por lo tanto, el criterio es _negativo_ cuando uno de los dos ítems es "2" y
_positivo_ cuando uno de los ítems es "1" (y "missing" en otro caso).

```{stata criterion-g1}
*G1 criterion: "The depressive episode should last for at least 2 weeks"

* # Original:
gen d_crit_g1_org = .
replace d_crit_g1_org = 0 if q4211!=1 & q4213!=1
replace d_crit_g1_org = 1 if q4211==1 | q4213==1

* # Revision:
gen d_crit_g1 = .
replace d_crit_g1 = 0 if q4211==2 | q4213==2
replace d_crit_g1 = 1 if q4211==1 | q4213==1

tab d_crit_g1_org d_crit_g1, missing
```

Todos los "missing" se codifican como "0" en el algoritmo original.

#### Criterio G2

La explicación de cómo se ha implementado es similar a la del
[criterio G1](#criterio-g1);
la única diferencia es que si `q4225_causemed2` no se ha seleccionado, su valor
puede ser "0" o "missing", por lo que se tiene en cuenta que sea distinto de 1
para determinar si el valor de este criterio es "0"
(pero sólo cuando `q4224==1`, ya que de otro modo tendríamos que
se cumple cuando este ítem se ha saltado).

```{stata criterion-g2}
*G2 criterion: "There have been no hypomanic or manic symptoms sufficient to meet the criteria for hypomanic or manic episode at any time in the individual's life"

* # Original:
gen d_crit_g2_org = 1
replace d_crit_g2_org = 0 if q4233==1 | q4225_causemed2==1

* # Revision:
gen d_crit_g2 = .
replace d_crit_g2 = 0 if q4233==1 | q4225_causemed2==1
replace d_crit_g2 = 1 if q4233==2 & q4225_causemed2!=1 & q4224==1

tab d_crit_g2_org d_crit_g2, missing
```

Todos los "missing" se codifican como "1" en el algoritmo original.

#### Criterio G3

El criterio es _positivo_ cuando hay al menos algún episodio que no puede ser
atribuido a causas físicas (es decir, o bien ningún episodio puede ser atribuido
a causas físicas, i.e. `q4224a` es "No", o bien los episodios **NO SIEMPRE**
fueron atribuibles a cusas físicas, i.e. `q4224b` es "No").
Cuando se consultó a un médico, se consider el caso de que se dio _ALGUNA_
las causas físicas (por lo que no se cumple el criterio),
o bien no se dió ninguna.
En este último caso, es necesario considerar que la "causa médica" es
"distinta de 1", ya que se dan casos en los que está codificada como "0" y otros
en los que está codificada como "missing".

```{stata criterion-g3}
*G3 criterion: "The episode is not attributable to psychoactive substance use or to any organic mental disorder"

* # Original:
gen d_crit_g3_org = 0
replace d_crit_g3_org = 1 if (q4224a!=1 | q4224b!=1) | (q4224==1 & q4225_causemed12!=1 & q4225_causemed23!=1 & q4225_causemed24!=1 & q4225_causemed25!=1 & q4225_causemed30!=1 & q4225_causemed31!=1)
replace d_crit_g3_org = . if q4224a==. & q4224b==. & q4224==. & q4225_causemed12==. & q4225_causemed23==. & q4225_causemed24==. & q4225_causemed25==. & q4225_causemed30==. & q4225_causemed31==.

* # Revision:
gen d_crit_g3 = .
replace d_crit_g3 = 0 if (q4224a==1 & q4224b==1) | (q4224==1 & (q4225_causemed12==1 | q4225_causemed23==1 | q4225_causemed24==1 | q4225_causemed25==1 | q4225_causemed30==1 | q4225_causemed31==1))
replace d_crit_g3 = 1 if (q4224a==2 | q4224b==2) | (q4224==1 & (q4225_causemed12!=1 & q4225_causemed23!=1 & q4225_causemed24!=1 & q4225_causemed25!=1 & q4225_causemed30!=1 & q4225_causemed31!=1))

tab d_crit_g3_org d_crit_g3, missing
tab q4224a d_crit_g3_org, missing
```

Hay 5 casos "missing" que originalmente se codifican como "1";
corresponden a respuestas "DK" o "Refuse" en `q4224a` (ver segunda tabla).
Los 23 casos que son "1" en el algoritmo original y "0" en el revisado
corresponden a casos en los que `q4224==1`; cuando se da esta condición, tanto
`q4224a` como `q4224b` son "missing" (y por tanto distintos de "1") por lo que
se cumple siempre la condición del primer criterio de reemplazo,
haciendo que los valores de las causas médicas sean irrelevantes.

#### Cálculo final del criterio A

El criterio es _positivo_ cuando se cumplen **los tres** criterios G1-G3.
Cuando **alguno** de ellos no se cumple (== "0"), entonces el criterio general
**NO** se cumple (i.e. los otros dos no importan).

```{stata criterion-a}
***Mild depressive episode (F32.0)

*A criterion: "The general criteria for depressive episode must be met"

* # Original:
gen d_crit_a_org = .
replace d_crit_a_org = 0 if d_crit_g1_org!=1 | d_crit_g2_org!=1 | d_crit_g3_org!=1
replace d_crit_a_org = 1 if d_crit_g1_org==1 & d_crit_g2_org==1 & d_crit_g3_org==1

* # Revision:
gen d_crit_a = .
replace d_crit_a = 0 if d_crit_g1==0 | d_crit_g2==0 | d_crit_g3==0
replace d_crit_a = 1 if d_crit_g1==1 & d_crit_g2==1 & d_crit_g3==1

tab d_crit_a_org d_crit_a, missing
```

Hay 2528 valores "missing" que se codifican como "0" y 7 como "1"
en el algoritmo original.
Debido a las diferencias entre los resultados en G3,
hay diferencias en 16 casos que originalmente son codificados como "1" y
en la revisión como "0".

### Criterio B

Se ha de cumplir el criterio B, consistente en que se den _al menos_ dos de los
tres síntomas B a continuación.

#### Criterio B1

La explicación para el cálculo es la siguiente: Cuando **ALGUNA** de
las respuestas es _positiva_, el sínoma es positivo.
Cuando hay una respuesta _negativa_ Y una respuesta "missing"
(en los ítems `q4220a` y `q4220b`), el síntoma debería ser "missing",
de acuerdo a este criterio. **SIN EMBARGO**, si tenemos en cuenta los filtros
del cuestionario, parece que una respuesta _negativa_ en `q4220a` salta el ítem
`q4220b` (presumiblemente porque se asume también una respuesta _negativa_),
por lo que este patrón de respuestas (`q4220a=0`, `q4220b=.`) debería entenderse
como un _negativo_ en el síntoma.
Algo similar pasa cuando la respuesta a `q4220a` es "missing"
(se respondió "DK" o "Refuse") y a `q4220b` _negativa_.
Por tanto, asumimos que **CUALQUIER** respuesta negativa en uno de estos dos
ítems implica un síntoma negativo (en principio, ya que puede que el otro sí
tenga una respuesta positiva, por lo que se recodificaría a _positivo_ en la
siguiente instrucción).
Lo mismo ocurre con los ítems `q4220c` y `q4220d`.
En conclusión, para que el síntoma sea _negativo_, tanto
**UNA DE LAS DOS PRIMERAS RESPUESTAS COMO UNA DE LAS DOS ÚLTIMAS** debe ser
_negativa_.

```{stata criterion-b1}
*B criterion: "At least two of the following three symptoms must be present"

*B1: "Depressed mood to a degree that is definitely abnormal for the individual, present for most of the day and almost every day, largely uninfluenced by circumstances, and sustained for at least 2 weeks"

* # Original:
gen d_b1_org = 0
replace d_b1_org = 1 if q4220a==1 | q4220b==1 | q4220c==1 | q4220d==1
replace d_b1_org = . if q4220a==. & q4220b==. & q4220c==. & q4220d==.

* # Revision:
gen d_b1 = .
replace d_b1 = 0 if (q4220a==0 | q4220b==0) & (q4220c==0 | q4220d==0)
replace d_b1 = 1 if q4220a==1 | q4220b==1 | q4220c==1 | q4220d==1

tab d_b1_org d_b1, missing
```

Se obtienen los mismos resultados.

#### Criterio B2

```{stata criterion-b2}
*B2: "Loss of interest or pleasure in activities that are normally pleasurable"

* # Original:
gen d_b2_org = 0
replace d_b2_org = 1 if q4220e==1 | q4220f==1
replace d_b2_org = . if q4220e==. & q4220f==.

* # Revision:
gen d_b2 = .
replace d_b2 = 0 if q4220e==0 & q4220f==0
replace d_b2 = 1 if q4220e==1 | q4220f==1

tab d_b2_org d_b2, missing
```

3 valores "missing" se codifican como "0" en el algoritmo original.

#### Criterio B3

```{stata criterion-b3}
*B3: "Decreased energy or increased fatigability"

* # Original:
gen d_b3_org = 0
replace d_b3_org = 1 if q4222e==1
replace d_b3_org = . if q4222e==.

* # Revision:
gen d_b3 = .
replace d_b3 = 0 if q4222e==0
replace d_b3 = 1 if q4222e==1

tab d_b3_org d_b3, missing
```

Se obtienen los mismos resultados.

#### Cálculo final del criterio B

Se debe usar la opción `, missing` en `rowtotal`, para evitar que la suma de
síntomas se calcule como "0" cuando todos los valores son "missing".
También, es crucial saber si hay demasiados "missing" como para prevenir que se
puede calcular si el total de síntomas B es dos o más (se cumple el criterio B)
o menos de 2 (no se cumple el criterio B).
Ejemplos:

- B1 = .; B2 = 1; B3 = 1: Se cumple el criterio B.
- B1 = 0; B2 = .; B3 = 0: No se cumple el criterio B.
- B1 = 1; B2 = .; B3 = 0: Criterio B indeterminado ("missing").
- B1 = 0; B2 = .; B3 = .: Criterio B indeterminado ("missing").

En general, la suma "1"s y "missing"s debe ser inferior a 2 para determinar que
NO se cumple el criterio B.

```{stata criterion-b}
*Sum of depressive screening symptoms

* # Original:

egen d_screentotal_org = rowtotal (d_b1_org d_b2_org d_b3_org)

gen d_crit_b_org = .
replace d_crit_b_org = 0 if d_screentotal_org<2
replace d_crit_b_org = 1 if d_screentotal_org>=2 


* # Revision:

egen d_screentotal = rowtotal (d_b1 d_b2 d_b3), missing
egen d_screenmissing = rowmiss (d_b1 d_b2 d_b3)

gen d_crit_b = .
replace d_crit_b = 0 if (d_screentotal + d_screenmissing) < 2
replace d_crit_b = 1 if d_screentotal >= 2 & d_screentotal != .


tab d_screentotal_org d_screentotal, missing
tab d_crit_b_org d_crit_b, missing
```

En ambos casos, tanto en la variable auxiliar como en el criterio B, los
"missing" siempre se codifican como "0" en el algoritmo original.

### Criterio C

El criterio que se ha de cumplir para un diagnóstico _positivo_ de depresión,
además del criterio B, es que la suma de síntomas B y C sea al menos de 4.
<!-- # TODO: 3 ó 4? -->

#### Criterio C1

Cuando **ALGUNA** de las respuestas es _positiva_, el síntoma es _positivo_.
Cuando la respuesta a `q4222l` es _negativa_, el ítem `q4222m` se salta,
por lo que solamente los dos primeros han de ser tenidos en cuenta para un
síntoma _negativo_.

```{stata criterion-c1}
*C criterion: "An additional symptom or symptoms from the following list should be present, to give a total of at least four"

*C1: "Loss of confidence and self-esteem"

* # Original:
gen d_c1_org = 0
replace d_c1_org = 1 if q4222k==1 | q4222l==1 | q4222m==1
replace d_c1_org = . if q4222k==. & q4222l==. & q4222m==.

* # Revision:
gen d_c1 = .
replace d_c1 = 0 if q4222k==0 & q4222l==0
replace d_c1 = 1 if q4222k==1 | q4222l==1 | q4222m==1

tab d_c1_org d_c1, missing
```

1 "missing" es "0" en el algoritmo original.

#### Criterio C2

```{stata criterion-c2}
*C2: "Unreasonable feelings of self-reproach or excessive and inappropriate guilt"

* # Original:
gen d_c2_org = 0
replace d_c2_org = 1 if q4222n==1
replace d_c2_org = . if q4222n==.

* # Revision:
gen d_c2 = .
replace d_c2 = 0 if q4222n==0
replace d_c2 = 1 if q4222n==1

tab d_c2_org d_c2, missing
```

Se obtienen los mismos resultados.

#### Criterio C3

Cuando **ALGUNA** de las respuestas es _positiva_ el síntoma es _positivo_.
Cuando `q4222t` es _negativa_, los dos últimos ítems se saltan por lo que sólo
las tres primeras respuestas se deben tener en cuenta para diagnosticar un
síntoma _negativo_.

(**NOTA**: ¿Es posible que convenga considerar que _SÓLO_ `q4222r`,
y no `q4222s`, indica pensamientos de muerte **recurrentes**?)

```{stata criterion-c3}
*C3: "Recurrent thoughts of death or suicide, or any suicidal behaviour"

* # Original:
gen d_c3_org = 0
replace d_c3_org = 1 if q4222r==1 | q4222s==1 | q4222t==1 | q4222u==1 | q4222v==1
replace d_c3_org = 1 if q4222r==. & q4222s==. & q4222t==. & q4222u==. & q4222v==.

* # Revision:
gen d_c3 = .
replace d_c3 = 0 if q4222r==0 & q4222s==0 & q4222t==0
replace d_c3 = 1 if q4222r==1 | q4222s==1 | q4222t==1 | q4222u==1 | q4222v==1

tab d_c3_org d_c3, missing
```

De los valores "missing", 2 son "0" y 2730 "1" en el algoritmo original.

#### Criterio C4

```{stata criterion-c4}
*C4: "Complaints or evidence of diminished ability to think or concentrate, such as indecisiveness or vacillation"

* # Original:
gen d_c4_org = 0
replace d_c4_org = 1 if q4222h==1 | q4222i==1 | q4222j==1
replace d_c4_org = . if q4222h==. & q4222i==. & q4222j==.

* # Revision:
gen d_c4 = .
replace d_c4 = 0 if q4222h==0 & q4222i==0 & q4222j==0
replace d_c4 = 1 if q4222h==1 | q4222i==1 | q4222j==1

tab d_c4_org d_c4, missing
```

2 "missing" son "0" en el algoritmo original.

#### Criterio C5

```{stata criterion-c5}
*C5: "Change in psychomotor activity, with agitation or retardation (either subjective or objective)"

* # Original:
gen d_c5_org = 0
replace d_c5_org = 1 if (q4222f==1 | q4222fa==1) | (q4222g==1 | q4222ga==1)
replace d_c5_org = . if q4222f==. & q4222fa==. & q4222g==. & q4222ga==.

* # Revision:
gen d_c5 = .
replace d_c5 = 0 if q4222f==0 & q4222fa==0 & q4222g==0 & q4222ga==0
replace d_c5 = 1 if (q4222f==1 | q4222fa==1) | (q4222g==1 | q4222ga==1)

tab d_c5_org d_c5, missing
```

3 "missing" son "0" en el algoritmo original.

#### Criterio C6

```{stata criterion-c6}
*C6: "Sleep disturbance of any type"

* # Original:
gen d_c6_org = 0
replace d_c6_org = 1 if q4222c==1 | q4222d==1
replace d_c6_org = . if q4222c==. & q4222d==.

* # Revision:
gen d_c6 = .
replace d_c6 = 0 if q4222c==0 & q4222d==0
replace d_c6 = 1 if q4222c==1 | q4222d==1

tab d_c6_org d_c6, missing
```

1 "missing" es "0" en el algoritmo original.

#### Criterio C7

```{stata criterion-c7}
*C7: "Change in appetite (decrease or increase) with corresponding weight change"

* # Original:
gen d_c7_org = 0
replace d_c7_org = 1 if q4222a==1 & q4222b==1
replace d_c7_org = . if q4222a==. & q4222b==.

* # Revision:
gen d_c7 = .
replace d_c7 = 0 if q4222a==0 | q4222b==0
replace d_c7 = 1 if q4222a==1 & q4222b==1

tab d_c7_org d_c7, missing
```

5 "missing" son "0" en el algoritmo original.

#### Cálculo final del criterio C

Se debe usar la opción `, missing` en `rowtotal`, para evitar que la suma de
síntomas se calcule como "0" cuando todos los valores son "missing".
También, es crucial saber si hay demasiados "missing" como para prevenir que se
puede calcular si el total de síntomas B+C es mayor o menor que un determinado
umbral.

```{stata criterion-symtotal}
*Sum of depressive symptoms

* # Original:
egen d_symtotal_org = rowtotal (d_c1_org d_c2_org d_c3_org d_c4_org d_c5_org d_c6_org d_c7_org)

* # Revision:
egen d_symtotal = rowtotal (d_c1 d_c2 d_c3 d_c4 d_c5 d_c6 d_c7), missing
egen d_symmissing = rowmiss (d_c1 d_c2 d_c3 d_c4 d_c5 d_c6 d_c7)

tab d_symtotal_org d_symtotal, missing
```

De los valores "missing", 1 es "0" y 2730 son "1" en el algoritmo original.

### Salto del módulo de depresión al completo

Este criterio está definido originalmente como _todas las respuestas en el
módulo de 'lifetime depression' son "missing"_.
Sin embargo, este criterio no es estrictamente confiable, dado que
_no es imposible_ que alguien que **NO** haya saltado el módulo decida omitir
("DK" o "Refuse") _todas las respuestas_.
El único punto de entrada al módulo de 'lifetime depression' es el ítem `q4218`
(no hay filtros que redirijan a ningún ítem `q4220*` ó `q4222*`), por lo que la
respuesta a este ítem debe ser _positiva_ para entrar en el módulo;
en otro caso, el módulo es saltado.
Además, el módulo completo de depresión se puede saltar si alguno de los
siguientes ítems tiene una respuesta que sea distinta de _positiva_:
`q4202`, `q4211`, `q4213`, o el filtro F4217
(lo cual implica `q4214==1` Y `q4215==4` Y `q4216==4`,
por lo que se asigna el valor de verdad de este filtro,
pero sólo cuando `q4214` no es "missing", ya que ello implica que
`q4215` y `q4216` tampoco lo son).

(**NOTA**: Se ha considera que el modulo ha sido "saltado" por los participantes
a los que se ha preguntado por al menos alguno de estos ítems.
Si no lo han sido, su valor será "missing", i.e. se han saltado la sección CIDI
al completo, e.g. por ser entrevistas "proxy".)

```{stata skipped}
*Has the person skipped the lifetime depression module?

* # Original:
egen serie_symp_org = rowtotal (q4220a-q4222v)
gen d_skipped_org = 0
* # En la siguiente línea creo `q4202_alt` para evitar sobreescribir el valor de
* # `q4202` que voy a necesitar luego para calcular la versión revisada
gen q4202_alt = q4202
recode q4202_alt q4211 q4213 q4218 (1=1) (.=.) (else=0)
replace d_skipped_org = 1 if q4202_alt==0 | q4211==0 | q4213==0 | (q4214==1 & q4215==4 & q4216==4) | q4218==0 | (q4220a!=1 & q4220b!=1 & q4220c!=1 & q4220d!=1 & q4220e!=1 & q4220f!=1) | serie_symp<2

* # Revision:
gen d_skipped = .
replace d_skipped =                q4202!=1 if q4202!=.
replace d_skipped = d_skipped==1 | q4211!=1 if q4211!=.
replace d_skipped = d_skipped==1 | q4213!=1 if q4213!=.
replace d_skipped = d_skipped==1 | q4218!=1 if q4218!=.
replace d_skipped = d_skipped==1 | (q4214==1 & q4215==4 & q4216==4) if q4214!=.

tab d_skipped_org d_skipped, missing
```

7 valores "0" y 91 "missing" se codifican como "1" en el algoritmo original.
La diferencia se debe a las dos últimas condiciones del algoritmo original
(` | (q4220a!=1 & q4220b!=1 & q4220c!=1 & q4220d!=1 & q4220e!=1 & q4220f!=1) | serie_symp<2`)
pero, de acuerdo a mi criterio, no deberían utilizarse para considerar si se ha
saltado o no el módulo de depresión.

(**NOTA**: En la versión revisada no se usa nunca esta variable para
el diagnóstico de "lifetime depression". En su lugar, se usa la variable
`d_screening`, ver apartado
[Diagnóstico final de depresión](#Diagnóstico_final_de_depresión).)

### Severidad

En esta versión incluimos la posibilidad de que haya demasiados "missing" como
para no poder calcular si la suma total de síntomas (criterios B + C) es igual
o mayor que 4, o menor que 4. Esto ocurrirá si la suma total de síntomas
positivos y "missing" es igual o mayor que 4 (y no se cumple ningún otro
criterio). Por tanto, se calcula el número de "missing"s, y después se obtiene
la severidad, en orden creciente, de forma que cada nivel de severidad
sobreescribe el anterior.  
(**NOTA**: Este criterio tiene la desventaja de no considerar cuándo hay
demasiados "missing" para "desambiguar" entre niveles de severidad, lo cual
implica que siempre se va a asignar el menor nivel de severidad "no ambiguo",
cuando en ese caso debería asignar un "missing". No obstante,
de cara a la variable de diagnóstico tiene la ventaja de que se pueden
recodificar directamente todos los casos con nivel de severidad distinto de "0"
como casos _positivos_ en depresión.)

Para calcular el nivel final de severidad, este criterio inicial de severidad es
combinado con el criterio B (para obtener los niveles de severidad "leve"
o "moderado"), o con el criterio de tener los tres síntomas B (para el nivel
"grave"). Estos son calculados también en orden creciente, de forma que cada
nivel sobreescribe al anterior.

```{stata severity}
***Severity of depressive episode

*To meet the criteria for "Mild depressive episode (F32.0)", at least two of the screening symptoms (B1-B3) must be present, and an additional symptom or symptoms from the rest of them (B1-C7) should be present to give a total of at least four. In the case of "Moderate depressive episode (F32.1)", there must be at least two screening symptoms and a total of at least six symptoms. In the case of "Severe depressive episode (F32.3)", the three screening symptoms (B1-B3) must be present, and additional symptoms (C1-C7) should be present to give a total of at least eight.

* # Original:
gen d_lifetime_severity_org = .
replace d_lifetime_severity_org = 0 if d_crit_a_org==0 | d_crit_b_org==0 | (d_screentotal_org==2 & d_symtotal_org<2) | (d_screentotal_org==3 & d_symtotal_org==0)
replace d_lifetime_severity_org = 1 if d_crit_a_org==1 & ((d_screentotal_org==2 & d_symtotal_org>=2) | (d_screentotal==3 & d_symtotal_org>=1))
replace d_lifetime_severity_org = 2 if d_crit_a_org==1 & ((d_screentotal_org==2 & d_symtotal_org>=4) | (d_screentotal==3 & d_symtotal_org>=3))
replace d_lifetime_severity_org = 3 if d_crit_a_org==1 & (d_screentotal_org==3 & d_symtotal_org>=5)

* # Revision:

gen d_lifetime_severity_total = .
replace d_lifetime_severity_total = 0 if d_screentotal + d_screenmissing + d_symtotal + d_symmissing < 4
replace d_lifetime_severity_total = 1 if d_screentotal + d_symtotal >= 4 & d_screentotal!=. & d_symtotal !=.
replace d_lifetime_severity_total = 2 if d_screentotal + d_symtotal >= 6 & d_screentotal!=. & d_symtotal !=.
replace d_lifetime_severity_total = 3 if d_screentotal + d_symtotal >= 8 & d_screentotal!=. & d_symtotal !=.

gen d_lifetime_severity = .
replace d_lifetime_severity = 0 if d_crit_a==0 | d_crit_b==0 | d_lifetime_severity_total==0
replace d_lifetime_severity = 1 if d_crit_a==1 & d_crit_b==1 & d_lifetime_severity_total>=1 & d_lifetime_severity_total!=.
replace d_lifetime_severity = 2 if d_crit_a==1 & d_crit_b==1 & d_lifetime_severity_total>=2 & d_lifetime_severity_total!=.
replace d_lifetime_severity = 3 if d_crit_a==1 & d_screentotal==3 & d_lifetime_severity_total==3


tab d_lifetime_severity_org d_lifetime_severity, missing
```

2569 valores "missing" tienen diferentes valores de severidad
en el algoritmo original, habiendo 24 que tienen al menos valor "1".
Además, hay casos con valores distintos de "0" en en la versión original que
dan "0" en la revisada.

### Diagnóstico final de depresión

Este código primero asigna "0" a los participantes que dieron tres respuestas
negativas al test de screening inicial (ítems `q4200`-`q4202`).
Nótese que cuando se dan respuestas de "DK" / "Refuse", aunque el resto
del módulo CIDI se omite, un diagnóstico positivo de depresión no se puede
descartar, dado que no se sabe si el screening es _positivo_ (al menos
una respuesta es _positiva_) y por tanto si se necesitaría el resto del módulo
para evaluar depresión o no.

```{stata liferime-depression}
* # Original:
gen depression_lifetime_org = .
replace depression_lifetime_org = 1 if d_lifetime_severity_org >=1 
replace depression_lifetime_org = 0 if proxy==2 & (d_lifetime_severity_org ==0 | d_skipped_org==1)

* # Revision:

gen d_screening=.
replace d_screening=0 if q4200==2 & q4201==2 & q4202==2
replace d_screening=1 if q4200==1 | q4201==1 | q4202==1

***Lifetime depression diagnostic
gen depression_lifetime = .
replace depression_lifetime = 0 if d_lifetime_severity ==0 | d_screening==0
replace depression_lifetime = 1 if d_lifetime_severity >=1 & d_lifetime_severity !=. & d_screening==1
* # ALTERNATIVE: Use `d_skipped` to determine whether the value of
* # `d_lifetime_severity` is valir or not, and then fill in with the ones that
* # have a value of "0" in the screening (but this gives MORE missing).
* replace depression_lifetime = d_lifetime_severity >=1 if d_lifetime_severity !=. & d_skipped==0
* replace depression_lifetime=0 if d_screening==0

tab depression_lifetime_org depression_lifetime, missing
```

92 respuestas "missing" son originalmente "0" y 46 son "1".
14 "0" son "1" en la versión original.
Las discrepancias se deben, principalmente,
a los criterios G3 y C3, aparte de los valores perdidos.
Sería necesario modificar G3 para tener en cuenta los valores perdidos en
`q4224a` y `q4224b` cuando no se alcanzan esos ítems,
y codificar los "missing" en C3 como "missing" o "0" en lugar de "1".

## Sintaxis completa (Stata 13)

```{stata reload-data, include=FALSE}
use "~\UAM\marta.miret@uam.es - Bases de datos maestras Edad con Salud\Ola_3\Cohorte_2019\rawdata_c2019w1.dta", clear
```

### Revisión

```{stata review, results='hide'}
*Previous recoding of the symptomatology questions
recode q4220a-q4222v (1=1) (2=0) (888 999=.) 

***Depressive episode (F32)

*G1 criterion: "The depressive episode should last for at least 2 weeks"
gen d_crit_g1 = .
replace d_crit_g1 = 0 if q4211==2 | q4213==2
replace d_crit_g1 = 1 if q4211==1 | q4213==1
label variable d_crit_g1 "G1 criterion for depression"

*G2 criterion: "There have been no hypomanic or manic symptoms sufficient to meet the criteria for hypomanic or manic episode at any time in the individual's life"
gen d_crit_g2 = .
replace d_crit_g2 = 0 if q4233==1 | q4225_causemed2==1
replace d_crit_g2 = 1 if q4233==2 & q4225_causemed2!=1 & q4224==1
label variable d_crit_g2 "G2 criterion for depression"

*G3 criterion: "The episode is not attributable to psychoactive substance use or to any organic mental disorder"
gen d_crit_g3 = .
replace d_crit_g3 = 0 if (q4224a==1 & q4224b==1) | (q4224==1 & (q4225_causemed12==1 | q4225_causemed23==1 | q4225_causemed24==1 | q4225_causemed25==1 | q4225_causemed30==1 | q4225_causemed31==1))
replace d_crit_g3 = 1 if (q4224a==2 | q4224b==2) | (q4224==1 & (q4225_causemed12!=1 & q4225_causemed23!=1 & q4225_causemed24!=1 & q4225_causemed25!=1 & q4225_causemed30!=1 & q4225_causemed31!=1))
label variable d_crit_g3 "G3 criterion for depression" 

***Mild depressive episode (F32.0)

*A criterion: "The general criteria for depressive episode must be met"
gen d_crit_a = .
replace d_crit_a = 0 if d_crit_g1==0 | d_crit_g2==0 | d_crit_g3==0
replace d_crit_a = 1 if d_crit_g1==1 & d_crit_g2==1 & d_crit_g3==1
label variable d_crit_a "A criterion for depression"

*B criterion: "At least two of the following three symptoms must be present"

*B1: "Depressed mood to a degree that is definitely abnormal for the individual, present for most of the day and almost every day, largely uninfluenced by circumstances, and sustained for at least 2 weeks"
gen d_b1 = .
replace d_b1 = 0 if (q4220a==0 | q4220b==0) & (q4220c==0 | q4220d==0)
replace d_b1 = 1 if q4220a==1 | q4220b==1 | q4220c==1 | q4220d==1
label variable d_b1 "B1 criterion for depression"

*B2: "Loss of interest or pleasure in activities that are normally pleasurable"
gen d_b2 = .
replace d_b2 = 0 if q4220e==0 & q4220f==0
replace d_b2 = 1 if q4220e==1 | q4220f==1
label variable d_b2 "B2 criterion for depression"

*B3: "Decreased energy or increased fatigability"
gen d_b3 = .
replace d_b3 = 0 if q4222e==0
replace d_b3 = 1 if q4222e==1
label variable d_b3 "B3 criterion for depression"

*Sum of depressive screening symptoms
egen d_screentotal = rowtotal (d_b1-d_b3), missing
egen d_screenmissing = rowmiss (d_b1-d_b3)
label variable d_screentotal "Screening symptoms for depression"

*To fulfil B criterion >=2 screening symptoms should be present.
gen d_crit_b = .
replace d_crit_b = 0 if (d_screentotal + d_screenmissing) < 2
replace d_crit_b = 1 if d_screentotal >= 2 & d_screentotal != .
label variable d_crit_b "B criterion for depression"

*C criterion: "An additional symptom or symptoms from the following list should be present, to give a total of at least four"

*C1: "Loss of confidence and self-esteem"
gen d_c1 = .
replace d_c1 = 0 if q4222k==0 & q4222l==0
replace d_c1 = 1 if q4222k==1 | q4222l==1 | q4222m==1
label variable d_c1 "C1 criterion for depression"

*C2: "Unreasonable feelings of self-reproach or excessive and inappropriate guilt"
gen d_c2 = .
replace d_c2 = 0 if q4222n==0
replace d_c2 = 1 if q4222n==1
label variable d_c2 "C2 criterion for depression"

*C3: "Recurrent thoughts of death or suicide, or any suicidal behaviour"
gen d_c3 = .
replace d_c3 = 0 if q4222r==0 & q4222s==0 & q4222t==0
replace d_c3 = 1 if q4222r==1 | q4222s==1 | q4222t==1 | q4222u==1 | q4222v==1
label variable d_c3 "C3 criterion for depression"

*C4: "Complaints or evidence of diminished ability to think or concentrate, such as indecisiveness or vacillation"
gen d_c4 = .
replace d_c4 = 0 if q4222h==0 & q4222i==0 & q4222j==0
replace d_c4 = 1 if q4222h==1 | q4222i==1 | q4222j==1
label variable d_c4 "C4 criterion for depression"

*C5: "Change in psychomotor activity, with agitation or retardation (either subjective or objective)"
gen d_c5 = .
replace d_c5 = 0 if q4222f==0 & q4222fa==0 & q4222g==0 & q4222ga==0
replace d_c5 = 1 if (q4222f==1 | q4222fa==1) | (q4222g==1 | q4222ga==1)
label variable d_c5 "C5 criterion for depression"

*C6: "Sleep disturbance of any type"
gen d_c6 = .
replace d_c6 = 0 if q4222c==0 & q4222d==0
replace d_c6 = 1 if q4222c==1 | q4222d==1
label variable d_c6 "C6 criterion for depression"

*C7: "Change in appetite (decrease or increase) with corresponding weight change"
gen d_c7 = .
replace d_c7 = 0 if q4222a==0 | q4222b==0
replace d_c7 = 1 if q4222a==1 & q4222b==1
label variable d_c7 "C7 criterion for depression"

*Sum of depressive symptoms
egen d_symtotal = rowtotal (d_c1-d_c7), missing
egen d_symmissing = rowmiss (d_c1-d_c7)
label variable d_symtotal "Symptoms for depression"
label variable d_symmissing "Missing symptoms for depression"

*`d_skipped` is not used so its computations are skipped

***Severity of depressive episode

*To meet the criteria for "Mild depressive episode (F32.0)", at least two of the screening symptoms (B1-B3) must be present, and an additional symptom or symptoms from the rest of them (B1-C7) should be present to give a total of at least four. In the case of "Moderate depressive episode (F32.1)", there must be at least two screening symptoms and a total of at least six symptoms. In the case of "Severe depressive episode (F32.3)", the three screening symptoms (B1-B3) must be present, and additional symptoms (C1-C7) should be present to give a total of at least eight.
gen d_lifetime_severity_total = .
replace d_lifetime_severity_total = 0 if d_screentotal + d_screenmissing + d_symtotal + d_symmissing < 4
replace d_lifetime_severity_total = 1 if d_screentotal + d_symtotal >= 4 & d_screentotal!=. & d_symtotal !=.
replace d_lifetime_severity_total = 2 if d_screentotal + d_symtotal >= 6 & d_screentotal!=. & d_symtotal !=.
replace d_lifetime_severity_total = 3 if d_screentotal + d_symtotal >= 8 & d_screentotal!=. & d_symtotal !=.

gen d_lifetime_severity = .
replace d_lifetime_severity = 0 if d_crit_a==0 | d_crit_b==0 | d_lifetime_severity_total==0
replace d_lifetime_severity = 1 if d_crit_a==1 & d_crit_b==1 & d_lifetime_severity_total>=1 & d_lifetime_severity_total!=.
replace d_lifetime_severity = 2 if d_crit_a==1 & d_crit_b==1 & d_lifetime_severity_total>=2 & d_lifetime_severity_total!=.
replace d_lifetime_severity = 3 if d_crit_a==1 & d_screentotal==3 & d_lifetime_severity_total==3
label variable d_lifetime_severity "Severity of lifetime depressive episode (c2019_w1)"
label define severity 0 "No" 1 "Mild" 2 "Moderate" 3 "Severe"
label values d_lifetime_severity severity

*CIDI initial screening test
gen d_screening=.
replace d_screening=0 if q4200==2 & q4201==2 & q4202==2
replace d_screening=1 if q4200==1 | q4201==1 | q4202==1

***Lifetime depression diagnostic
gen depression_lifetime = .
replace depression_lifetime = 0 if d_lifetime_severity ==0 | d_screening==0
replace depression_lifetime = 1 if d_lifetime_severity >=1 & d_lifetime_severity !=. & d_screening==1
label define yesno 0 "No" 1 "Yes"
label values depression_lifetime yesno
label variable depression_lifetime "Lifetime depression (c2019_w1)"
```

### Algoritmo original

```{stata original, results='hide'}
*Previous recoding of the symptomatology questions
* recode q4220a-q4222v (1=1) (2=0) (888 999=.) 

***Depressive episode (F32)

*G1 criterion: "The depressive episode should last for at least 2 weeks"
gen d_crit_g1_org = .
replace d_crit_g1_org = 0 if q4211!=1 & q4213!=1
replace d_crit_g1_org = 1 if q4211==1 | q4213==1

*G2 criterion: "There have been no hypomanic or manic symptoms sufficient to meet the criteria for hypomanic or manic episode at any time in the individual's life"
gen d_crit_g2_org = 1
replace d_crit_g2_org = 0 if q4233==1 | q4225_causemed2==1

*G3 criterion: "The episode is not attributable to psychoactive substance use or to any organic mental disorder"
gen d_crit_g3_org = 0
replace d_crit_g3_org = 1 if (q4224a!=1 | q4224b!=1) | (q4224==1 & q4225_causemed12!=1 & q4225_causemed23!=1 & q4225_causemed24!=1 & q4225_causemed25!=1 & q4225_causemed30!=1 & q4225_causemed31!=1)
replace d_crit_g3_org = . if q4224a==. & q4224b==. & q4224==. & q4225_causemed12==. & q4225_causemed23==. & q4225_causemed24==. & q4225_causemed25==. & q4225_causemed30==. & q4225_causemed31==.

***Mild depressive episode (F32.0)

*A criterion: "The general criteria for depressive episode must be met"
gen d_crit_a_org = .
replace d_crit_a_org = 0 if d_crit_g1_org!=1 | d_crit_g2_org!=1 | d_crit_g3_org!=1
replace d_crit_a_org = 1 if d_crit_g1_org==1 & d_crit_g2_org==1 & d_crit_g3_org==1

*B criterion: "At least two of the following three symptoms must be present"

*B1: "Depressed mood to a degree that is definitely abnormal for the individual, present for most of the day and almost every day, largely uninfluenced by circumstances, and sustained for at least 2 weeks"
gen d_b1_org = 0
replace d_b1_org = 1 if q4220a==1 | q4220b==1 | q4220c==1 | q4220d==1
replace d_b1_org = . if q4220a==. & q4220b==. & q4220c==. & q4220d==.

*B2: "Loss of interest or pleasure in activities that are normally pleasurable"
gen d_b2_org = 0
replace d_b2_org = 1 if q4220e==1 | q4220f==1
replace d_b2_org = . if q4220e==. & q4220f==.

*B3: "Decreased energy or increased fatigability"
gen d_b3_org = 0
replace d_b3_org = 1 if q4222e==1
replace d_b3_org = . if q4222e==.

*Sum of depressive screening symptoms
egen d_screentotal_org = rowtotal (d_b1_org-d_b3_org)

*To fulfil B criterion >=2 screening symptoms should be present.
gen d_crit_b_org = .
replace d_crit_b_org = 0 if d_screentotal_org<2
replace d_crit_b_org = 1 if d_screentotal_org>=2 

*C criterion: "An additional symptom or symptoms from the following list should be present, to give a total of at least four"

*C1: "Loss of confidence and self-esteem"
gen d_c1_org = 0
replace d_c1_org = 1 if q4222k==1 | q4222l==1 | q4222m==1
replace d_c1_org = . if q4222k==. & q4222l==. & q4222m==.

*C2: "Unreasonable feelings of self-reproach or excessive and inappropriate guilt"
gen d_c2_org = 0
replace d_c2_org = 1 if q4222n==1
replace d_c2_org = . if q4222n==.

*C3: "Recurrent thoughts of death or suicide, or any suicidal behaviour"
gen d_c3_org = 0
replace d_c3_org = 1 if q4222r==1 | q4222s==1 | q4222t==1 | q4222u==1 | q4222v==1
replace d_c3_org = 1 if q4222r==. & q4222s==. & q4222t==. & q4222u==. & q4222v==.

*C4: "Complaints or evidence of diminished ability to think or concentrate, such as indecisiveness or vacillation"
gen d_c4_org = 0
replace d_c4_org = 1 if q4222h==1 | q4222i==1 | q4222j==1
replace d_c4_org = . if q4222h==. & q4222i==. & q4222j==.

*C5: "Change in psychomotor activity, with agitation or retardation (either subjective or objective)"
gen d_c5_org = 0
replace d_c5_org = 1 if (q4222f==1 | q4222fa==1) | (q4222g==1 | q4222ga==1)
replace d_c5_org = . if q4222f==. & q4222fa==. & q4222g==. & q4222ga==.

*C6: "Sleep disturbance of any type"
gen d_c6_org = 0
replace d_c6_org = 1 if q4222c==1 | q4222d==1
replace d_c6_org = . if q4222c==. & q4222d==.

*C7: "Change in appetite (decrease or increase) with corresponding weight change"
gen d_c7_org = 0
replace d_c7_org = 1 if q4222a==1 & q4222b==1
replace d_c7_org = . if q4222a==. & q4222b==.

*Sum of depressive symptoms
egen d_symtotal_org = rowtotal (d_c1_org-d_c7_org)

*Have the person skipped the lifetime depression module?
egen serie_symp_org = rowtotal (q4220a-q4222v)
gen d_skipped_org = 0
recode q4202 q4211 q4213 q4218 (1=1) (.=.) (else=0)
replace d_skipped_org = 1 if q4202==0 | q4211==0 | q4213==0 | (q4214==1 & q4215==4 & q4216==4) | q4218==0 | (q4220a!=1 & q4220b!=1 & q4220c!=1 & q4220d!=1 & q4220e!=1 & q4220f!=1) | serie_symp<2

***Severity of depressive episode

*To meet the criteria for "Mild depressive episode (F32.0)", at least two of the screening symptoms (B1-B3) must be present, and an additional symptom or symptoms from the rest of them (B1-C7) should be present to give a total of at least four. In the case of "Moderate depressive episode (F32.1)", there must be at least two screening symptoms and a total of at least six symptoms. In the case of "Severe depressive episode (F32.3)", the three screening symptoms (B1-B3) must be present, and additional symptoms (C1-C7) should be present to give a total of at least eight.
gen d_lifetime_severity_org = .
replace d_lifetime_severity_org = 0 if d_crit_a_org==0 | d_crit_b_org==0 | (d_screentotal_org==2 & d_symtotal_org<2) | (d_screentotal_org==3 & d_symtotal_org==0)
replace d_lifetime_severity_org = 1 if d_crit_a_org==1 & ((d_screentotal_org==2 & d_symtotal_org>=2) | (d_screentotal==3 & d_symtotal_org>=1))
replace d_lifetime_severity_org = 2 if d_crit_a_org==1 & ((d_screentotal_org==2 & d_symtotal_org>=4) | (d_screentotal==3 & d_symtotal_org>=3))
replace d_lifetime_severity_org = 3 if d_crit_a_org==1 & (d_screentotal_org==3 & d_symtotal_org>=5)
*label define severity 0 "No" 1 "Mild" 2 "Moderate" 3 "Severe"
label values d_lifetime_severity severity

***Lifetime depression prevalence
gen depression_lifetime_org = .
replace depression_lifetime_org = 1 if d_lifetime_severity_org >=1 
replace depression_lifetime_org = 0 if proxy==2 & (d_lifetime_severity_org ==0 | d_skipped_org==1)
*label define yesno 0 "No" 1 "Yes"
label values depression_lifetime_org yesno
```

# Comparación de resultados

## Contingencia entre los resultados de ambos algoritmos

```{stata contingency-final}
tab depression_lifetime_org depression_lifetime, missing
```

(Mismo resultado que en la versión desglosada)

## Frecuencias absolutas y relativas en la muestra de ambas variables

```{stata freqs}
tab depression_lifetime_org
tab depression_lifetime
```


# Session info

```{r session-info, results='markup'}
devtools::session_info()
```
